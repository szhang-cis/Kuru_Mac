      SUBROUTINE PLINVA(NCRIT,PROPS,NTYPE,SGTOT,STRES,ANGFI,CAPAP,
     .                  PMEAN,VARJ2,VARJ3,DEVIA,STEFF,SMAXS,THETA,
     .                  DFTEQ,DFAFI,YIELD,NSTR1,ALFAT,BETAT,
     .                  GAMAT,RETEN,A1COE,A2COE,A3COE,BACKS,
     .                  CCERO,POROS,IPORO,CPOE1,CPOE2,CLANK,
     .                  CLAN1,CLAN2,CLAN3,CLAN4,CLAN5,CLAN6,
     .                  LARGE,IFREN,IKINE,NDIME,XJACM,XJA3M,DETJM,
     .                  TRMTX)
C***********************************************************************
C
C**** THIS SUBROUTINE EVALUATES THE STRESS INVARIANTS AND THE CURRENT
C     VALUE OF THE YIELD FUNCTION
C
C     ( Old form: DATA ROOT3,PIVAL/1.732050807568877,3.141592654/ )
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      DIMENSION SGTOT(*), PROPS(*), DEVIA(6), BACKS(*), DBACK(6),
     .          STRES(*), XJACM(NDIME,*)
      DIMENSION TRMTX(NDIME,*), STREX(6)
C
      ROOT3=1.732050807568877D0
      PIVAL=3.141592654D0
C
      IF(LARGE.EQ.0) THEN
       DO ISTR1=1,NSTR1
        STRES(ISTR1)=SGTOT(ISTR1)
       ENDDO
      ELSE
C
C**** TRANSFORMS TOTAL STRESSES
C
       IF(IFREN.EQ.1.OR.IFREN.EQ.4) THEN                     ! no change
        DETJX=1.0D0
        CALL PUFOBA(SGTOT,STRES,XJACM,XJA3M,DETJX,    3)
       ENDIF
       IF(IFREN.EQ.2.OR.IFREN.EQ.5.OR.IFREN.EQ.7.OR.         ! Kirchhoff
     .    IFREN.EQ.8.OR.IFREN.EQ.9.OR.IFREN.EQ.10) THEN
        DETJX=1.0D0
        CALL PUFOBA(SGTOT,STRES,XJACM,XJA3M,DETJX,    1)
       ENDIF
       IF(IFREN.EQ.3.OR.IFREN.EQ.6) THEN                     ! Cauchy
        DETJX=DETJM
        CALL PUFOBA(SGTOT,STRES,XJACM,XJA3M,DETJX,    1)
       ENDIF
      ENDIF        ! large.eq.0
C
C**** ROTATES TOTAL STRESSES FROM GLOBAL TO LOCAL (MATERIAL) SYSTEM OF
C     COORDINATES (THIS IS NEEDED FOR ORTHOTROPIC MATERIALS)
C
      ISOTR=INT(PROPS(1))   ! 0=isotropic; 1=orthotropic
      IF(ISOTR.NE.0) THEN
       DO ISTR1=1,NSTR1
        STREX(ISTR1)=STRES(ISTR1)
       ENDDO
       IF(IFREN.EQ.2.OR.IFREN.EQ.3) THEN
        XJA3M=1.0D0
        DETJX=1.0D0
        CALL PUFOBA(STREX,STRES,TRMTX,XJA3M,DETJX,    1)
       ELSE
        CALL RUNEND('ERROR: IFREN NE 2 OR 3 NOT IMPLEMENTED')
       ENDIF
      ENDIF
C
C**** DEVIATORIC STRESS
C
      IF(NTYPE.EQ.5) THEN                         ! 1D case
       PMEAN=1.0D0/3.0D0*STRES(1)
       DEVIA(1)=2.0D0/3.0D0*STRES(1)
       DEVIA(2)=-1.0D0/3.0D0*STRES(1)
       DEVIA(3)=0.0D0
       DEVIA(4)=-1.0D0/3.0D0*STRES(1)
      ELSE                                        ! 2&3D cases
       PMEAN=(STRES(1)+STRES(2)+STRES(4))/3.0D0
       DEVIA(1)=STRES(1)-PMEAN
       DEVIA(2)=STRES(2)-PMEAN
       DEVIA(3)=STRES(3)
       DEVIA(4)=STRES(4)-PMEAN
      ENDIF
C
C**** DEVIATORIC BACK STRESS
C
      IF(NTYPE.EQ.5) THEN                         ! 1D case
       PMEAB=1.0D0/3.0D0*BACKS(1)
       DBACK(1)=2.0D0/3.0D0*BACKS(1)
       DBACK(2)=-1.0D0/3.0D0*BACKS(1)
       DBACK(3)=0.0D0
       DBACK(4)=-1.0D0/3.0D0*BACKS(1)
      ELSE                                        ! 2&3D cases
       PMEAB=(BACKS(1)+BACKS(2)+BACKS(4))/3.0D0
       DBACK(1)=BACKS(1)-PMEAB
       DBACK(2)=BACKS(2)-PMEAB
       DBACK(3)=BACKS(3)
       DBACK(4)=BACKS(4)-PMEAB
      ENDIF
C
      DO ISTR1=1,NSTR1
       DEVIA(ISTR1)=DEVIA(ISTR1)-DBACK(ISTR1)
      ENDDO
C
      IF(NTYPE.EQ.4) GO TO 10
C
C**** 1-D & 2-D CASES
C
      VARJ2=DEVIA(3)*DEVIA(3)+
     . 0.5D0*(DEVIA(1)*DEVIA(1)+DEVIA(2)*DEVIA(2)+DEVIA(4)*DEVIA(4))
      VARJ3=DEVIA(4)*(DEVIA(4)*DEVIA(4)-VARJ2)
      GO TO 20
C
C**** 3-D CASE
C
   10 DEVIA(5)=STRES(5)
      DEVIA(6)=STRES(6)
C
      DBACK(5)=BACKS(5)
      DBACK(6)=BACKS(6)
C
      DEVIA(5)=DEVIA(5)-DBACK(5)
      DEVIA(6)=DEVIA(6)-DBACK(6)
C
      VARJ2= DEVIA(3)*DEVIA(3)+DEVIA(5)*DEVIA(5)+DEVIA(6)*DEVIA(6)
     . +0.5D0*(DEVIA(1)*DEVIA(1)+DEVIA(2)*DEVIA(2)+DEVIA(4)*DEVIA(4))
      VARJ3= DEVIA(1)*DEVIA(2)*DEVIA(4)+DEVIA(3)*DEVIA(5)*DEVIA(6)*2.0D0
     .     -(DEVIA(1)*DEVIA(6)*DEVIA(6)+DEVIA(2)*DEVIA(5)*DEVIA(5)
     .      +DEVIA(4)*DEVIA(3)*DEVIA(3))
C
C**** CALCULATE THE THREE STRESS INVARIANTS
C
   20 CONTINUE
      STEFF=DSQRT(VARJ2)
C
      SINT3=1.0D0
      DENOM=VARJ2*STEFF
      IF(DENOM.NE.0.0D0)SINT3=-3.0D0*ROOT3*VARJ3/(2.0D0*DENOM)
      IF(SINT3.LT.-0.9986295348D0) SINT3=-1.0D0
      IF(SINT3.GT. 0.9986295348D0) SINT3= 1.0D0
      THETA=DASIN(SINT3)/3.0D0
C
C**** YIELD CRITERION CHOICE
C
      GO TO (31,32,33,34,35,36,37,38,39,40,41,42) (NCRIT-30)
C
C**** TRESCA
C
   31 YIELD=2.0D0*DCOS(THETA)*STEFF
      DFTEQ=-1.0D0
      DFAFI=0.0D0
      RETEN=1.0D0
      RETURN
C
C**** VON MISES
C
   32 YIELD=DSQRT(3.0D0*A1COE*VARJ2)
      RETURN
C
C**** MOHR-COULOMB & VERSION  WITH "TENSION CUT-OFF"
C
   33 CONTINUE
      ANPHI=(PIVAL*0.25D0)+ANGFI*0.5D00
      ALFA=RETEN/(DTAN(ANPHI)*DTAN(ANPHI))
      SNPHI=DSIN(ANGFI)
      CNPHI=DCOS(ANGFI)
      CAPA1=(0.5D0*(1.0D0+ALFA))-(0.5D0*(1.0D0-ALFA)*SNPHI)
      CAPA2=1.0D+23
      CAPA3=-((0.5D0*(1.0D0-ALFA))-(0.5D0*(1.0D0+ALFA)*SNPHI))
      IF(SNPHI.NE.0.0D0)
     .CAPA2=(0.5D0*(1.0D0+ALFA))+(0.5D0*(-1.0D0+ALFA)/SNPHI)
C
      TEN0=PMEAN*(CAPA3)+STEFF*((CAPA1*DCOS(THETA))
     .              -((CAPA2)*DSIN(THETA)*SNPHI/ROOT3))
      YIELD=2.0D0*(TEN0/CNPHI)*DTAN(ANPHI)
C
      DK1=-0.5D0*CNPHI*(1.0D0-ALFA)
      DK2=DK1/(SNPHI)**2.0D0
      DK3=0.5D0*CNPHI*(ALFA+1.0D0)
      DCF=0.25D00*CAPAP/(DSIN(ANPHI)*DSIN(ANPHI))
C
      TEN0P=PMEAN*DK3+STEFF*((DK1*DCOS(THETA))-(DK2*DSIN(THETA)*SNPHI
     .      /ROOT3)-(CAPA2*DSIN(THETA)*CNPHI/ROOT3))
      DFTEQ=-1.0D0
      DFAFI= TEN0*((CNPHI/(DCOS(ANPHI)*DCOS(ANPHI)))+
     .       (2.0D0*DTAN(ANPHI)*SNPHI))/(CNPHI*CNPHI)+
     .       (2.0D0*DTAN(ANPHI)/CNPHI)*TEN0P
      RETURN
C
C**** DRUCKER-PRAGER
C
   34 CNPHI=DCOS(ANGFI)
      SNPHI=DSIN(ANGFI)
C
      IF(PROPS(55).EQ.0.0D0)THEN 
C
C**** DRUCKER-PRAGER
C     (coincide with the OUTER apices of the Mohr-Coulomb)
C
      CFL=-ROOT3*(3.0D0-SNPHI)/(3.0D0*SNPHI-3.0D0)
      TEN0=(6.0D0*PMEAN*SNPHI/(ROOT3*(3.0D0-SNPHI)))+STEFF
      YIELD=CFL*TEN0
      TEN0P=6.0D0*PMEAN*ROOT3*CNPHI/((3.0D0-SNPHI)*(3.0D0-SNPHI))
      DFTEQ=-1.0D0
      DFAFI=(6.0D0*ROOT3*CNPHI*TEN0/
     .      ((3.0D0*SNPHI-3.0D0)*(3.0D0*SNPHI-3.0D0)))-
     .      (ROOT3*(3.0D0-SNPHI)/(3.0D0*SNPHI-3.0D0))*TEN0P
      ELSE
C
C**** DRUCKER-PRAGER
C     (coincide with the INNER apices of the Mohr-Coulomb)
C
      CFL=-ROOT3*(3.0D0+SNPHI)/(SNPHI-3.0D0)
      TEN0=(6.0D0*PMEAN*SNPHI/(ROOT3*(3.0D0+SNPHI)))+STEFF
      YIELD=CFL*TEN0
      TEN0P=6.0D0*PMEAN*ROOT3*CNPHI/((3.0D0+SNPHI)*(3.0D0+SNPHI))
      DFTEQ=-1.0D0
      DFAFI=(6.0D0*ROOT3*CNPHI*TEN0/
     .      ((SNPHI-3.0D0)*(SNPHI-3.0D0)))-
     .      (ROOT3*(3.0D0+SNPHI)/(SNPHI-3.0D0))*TEN0P
      ENDIF
C
      RETURN
C 
C**** J. LUBLINER'S THEORY
C
   35 CFL=1.0D0/(1.0D0-ALFAT)
      SMAXS=((2.0D0*STEFF*DSIN(THETA+(2.0D0*PIVAL/3.0D0)))/ROOT3)+PMEAN
      DELTA=0.0D0
      IF(SMAXS.LT.0.0D0) DELTA=GAMAT
      IF(SMAXS.GT.0.0D0) DELTA=BETAT
C   
      PROD=DSQRT(3.0D0*VARJ2)+(ALFAT*PMEAN*3.0D0)+(DELTA*SMAXS)
      YIELD=CFL*PROD
      DFTEQ=-1.0D0
      DFAFI=0.0D0
C
      RETURN
C
C**** ABOUAF'S MODEL
C
   36 YIELD=DSQRT((3.0D0*A1COE*VARJ2)+(A2COE*(3.0D0*PMEAN)**2.0D0))
      DFTEQ=-1.0D0
      DFAFI=0.0D0
      RETEN=1.0D0
      RETURN
C
C**** WEBER BROWN'S MODEL
C
   37 YIELD=1.0E+20
      IF(A2COE.NE.0.0D0)
     . YIELD=DSQRT((1.0/A2COE)*(3.0D0*VARJ2+(A1COE/6.0D0)*
     .                                            (PMEAN*3.0D0)**2.0D0))
      DFTEQ=-1.0D0
      DFAFI=0.0D0
      RETEN=1.0D0
      RETURN
C 
C**** SG CAST IRON
C
   38 YIELD=DSQRT(3.0D0*A1COE*VARJ2)
      RETURN
C 
C**** GREEN SAND
C
   39 YIELD=DSQRT(3.0D0*A1COE*VARJ2)
      RETURN
C 
C**** GREEN SAND
C
   40 YIELD=DSQRT(3.0D0*A1COE*VARJ2)
      RETURN
C 
C**** GURSON
C
   41 PREYS=A3COE*CCERO+A2COE*CAPAP          ! as in pltoha.f
      IFVER=INT(PROPS(35))
      IF(IFVER.EQ.4) THEN
       IF(CAPAP.EQ.0.0D0) PREYS=CCERO
      ENDIF
C
      Q1PAM=1.0D+00
      Q2PAM=1.0D+00
      IF(IPORO.EQ.4) THEN
       Q1PAM=CPOE1
       Q2PAM=CPOE2
      ENDIF
      WWWAU=(3.0D+00*PMEAN*Q2PAM)/(2.0D+00*PREYS)
      WWWGG=1.0D+00+Q1PAM*Q1PAM*POROS*POROS-
     .                                  2.0D+00*Q1PAM*POROS*DCOSH(WWWAU)
C
      YIELD=DSQRT(3.0D0/WWWGG*VARJ2)
      RETURN
C
C**** HILL 48
C
   42 IF(ISOTR.EQ.0) THEN   ! only for plane stress problems
       A1=2.0D0*CLANK/(1.0D0+CLANK)
       A2=A1*(1.0D0+2.0D0*CLANK)
       VARJ2H=STRES(1)*STRES(1)+STRES(2)*STRES(2)-
     .                       A1*STRES(1)*STRES(2)+
     .                       A2*STRES(3)*STRES(3)
      ENDIF
      IF(ISOTR.EQ.1) THEN
       NC42T=INT(PROPS(45))
       IF(NC42T.EQ.1) THEN
        AFP=CLAN1
        AGP=CLAN2
        AHP=CLAN3
        ANP=CLAN4
       ENDIF
       IF(NC42T.EQ.2) THEN
        AFP=0.5D0*(1.0D0/(CLAN2*CLAN2)+1.0D0/(CLAN3*CLAN3)-
     .             1.0D0/(CCERO*CCERO))*CCERO*CCERO
        AGP=0.5D0*(1.0D0/(CCERO*CCERO)+1.0D0/(CLAN3*CLAN3)-
     .             1.0D0/(CLAN2*CLAN2))*CCERO*CCERO
        AHP=0.5D0*(1.0D0/(CCERO*CCERO)+1.0D0/(CLAN2*CLAN2)-
     .             1.0D0/(CLAN3*CLAN3))*CCERO*CCERO
        ANP=0.5D0*(1.0D0/(CLAN1*CLAN1)-1.0D0/(CLAN3*CLAN3))*CCERO*CCERO
       ENDIF
       IF(NC42T.EQ.3) THEN
        AFP=CLAN1/(CLAN3*(1.0D0+CLAN1))
        AGP=1.0D0/(1.0D0+CLAN1)
        AHP=CLAN1/(1.0D0+CLAN1)
        ANP=(0.5D0+CLAN2)/(1.0D0+CLAN1)*(CLAN1/CLAN3+1.0D0)
       ENDIF
       IF(NTYPE.EQ.1) THEN  ! plane stress
        VARJ2H=AFP*STRES(2)*STRES(2)+AGP*STRES(1)*STRES(1)+
     .         AHP*(STRES(1)-STRES(2))*(STRES(1)-STRES(2))+
     .         2.0D0*ANP*STRES(3)*STRES(3)
       ENDIF
       IF(NTYPE.EQ.3) THEN  ! axisymmetry
        VARJ2H=AFP*STRES(4)*STRES(4)+AGP*STRES(1)*STRES(1)+
     .         AHP*(STRES(1)-STRES(4))*(STRES(1)-STRES(4))
       ENDIF
       IF(NTYPE.EQ.4) THEN  ! 3D
        IF(NC42T.EQ.1) THEN
         AMP=CLAN5
         ALP=CLAN6
        ENDIF
        IF(NC42T.EQ.2.OR.NC42T.EQ.3) THEN
         AMP=0.5D0*(AFP+4.0D0*AGP+AHP)
         ALP=0.5D0*(4.0D0*AFP+AGP+AHP)
        ENDIF
        VARJ2H=AFP*(STRES(2)-STRES(4))*(STRES(2)-STRES(4))+
     .         AGP*(STRES(4)-STRES(1))*(STRES(4)-STRES(1))+
     .         AHP*(STRES(1)-STRES(2))*(STRES(1)-STRES(2))+
     .         2.0D0*ALP*STRES(6)*STRES(6)+
     .         2.0D0*AMP*STRES(5)*STRES(5)+
     .         2.0D0*ANP*STRES(3)*STRES(3)
       ENDIF
      ENDIF
      YIELD=DSQRT(A1COE*VARJ2H)
      RETURN
C
      END
