      SUBROUTINE UPDATET(ASDITT,ASDTFT,DISINT,DISITT,NELEMT,NEVABT,
     .                   NTOTVT,RLOADT,TLOADT)
C***********************************************************************
C
C**** THIS ROUTINE CALCULATES THE CHANGE IN LOAD STEP ACCORDING TO 
C     SELECTED PATH AND UPDATES LOAD LEVEL AND DISPLACEMENT VECTORS
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'inte_omt.f'
C
      COMMON/LOGUNT/LUDTST,LUSOLT,LUFROT,LUFRHT,LUDATT,LUPRIT,LUREST,
     .              LUSO2T,LUFR2T,LUPOST,LURSTT,LUBFGT,LUPIPT,LUPANT,
     .              LUGEOT,LUSETT,LUMATT,LUINIT,LULOAT,LUFIXT,LUADVT,
     .              LUACTT,LUFANT,LUSTRT,
     .              LUCU1T,LUCU2T,LUCU3T,LUCU4T,LUCU5T,LUCU6T,LUCU7T,
     .              LUCU8T,LUCU9T,LUC10T
C
      DIMENSION ASDITT(*), ASDTFT(*), DISINT(*), 
     .          DISITT(*), RLOADT(*), TLOADT(*)
      SAVE FACTFT
C
C**** FOR THE FIRST ITERATION COMPUTE FACTO
C
      IF(IITERT.EQ.1) THEN
C
        DO 1 ITOTVT=1,NTOTVT
    1   DISINT(ITOTVT)=0.
C
        ARCLNT=ARCLNT*DSQRT(DITERT/PITERT)
C
        IF(KARCLT.EQ.4) THEN                      ! FOR DISPL. CONTROL
          FACTOT=(ARCLNT-DISITT(NCDIST))/ASDITT(NCDIST)
        ELSE                                     ! FOR ARC-LENGTH
          DISPST= 0.0     
          SIGNRT= 0.0                  
          DO 5 ITOTVT= 1,NTOTVT
          ASDTFT(ITOTVT) = ASDITT(ITOTVT)
          SIGNRT = SIGNRT + ASDITT(ITOTVT)*RLOADT(ITOTVT)
    5     DISPST = DISPST + ASDITT(ITOTVT)*ASDITT(ITOTVT)
          SIGNRT = SIGNRT/DABS(SIGNRT)
          FACTOT = SIGNRT*ARCLNT/DSQRT(DISPST)
        ENDIF
C
        TFACTT=TFACTT+FACTOT                    ! UPDATE LOAD FACTOR
        FACTFT=FACTOT
        DELTAT=FACTOT
        WRITE(LUREST,900) ISTEPT,TFACTT,FACTOT 
C
      ELSE
C
C***FOR ANY OTHER ITERATION COMPUTE DELTA ACCORDING TO PATH
C
        PROD1T=0.0
        PROD2T=0.0
        PROD3T=0.0
C
        IF(KARCLT.EQ.1) THEN                  ! NORMAL PLANE PATH
          DO 25 ITOTVT=1,NTOTVT
          VALUET=ASDTFT(ITOTVT)*FACTFT
          PROD1T=PROD1T+VALUET*DISITT(ITOTVT)
   25     PROD2T=PROD2T+VALUET*ASDITT(ITOTVT)
          DELTAT=-PROD1T/PROD2T
        ENDIF
C
        IF(KARCLT.EQ.2) THEN               ! UPDATED NORMAL PLANE PATH
          DO 35 ITOTVT=1,NTOTVT
          VALUET = DISINT(ITOTVT)
          PROD1T = PROD1T + VALUET*DISITT(ITOTVT)
   35     PROD2T = PROD2T + VALUET*ASDITT(ITOTVT)
          DELTAT =-PROD1T / PROD2T
        ENDIF
C
        IF(KARCLT.EQ.3) THEN                  ! SPHERICAL PATH
C
          DO 50 ITOTVT=1,NTOTVT
          VALU1T=ASDITT(ITOTVT)
          VALU3T=DISINT(ITOTVT)+DISITT(ITOTVT)
          PROD1T=PROD1T+VALU1T*VALU1T
          PROD2T=PROD2T+VALU3T*VALU1T
          PROD3T=PROD3T+VALU3T*VALU3T
   50     CONTINUE
C
          AVALUT = PROD1T
          BVALUT = 2.0 * PROD2T
          CVALUT = PROD3T - ARCLNT * ARCLNT
          USQRTT = BVALUT * BVALUT - 4.0 * AVALUT * CVALUT
          USQRTT = SQRT(USQRTT)
          ROOT1T = (-BVALUT+USQRTT)/(2.0*AVALUT)
          ROOT2T = (-BVALUT-USQRTT)/(2.0*AVALUT)
          ANGL1T = 0.0
          ANGL2T = 0.0
C
          DO 60 ITOTVT=1,NTOTVT
          VALU1T = DISINT(ITOTVT)
          VALU2T = DISITT(ITOTVT)
          VALU3T = ASDITT(ITOTVT)
          VALU4T = VALU1T + VALU2T + ROOT1T*VALU3T
          VALU5T = VALU1T + VALU2T + ROOT2T*VALU3T
          ANGL1T = ANGL1T + VALU4T*VALU1T
          ANGL2T = ANGL2T + VALU5T*VALU1T
   60     CONTINUE
C
          IF(ANGL1T.GT.0.0.AND.ANGL2T.GT.0.0) THEN
            RLINRT = -CVALUT / BVALUT
            DIFF1T = ABS(ROOT1T-RLINRT)
            DIFF2T = ABS(ROOT2T-RLINRT)
            DELTAT = ROOT1T
            IF(DIFF1T.GT.DIFF2T) DELTAT = ROOT2T
         ELSE
            IF(ANGL1T.GT.0.0) DELTAT = ROOT1T
            IF(ANGL2T.GT.0.0) DELTAT = ROOT2T
          ENDIF
C
        ENDIF
C
        IF(KARCLT.EQ.4) THEN                  ! DISPLACEMENT CONTROL
          DELTAT = -DISITT(NCDIST)/ASDITT(NCDIST)
        ENDIF
C
C***CORRECT LOAD LEVEL
C
        FACTOT = FACTOT + DELTAT
        TFACTT = TFACTT + DELTAT
        WRITE(LUREST,910) TFACTT,FACTOT
      ENDIF
C
C***FOR EVERY ITERATION UPDATE LOAD VECTORS &
C                COMPUTE COMPLETE ITERATIVE DISPLACEMENT VECTOR
C
      DO 90 ITOTVT=1,NTOTVT
      TLOADT(ITOTVT)=TLOADT(ITOTVT)+DELTAT*RLOADT(ITOTVT)
  90  DISITT(ITOTVT)=DISITT(ITOTVT)+DELTAT*ASDITT(ITOTVT)
C
      RETURN
C
  900 FORMAT(1H1,//,132('*'),/,
     .       10X,'LOAD STEP NO.',I5,/,
     .       15X,'INITIAL LOAD LEVEL  =',E15.6,/,
     .       15X,'INITIAL LOAD STEP   =',E15.6,/132('*'))
  910 FORMAT(//,132('='),//,
     .       15X,'UPDATED LOAD LEVEL  =',E15.6,/,
     .       15X,'UPDATED LOAD STEP   =',E15.6,/)
      END
