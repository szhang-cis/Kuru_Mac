      SUBROUTINE NODES2(COORD,LNODS,NDIME,IELEM,NNODE)
C***********************************************************************
C
C**** THIS ROUTINE INTERPOLATES THE MID-SIDE NODES OF STRAIGHT
C     SIDES OF ELEMENTS AND THE CENTRAL NODE OF 9 NODED ELEMENTS
C     FOR 2D ELEMENTS
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      DIMENSION COORD(NDIME,*), LNODS(*)
C
      IF(NNODE.LE.4)RETURN
C
C**** LOOP OVER EACH ELEMENT EDGE
C
      NNOD1=4
      IF(NNODE.EQ.8.OR.NNODE.EQ.9) NNOD1=5
      DO INODE=NNOD1,NNODE
        LNODE=LNODS(INODE)
        TOTAL=DABS(COORD(1,LNODE))+DABS(COORD(2,LNODE))
        IF(TOTAL.LE.0.0) THEN
          IF(INODE.NE.9) THEN
C
C**** COMPUTE THE NODE NUMBER OF THE FIRST NODE
C
            IGASH=INODE-NNOD1+1
            NODST=LNODS(IGASH)
C
C**** COMPUTE THE NODE NUMBER OF THE LAST NODE
C
            IGASH=IGASH+1
            IF(IGASH.GT.(NNOD1-1)) IGASH=1
            NODFN=LNODS(IGASH)
C
C**** IF THE COORDINATES OF THE INTERMEDIATE NODE ARE BOTH ZERO
C     INTERPOLATE BY A STRAIGHT LINE
C
            DO IDIME=1,NDIME
              COORD(IDIME,LNODE)=(COORD(IDIME,NODST)+
     .                            COORD(IDIME,NODFN))*0.5
            ENDDO
C
C**** COMPUTE THE CENTRAL POINT FOR 9-NODED ELEMENT
C
          ELSE
            CALL MIDPOI(COORD,IELEM,LNODS,NDIME,NNODE)
          ENDIF
        ENDIF
      ENDDO
C
      RETURN
      END
