      SUBROUTINE FROAPP(LNODS,NBUFA,NDFRO,NDOFN,NELEM,NFRON,NNODE,
     .                  NPOIN,NSTIF,KSYMM)
C***********************************************************************
C
C**** THIS ROUTINE CHANGE SIGNE TO LAST APPERANCE OF NODES, AND FINDS
C     THE LARGEST FRONT WIDTH (NFRON & NSTIF)
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      COMMON/LOGUN/LUDTS,LUSOL,LUFRO,LUFRH,LUDAT,LUPRI,LURES,
     .             LUSO2,LUFR2,LUPOS,LURST,LUBFG,LUPIP,LUPAN,LUINF,
     .             LUGEO,LUSET,LUMAT,LUINI,LULOA,LUFIX,LUIN1,
     .             LUTUN,LUCON,LUACT,LUFAN,
     .             LUCU1,LUCU2,LUCU3,LUCU4,LUCU5,LUCU6,LUCU7,
     .             LUCU8,LUCU9,LUC10
C
      DIMENSION LNODS(NNODE,*), NDFRO(*)
C
      KFRON=0
      DO 5  IELEM=1,NELEM
    5 NDFRO(IELEM)=0
C
C**** CHECK FOR ANY REPETITION OF A NODE NUMBER WITHIN AN ELEMENT
C
      DO 10 IPOIN=1,NPOIN
C
C**** SEEK FIRST,LAST AND INTERMEDIATE APPEARANCES OF NODE IPOIN AND
C     CALCULATE INCREASE OR DECREASE IN FRONTWIDTH AT EACH ELEMENT STAGE
C
        KSTAR=0
        DO 20 IELEM=1,NELEM
C$DIR SCALAR
          DO 30 INODE=1,NNODE
            IF(LNODS(INODE,IELEM).EQ.IPOIN) THEN
C
              IF(KSTAR.EQ.0)THEN
                KSTAR=IELEM
                NDFRO(IELEM)=NDFRO(IELEM)+NDOFN
              ENDIF
C
              KLAST=IELEM
              NLAST=INODE
            ENDIF
   30     CONTINUE
   20   CONTINUE
C
C**** AND CHANGE THE SIGN OF THE LAST APPEARANCE OF EACH NODE
C
        IF(KSTAR.EQ.0)THEN
          WRITE(LURES,900) IPOIN
        ELSE
          IF(KLAST.LT.NELEM) NDFRO(KLAST+1)=NDFRO(KLAST+1)-NDOFN
          LNODS(NLAST,KLAST)=-IPOIN
        ENDIF
C
   10 CONTINUE
C
C**** CALCULATE THE LARGEST FRONTWIDTH
C
      NFRON=0
      KFRON=0
      DO 50 IELEM=1,NELEM
        NFRON=NFRON+NDFRO(IELEM)
        IF(NFRON.GT.KFRON) KFRON=NFRON
   50 CONTINUE
      NFRON=KFRON
      NSTIF=(NFRON-1)*NFRON/2+NFRON      ! symmetric case
      IF(KSYMM.EQ.0) NSTIF=NFRON*NFRON   ! unsymmetric case
C
      IF(NBUFA.EQ.0) NBUFA=NFRON+1
C
      WRITE(LURES,901) NFRON,NBUFA
      WRITE(LUPRI,901) NFRON,NBUFA
C
C**** VERIFIED IF ANY INPUT ERRORS HAVE BEEN DETECTED
C
      RETURN
  900 FORMAT(/,15H CHECK WHY NODE,I4,
     . 54H NEVER APPEARS IN THE LIST OF NODAL CONNECTION (LNODS))
  901 FORMAT(//5X,'FRONTAL SOLVER INFORMATION :',/,
     .         5X,'==========================  ',/,
     .        15X,'MEAN FRONT-WIDTH        =',I8,/
     .        15X,'MAX. FRONT-WIDTH ALLOWED=',I8,/)
      END
