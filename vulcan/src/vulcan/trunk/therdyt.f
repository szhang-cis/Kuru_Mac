      SUBROUTINE THERDYT(VELCTT,LNODST,MATNOT,PROELT,PROPST,
     .                   ELOADT,ELDATT,ELPRET,ELVART,ELMATT,
     .                   WORK1T,DISITT,ELOATT,COORDT,
     .                   TEMPIT,ADVELT,FPCHAT,DISTOT,DISPLT,
     .                   LACTIT)
C***********************************************************************
C
C**** THIS ROUTINE CALCULATES THE "THERMAL DYNAMIC FORCES"
C    
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
C**** ADDITIONAL PARAMETERS
C
      INCLUDE 'addi_omt.f'
C
C**** COUPLING VARIABLES
C
      INCLUDE 'nuec_om.f'
C
C**** THERMAL VARIABLES
C
      INCLUDE 'prob_omt.f'
      INCLUDE 'inte_omt.f'
      INCLUDE 'auxl_omt.f'
C
      DIMENSION VELCTT(*),        ELOADT(*),      LNODST(NNODET,NELEMT),
     .          DISITT(*),        MATNOT(NELEMT), PROELT(NPRELT,NGRUPT),
     .          PROPST(NPROPT,NMATST)
      DIMENSION WORK1T(*)
      DIMENSION ELDATT(NDATAT),        ELPRET(NPREVT),
     .          ELVART(NSTATT),        ELMATT(NMATXT)
      DIMENSION ELOATT(*)
      DIMENSION COORDT(NDIMET,NPOINT)
      DIMENSION TEMPIT(NPOINT),        ADVELT(NTOTVT*NDIMET),
     .          FPCHAT(NFPCH,NPOINT)
      DIMENSION DISTOT(NTOTVT),        DISPLT(NTOTVM),
     .          LACTIT(NELEMT)
C
C**** LOOP ON ELEMENTS
C
      DO 1000 IELEMT=1,NELEMT
      IF(NACTIT.EQ.1) THEN
       IF(LACTIT(IELEMT).EQ.0) GO TO 1000     ! skip non active elements
      ENDIF
      LGRUPT=MATNOT(IELEMT)
      LMATST=INT(PROELT(1,LGRUPT))
      NNODLT=INT(PROELT(2,LGRUPT))
      LTYPET=INT(PROELT(5,LGRUPT))
C     
C**** READ ELEM. DATA FROM DATA BASE 
C
      IF(NMEMO1.EQ.0.OR.NMEMO2.EQ.0)
     . CALL DATBAST(ELDATT,    1,    2)
C
      IF(NMEMO3.EQ.1.OR.NMEMO4.EQ.1.OR.NMEMO5.EQ.0)
     . CALL DATBAST(ELVART,    3,    2)    ! current
C
      IF(NMEMO5.EQ.1) THEN
C
C**** SCALAR GATHER OPERATIONS ( DISTOT ---> ELDIST )
C
       CALL GATHER(DISTOT,NDOFCT,NPOINT,WORK1T(IFORDT(32)),NDOFCT,
     .             NNODLT,
     .             LNODST(1,IELEMT))
      ENDIF
C
C**** GATHER NODAL RATES OF TEMPERATURES
C
      CALL GATHER(VELCTT,NDOFCT,NPOINT,WORK1T(IFORDT(2)),NDOFNT,NNODLT,
     .            LNODST(1,IELEMT))
C
C**** GATHER NODAL ITERATIVE TEMPERATURES
C
      CALL GATHER(DISITT,NDOFCT,NPOINT,WORK1T(IFORDT(3)),NDOFNT,NNODLT,
     .            LNODST(1,IELEMT))
C
C**** GATHER NODAL ADVECTIVE VELOCITY (npoint=ntotvt)
C
      IF(ICONVT.EQ.1)
     . CALL GATHER(ADVELT,NDIMET,NTOTVT,WORK1T(IFORDT(22)),NDIMET,
     .             NNODLT,
     .             LNODST(1,IELEMT))
C
C**** GATHER NODAL COORDINATES
C
      IF(NMEMO1.EQ.1) THEN    ! coordinates in a global array
       CALL GATHER(COORDT,NDIMET,NPOINT,WORK1T(IFORDT(17)),NDIMET,
     .             NNODLT,LNODST(1,IELEMT))
      ENDIF
C
C**** SCALAR GATHER OPERATIONS ( TEMPIT ---> WORK1T(IFORDT(33)) )
C
      CALL GATHER(TEMPIT,NDOFCT,NPOINT,WORK1T(IFORDT(33)),NDOFCT,
     .            NNODLT,LNODST(1,IELEMT))     ! initial temperature
C
C**** GATHER PHASE-CHANGE VECTOR ( FPCHAT ---> WORK1T(IFORDT(34)) )
C
      IF(NFILL.EQ.1.OR.LTYPET.EQ.5)
     . CALL GATHER(FPCHAT,NFPCH,NPOINT,WORK1T(IFORDT(34)),NFPCH,
     .             NNODLT,LNODST(1,IELEMT))
C
      IF(ITERME.GT.0) THEN          ! bidirectional coupled
       IF(ITERMD.EQ.1) THEN         ! deformed shape
C
C**** SCALAR GATHER OPERATIONS ( DISPLT ---> WORK1T(IFORDT(37)) )
C
        CALL GATHER(DISPLT,NDOFCM,NPOINT,WORK1T(IFORDT(37)),NDOFCM,
     .              NNODLT,
     .              LNODST(1,IELEMT))
       ENDIF                        ! itermd.eq.1
      ENDIF                         ! iterme.gt.0
C
C**** CALL ELEMENT PROCESSOR TO PERFORM REQUIRED OPERATIONS
C
      CALL ELMLIBT(LNODST(1,IELEMT),PROELT(1,LGRUPT),PROPST(1,LMATST),
     .             ELDATT,ELPRET,ELVART,ELMATT,DUMMYT,WORK1T,     6)
C
C**** WRITE CURRENT STATE VARIABLES TO DATA BASE
C
      IF(NMEMO3.EQ.1.OR.NMEMO4.EQ.1.OR.NMEMO5.EQ.0)
     . CALL DATBAST(ELVART,    3,    1)
C
C**** SCATTER RESULTS INTO ELOAD
C
      CALL SCATER(WORK1T(IFORDT(1)),NDOFNT,NNODLT,ELOADT,NDOFCT,NPOINT,
     .            LNODST(1,IELEMT))
C
C**** SCALAR SCATTER OPERATION ( WORK1T --> ELOATT )
C
      LHINC=0                          ! better as input (see frdy05t.f)
      IF(LHINC.EQ.1)
     . CALL SCATER(WORK1T(IFORDT(6)),NDOFNT,NNODLT,ELOATT,NDOFCT,
     .             NPOINT,LNODST(1,IELEMT))
C
C**** SCALAR SCATTER OPERATION ( WORK1T --> FPCHAT )
C
      IF(LTYPET.EQ.5)
     . CALL SCATERA(WORK1T(IFORDT(34)),NFPCH,NNODLT,FPCHAT,NFPCH,
     .              NPOINT,LNODST(1,IELEMT))
C
 1000 CONTINUE
C
      RETURN
      END
