      SUBROUTINE HOURK8(CARTD,EPMTX,DVOLU,ELCOD,GPCOD,HPARA,PROPS,
     .                  ARRAY,DMATX,NDIME,NEVAB,NNODE,NSTRS,NTYPE)
C***********************************************************************
C
C**** THIS ROUTINE COMPUTES THE "HOURGLASS" STIFFNESS FOR THE 8-NODE EL.
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      DIMENSION CARTD(NDIME,*), EPMTX(*),       ELCOD(NDIME,*), 
     .          PROPS(*),       ARRAY(*),       DMATX(NSTRS,*)
      DIMENSION BMATX(6,24,6),  SIGMA(6,6),     EMATX(24,24)
C
      FMUL1=HPARA*DVOLU/3.0
      FMUL2=HPARA*DVOLU*4.0/9.0
C
C     SET UP THE ELASTIC MATRIX
C
      KOUNT=0
      DO ISTRS=1,NSTRS
       DO JSTRS=ISTRS,NSTRS
        KOUNT=KOUNT+1
        DMATX(ISTRS,JSTRS)=EPMTX(KOUNT)
        DMATX(JSTRS,ISTRS)=DMATX(ISTRS,JSTRS)
       ENDDO
      ENDDO
C
C     COMPUTE THE HOURGLASS B-MATRICES
C
      CALL HOURB8(BMATX,CARTD,DVOLU,ELCOD,NDIME,NEVAB,NNODE,NSTRS)
C
C     COMPUTE HOURGLASS STIFFNESS MATRIX
C
      DO INDEX=1,3
       CALL BDBCO1(BMATX(1,1,INDEX),DMATX,EMATX,1,NEVAB,NSTRS,NSTRS,
     .             NEVAB,NSTRS)
       CALL SQTOTR(ARRAY,EMATX,FMUL1,NEVAB)     ! symmetric
      ENDDO
      DO INDEX=4,6
       CALL BDBCO1(BMATX(1,1,INDEX),DMATX,EMATX,1,NEVAB,NSTRS,NSTRS,
     .             NEVAB,NSTRS)
       CALL SQTOTR(ARRAY,EMATX,FMUL2,NEVAB)     ! symmetric
      ENDDO
C
      RETURN
      END
