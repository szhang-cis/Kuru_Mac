      SUBROUTINE SKYDIA(GSTLO,GSTUP,GSTDI,NTERM,KSYMM,PIVOT)
C***********************************************************************
C
C**** THIS ROUTINE DIAGONALIZES ELEMENT IN TRIANGULAR DECOMPOSITION
C
C.... INPUT PARAMETERS
C         GSTUP(NTERM) - COLUMN OF UPPER TRIANGULAR PART OF MATRIX
C         GSTLO(NTERM) - ROW OF LOWER TRIANGULAR PART OF MATRIX
C         GSTDI(NTERM) - RECIPROCAL OF DIAGONALS IN TRIANGULAR FACTORS
C         NTERM        - NUMBER OF TERMS IN VECTORS
C         KSYMM        - IF 0 -> EQUATIONS ARE UNSYMMETRIC
C                        IF 1 -> EQUATIONS ARE SYMMETRIC
C         PIVOT        - DIAGONAL IN MATRIX TO BE FACTORED
C
C.... OUTPUT PARAMETER
C         PIVOT        - REDUCED DIAGONAL OF FACTOR
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION GSTLO(*),GSTUP(*),GSTDI(*)
C
C      <<<<<<<<<<<<<<<<<<<<< CUTE TRICK TO AVOID CONVEX ERROR
C
      IF(KSYMM.EQ.0) THEN
C
        DO 100 ITERM= 1,NTERM
        PIVOT=PIVOT-GSTLO(ITERM)*GSTUP(ITERM)*GSTDI(ITERM)
        GSTUP(ITERM)=GSTUP(ITERM)*GSTDI(ITERM)
  100   CONTINUE
C
      ELSE IF(KSYMM.EQ.1) THEN
C
        DO 105 ITERM= 1,NTERM
        PIVOT=PIVOT-GSTUP(ITERM)*GSTUP(ITERM)*GSTDI(ITERM)
        GSTUP(ITERM)=GSTUP(ITERM)*GSTDI(ITERM)
  105   CONTINUE
C
      ENDIF
C
C***FINISH COMPUTATION OF COLUMN OF GSTLO FOR UNSYMMETRIC MATRICES
C
      IF(KSYMM.EQ.0) THEN
C
        DO 200 ITERM=1,NTERM
  200   GSTLO(ITERM)=GSTLO(ITERM)*GSTDI(ITERM)
C
      ENDIF
C
      RETURN
      END
