      SUBROUTINE PORO05T(CARTDT,DVOLUT,EMASST,SHAPET,PROPST,
     .                   SPIPBT,SPIPAT,SPIPCT,
     .                   ELDIST,FPCHLT,VELCMT)
C***********************************************************************
C
C**** THIS ROUTINE COMPUTES THE ELEMENTAL CONTRIBUTION TO NODAL POROSITY
C     VARIABLES
C
C***********************************************************************
C
C     Index of variables:
C
C     SPIPAT=auxiliar array
C     SPIPBT=current smoothed porosity criteria (output)
C     SPIPCT=last converged porosity criteria (at nodes [smoothed])
C
C***********************************************************************
      IMPLICIT REAL*8 (A-H,O-Z)
C
C**** COUPLING VARIABLES
C
      INCLUDE 'nuec_om.f'
C
C**** THERMAL VARIABLES
C
      INCLUDE 'prob_omt.f'
      INCLUDE 'inte_omt.f'
      INCLUDE 'auxl_omt.f'
      INCLUDE 'inpo_omt.f'
C
      COMMON/SMOGAUT/FACTAT
C
      DIMENSION CARTDT(NDIMET,NNODLT,*),
     .          DVOLUT(*),        SHAPET(NNODLT,*),
     .          EMASST(NNODLT,*), PROPST(*)
      DIMENSION SPIPBT(NPOROT,*), SPIPAT(NNODLT,*), SPIPCT(NPOROT,*)
      DIMENSION ELDIST(NDOFCT,*), FPCHLT(NFPCH,*),  VELCMT(*)
      DIMENSION DSTRAT(3)
C
C**** SAVE LAST CONVERGED VALUES
C
      DO IPOROT=1,NPOROT
       DO INODLT=1,NNODLT
        SPIPBT(IPOROT,INODLT)=SPIPCT(IPOROT,INODLT)
       ENDDO
      ENDDO
C
      IF(KPOROT.EQ.0) RETURN
C
C**** INITIALISES SPIPBT
C
      NNODGT=NNODLT
      IF(NGAULT.GT.NNODLT) NNODGT=NGAULT
      DO IPOROT=1,NPOROT
       DO INODLT=1,NNODGT
        SPIPBT(IPOROT,INODLT)=0.0D0
       ENDDO
      ENDDO
C
C**** OBTAINS POROSITY CRITERIA AT GAUSS POINTS
C
      DO IPOROT=1,NPOROT
       DO IGAUST=1,NGAULT
        DO INODLT=1,NNODLT
         SPIPBT(IPOROT,IGAUST)=SPIPBT(IPOROT,IGAUST)+
     .                       SHAPET(INODLT,IGAUST)*SPIPCT(IPOROT,INODLT)
        ENDDO
       ENDDO
      ENDDO
      DO IPOROT=1,NPOROT
       DO IGAUST=1,NGAULT
        SPIPCT(IPOROT,IGAUST)=SPIPBT(IPOROT,IGAUST)
       ENDDO
      ENDDO
C
C**** INITIALISES SPIPBT (again)
C
      NNODGT=NNODLT
      IF(NGAULT.GT.NNODLT) NNODGT=NGAULT
      DO IPOROT=1,NPOROT
       DO INODLT=1,NNODGT
        SPIPBT(IPOROT,INODLT)=0.0D0
       ENDDO
      ENDDO
C
C**** POROSITY CRITERION 1: SOLIDIFICATION TIME (ts)
C
      FPCTOL=1.0D-06
      IF(IPRCOT.GT.0) THEN
       DO IGAUST=1,NGAULT
        FPCHAN=0.0D0
        DO INODLT=1,NNODLT
         FPCHAN=FPCHAN+SHAPET(INODLT,IGAUST)*FPCHLT(KPOROT,INODLT)
        ENDDO
        IF(FPCHAN.GT.FPCTOL) SPIPCT(1,IGAUST)=SPIPCT(1,IGAUST)+DTIMET
       ENDDO
      ENDIF
C
C**** POROSITY CRITERION 2: TEMPERATURE GRADIENT (G) AT fpc=0
C
      DO IGAUST=1,NGAULT
       FPCHAN=0.0D0
       DFPCHA=0.0D0
       DO INODLT=1,NNODLT
        FPCHAN=FPCHAN+SHAPET(INODLT,IGAUST)*FPCHLT(KPOROT,INODLT)
        DFPCHA=DFPCHA+SHAPET(INODLT,IGAUST)*FPCHLT(KPOROT+NNUPT,INODLT)
       ENDDO
       FPCHAA=FPCHAN-DFPCHA*DTIMET
C
       IF(FPCHAN.LT.FPCTOL.AND.FPCHAA.GT.FPCTOL) THEN
        DO IDIMET=1,NDIMET
         DSTRAT(IDIMET)=0.0D0
         DO INODLT=1,NNODLT
          DSTRAT(IDIMET)=DSTRAT(IDIMET)+CARTDT(IDIMET,INODLT,IGAUST)*
     .                   ELDIST(1,INODLT)
         END DO
        END DO
C
        TEMPCU=0.0D0
        DO IDIMET=1,NDIMET
         TEMPCU=TEMPCU+DSTRAT(IDIMET)*DSTRAT(IDIMET)
        ENDDO
        TEMPCU=DSQRT(TEMPCU)
        SPIPCT(2,IGAUST)=TEMPCU
       ENDIF
      ENDDO
C
C**** POROSITY CRITERION 3: G(fpc=0)*sqrt[ts]
C
      DO IGAUST=1,NGAULT
       TSSSS=0.0D0
       DO INODLT=1,NNODLT
        TSSSS=TSSSS+SHAPET(INODLT,IGAUST)*SPIPCT(1,INODLT)
       ENDDO
       SPIPCT(3,IGAUST)=SPIPCT(2,IGAUST)*DSQRT(TSSSS)
      ENDDO
C
C**** POROSITY CRITERION 4: G(fpc=0)/sqrt[R(fpc=0)] (R:temperature rate)
C
      TETOL=1.0D-06
      DO IGAUST=1,NGAULT
       DTEMPT=0.0D0
       FPCHAN=0.0D0
       DFPCHA=0.0D0
       DO INODLT=1,NNODLT
        DTEMPT=DTEMPT+SHAPET(INODLT,IGAUST)*VELCMT(INODLT)
        FPCHAN=FPCHAN+SHAPET(INODLT,IGAUST)*FPCHLT(KPOROT,INODLT)
        DFPCHA=DFPCHA+SHAPET(INODLT,IGAUST)*FPCHLT(KPOROT+NNUPT,INODLT)
       ENDDO
       FPCHAA=FPCHAN-DFPCHA*DTIMET
C
       IF(FPCHAN.LT.FPCTOL.AND.FPCHAA.GT.FPCTOL) THEN
        DTEMPT=DABS(DTEMPT)
        IF(DTEMPT.GT.TETOL)
     .   SPIPCT(4,IGAUST)=SPIPCT(2,IGAUST)/DSQRT(DTEMPT)
       ENDIF
      ENDDO
C
C**** POROSITY CRITERION 5: SOLIDUS VELOCITY V(fpc=0)=R(fpc=0)/G(fpc=0)
C
      GRTOL=1.0D-06
      DO IGAUST=1,NGAULT
       DTEMPT=0.0D0
       FPCHAN=0.0D0
       DFPCHA=0.0D0
       DO INODLT=1,NNODLT
        DTEMPT=DTEMPT+SHAPET(INODLT,IGAUST)*VELCMT(INODLT)
        FPCHAN=FPCHAN+SHAPET(INODLT,IGAUST)*FPCHLT(KPOROT,INODLT)
        DFPCHA=DFPCHA+SHAPET(INODLT,IGAUST)*FPCHLT(KPOROT+NNUPT,INODLT)
       ENDDO
       FPCHAA=FPCHAN-DFPCHA*DTIMET
C
       IF(FPCHAN.LT.FPCTOL.AND.FPCHAA.GT.FPCTOL) THEN
        DTEMPT=DABS(DTEMPT)
        IF(SPIPCT(2,IGAUST).GT.GRTOL)
     .   SPIPCT(5,IGAUST)=DTEMPT/SPIPCT(2,IGAUST)
       ENDIF
      ENDDO
C
C**** POROSITY CRITERION 6: G(fpc=0)*[ts**(2/3)]/V(fpc=0)
C
      VETOL=1.0D-06
      DO IGAUST=1,NGAULT
       DTEMPT=0.0D0
       FPCHAN=0.0D0
       DFPCHA=0.0D0
       DO INODLT=1,NNODLT
        DTEMPT=DTEMPT+SHAPET(INODLT,IGAUST)*VELCMT(INODLT)
        FPCHAN=FPCHAN+SHAPET(INODLT,IGAUST)*FPCHLT(KPOROT,INODLT)
        DFPCHA=DFPCHA+SHAPET(INODLT,IGAUST)*FPCHLT(KPOROT+NNUPT,INODLT)
       ENDDO
       FPCHAA=FPCHAN-DFPCHA*DTIMET
C
       IF(FPCHAN.LT.FPCTOL.AND.FPCHAA.GT.FPCTOL) THEN
        IF(SPIPCT(5,IGAUST).GT.VETOL)
     .   SPIPCT(6,IGAUST)=SPIPCT(2,IGAUST)*
     .                (SPIPCT(1,IGAUST)**(2.0D0/3.0D0))/SPIPCT(5,IGAUST)
       ENDIF
      ENDDO
C
C**** POROSITY CRITERION 7:
C




C
C**** COMPUTE TOTAL VOLUME IF NECESSARY
C
      IF(IABS(KSGAUT).EQ.2) THEN       ! local smoothing
       FACTAT=0.D+00
       DO IGAUST=1,NGAULT
        FACTAT=FACTAT+DVOLUT(IGAUST)
       ENDDO
      ENDIF
C
C**** FOR 'ONE-POINT' INTEGRATION RULES
C
      IF(NGAULT.EQ.1) THEN
       DO IPOROT=1,NPOROT
        DO INODET=1,NNODST
         SPIPBT(IPOROT,INODET)=SPIPCT(IPOROT,INODET)
        ENDDO
       ENDDO       ! iporot=1,nporot
       RETURN
      ENDIF
C
C**** INTEGRAL PERFORMING
C
      DO IPOROT=1,NPOROT
       DO INODET=1,NNODST
        DO IGAUST=1,NGAULT
         SPIPAT(INODET,IPOROT)=SPIPAT(INODET,IPOROT)+
     .                         SHAPET(INODET,IGAUST)*
     .                         SPIPCT(IPOROT,IGAUST)*DVOLUT(IGAUST)
        ENDDO
       ENDDO
      ENDDO        ! iporot=1,nporot
C
C**** PERFORMS MATRIX PRODUCT
C
      DO IPOROT=1,NPOROT
       DO INODET=1,NNODST
        DO JNODET=1,NNODST
         SPIPBT(IPOROT,INODET)=SPIPBT(IPOROT,INODET)+
     .                      SPIPAT(JNODET,IPOROT)*EMASST(INODET,JNODET)
        ENDDO
       ENDDO
      ENDDO        ! iporot=1,nporot
C
      RETURN
      END
