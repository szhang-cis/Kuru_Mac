      SUBROUTINE EXPASST(CSTIF,LNODS,
     .                   NDOFN,NELEM,NEVAB,NNODE,NPOIN,
     .                   NPREL,NGRUP,NPROP,NMATS,
     .                 NDATA,NPREV,NSTAT,NMATX,NTOTV,NDIME,NTOTVM,NFPCH,
     .                   MATNO,PROEL,PROPS,ELDAT,ELPRE,
     .                   ELVAR,ELMAT,WORK1,DISTO,COORD,DISIT,
     .                   ADVEL,TEMPI,PREAS,TGAPS,DISPL,FPCHA,LACTI,
     .                   CSTII,CSTIT)
C***********************************************************************
C
C**** THIS ROUTINE ASSEMBLES DIAGONAL OF THE ELEMENTAL STIFFNESS MATRIX 
C     INTO THE GLOBAL DIAGONAL STIFFNESS MATRIX
C
C.... INPUT PARAMETERS
C
C     CSTIF(NEVAB,NEVAB)      - SQUARE ARRAY CONTAINING ELEMENT MATRIX
C     CSTII(NEVAB)            - ARRAY CONTAINING DIAGONAL ELEMENT MATRIX
C
C.... OUPUT PARAMETERS
C
C     CSTIT(NTOTV)            - ARRAY CONTAINING DIAGONAL GLOBAL MATRIX
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
C**** ADDITIONAL PARAMETERS
C
      INCLUDE 'addi_omt.f'
C
C**** THERMAL VARIABLES
C
      INCLUDE 'auxl_omt.f'
C
      DIMENSION CSTIF(NEVAB,NEVAB), LNODS(NNODE,*)
C
      DIMENSION MATNO(NELEM),
     .          PROEL(NPREL,NGRUP), PROPS(NPROP,NMATS),
     .          ELDAT(NDATA),       ELPRE(NPREV),
     .          ELVAR(NSTAT),       ELMAT(NMATX)
      DIMENSION DISTO(NTOTV,3),     COORD(NDIME,NPOIN),
     .          DISIT(NTOTV)
      DIMENSION ADVEL(NTOTV*NDIME), TEMPI(NPOIN,2)
      DIMENSION PREAS(NPOIN),       TGAPS(NPOIN)
      DIMENSION CSTII(NEVAB),       CSTIT(NTOTV)
      DIMENSION DISPL(NTOTVM),      FPCHA(NFPCH,NPOIN),
     .          LACTI(NELEM)
C
C**** INITIALISES GLOBAL DIAGONAL JACOBIAN MATRIX
C
      DO ITOTV=1,NTOTV
       CSTIT(ITOTV)=0.0
      ENDDO
C
C**** ASSEMBLE GLOBAL PRECONDITIONING MATRIX 
C
      DO 1000 IELEMT=1,NELEM
C
      NNODL=0
      DO INODE=1,NNODE
       IPOIN=LNODS(INODE,IELEMT)
       IF(IPOIN.NE.0) NNODL=NNODL+1
      ENDDO
C
C**** READ CSTIF FROM DATA BASE
C 
      IF(NMEMO7.EQ.0) THEN
       CALL DATBAST(CSTIF,    6,    2)
      ELSE
       CALL STIFMST(ELDAT,ELPRE,ELVAR,ELMAT,LNODS,MATNO,PROEL,
     .              PROPS,WORK1,DISTO(1,2),DISIT,COORD,
     .              ADVEL,TEMPI(1,1),PREAS,TGAPS,DISTO(1,1),
     .              TEMPI(1,2),DISPL,FPCHA,LACTI,CSTIF)
      ENDIF
C
C**** PUTS CSTIF ON CSTII
C
      DO IEVAB=1,NEVAB
       CSTII(IEVAB)=CSTIF(IEVAB,IEVAB)
      ENDDO
C
C**** SCATTERS CSTII > CSTIT
C
       CALL SCATER(CSTII,NDOFN,NNODL,CSTIT,NDOFN,NPOIN,
     .             LNODS(1,IELEMT))
C
 1000 CONTINUE
C
      RETURN
      END
