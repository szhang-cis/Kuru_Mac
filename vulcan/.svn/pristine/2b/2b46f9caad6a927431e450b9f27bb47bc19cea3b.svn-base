      SUBROUTINE SKYBAK(GSTDI,GSTLO,GSTUP,LPONT,NEQNS,REFOR,ITAPE)
C***********************************************************************
C
C**** THIS ROUTINE PERFORMS THE BACK-SOLUTION OF SYMMETRIC EQUATIONS
C     STORED IN PROFILE FORM .
C     COEFFICIENT MATRIX MUST BE DECOMPOSED INTO ITS TRIANGULAR
C     FACTORS USING SKYLIN BEFORE USING SKYBAK.
C
C.... INPUT PARAMETERS
C
C         GSTDI(NEQNS)       -RECIPROCAL OF DIAGONAL OF TRIANGULAR 
C                               FACTOR
C         GSTLO(LPONT(NEQNS) -LOWER TRIANGULAR FACTOR OF MATRIX
C         GSTUP(LPONT(NEQNS) -UPPER TRIANGULAR FACTOR OF MATRIX
C                               (GSTUP AND GSTLO HAVE SAME CALLING 
C                               ADDRESS FOR SYMMETRIC MATRICES)
C         REFOR(NEQNS)       -RIGHT HAND SIDE VECTOR IN EQUATIONS
C         LPONT(NEQNS)       -POINTER ARRAY TO BOTTOM OF COLUMNS 
C                               OF GSTLO AND GSTUP
C         NEQNS              -NUMBER OF EQUATIONS TO BE SOLVED
C
C.... OUTPUT PARAMETER
C
C         REFOR(NEQNS)       -SOLUTION OF EQUATIONS
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION GSTLO(*), GSTUP(*), GSTDI(*), REFOR(*), LPONT(*)
      DATA ZEROS/0.0D0/
C
C***FIND THE FIRST NONZERO ENTRY IN THE RIGHT HAND SIDE
C
      DO 100 KEQNS=1,NEQNS
      IEQNS=KEQNS
  100 IF(REFOR(IEQNS).NE.ZEROS) GO TO 200
      WRITE(ITAPE,2000)
      RETURN
C
  200 IF(IEQNS.LT.NEQNS) THEN
C
C***REDUCE THE RIGHT HAND SIDE
C
        DO 300 JEQNS=IEQNS+1,NEQNS
        JROWS=LPONT(JEQNS-1)
        JHEIG=LPONT(JEQNS)-JROWS
        IF(JHEIG.GT.0) THEN
          REFOR(JEQNS)=REFOR(JEQNS)-
     .       VECDOT(GSTLO(JROWS+1),REFOR(JEQNS-JHEIG),JHEIG)
        ENDIF
  300   CONTINUE
      ENDIF
C
C***MULTIPLY BY INVERSE OF DIAGONAL ELEMENTS
C
      DO 400 JEQNS=IEQNS,NEQNS
        REFOR(JEQNS)=REFOR(JEQNS)*GSTDI(JEQNS)
  400 CONTINUE
C
C***BACKSUBSTITUTION
C
      IF(NEQNS.GT.1) THEN
        DO 500 JEQNS=NEQNS,2,-1
        JROWS=LPONT(JEQNS-1)
        JHEIG=LPONT(JEQNS)-JROWS
        IF(JHEIG.GT.0) THEN
          KTERM=JEQNS-JHEIG
C$DIR NO_RECURRENCE
          DO 600 ITERM=1,JHEIG
            REFOR(KTERM)=REFOR(KTERM)-GSTUP(JROWS+ITERM)*REFOR(JEQNS)
  600       KTERM=KTERM+1
        ENDIF
  500   CONTINUE
      ENDIF
C
      RETURN
2000  FORMAT(' ***SKYBAK WARNING 1*** ZERO RIGHT-HAND-SIDE VECTOR')
      END
