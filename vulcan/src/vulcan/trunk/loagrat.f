      SUBROUTINE LOAGRAT(ELDATT,ELPRET,ELVART,ELMATT,LNODST,MATNOT,
     .                   PROELT,PROPST,RLOADT,RLOAHT,TEMPIT,IFLAGT,
     .                   WORK1T,NLOELT,ADVELT,COORDT,BOUCHT,FPCHAT,
     .                   DISPLT,LACTIT)
C***********************************************************************
C
C**** THIS ROUTINE EVALUATES THE EQUIVALENT NODAL FORCES DUE TO 
C     THE WEIGHT 
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
C**** ADDITIONAL PARAMETERS
C
      INCLUDE 'addi_omt.f'
C
C**** COUPLING VARIABLES
C
      INCLUDE 'nuec_om.f'
C
C**** THERMAL VARIABLES
C
      INCLUDE 'prob_omt.f'
      INCLUDE 'inte_omt.f'
      INCLUDE 'auxl_omt.f'
      INCLUDE 'inpo_omt.f'
C
      COMMON/LDFILET/ITAPET
      COMMON/SUVOHEAT/SVHEAT
C
      DIMENSION MATNOT(NELEMT),        LNODST(NNODET,NELEMT),
     .          PROELT(NPRELT,NGRUPT), PROPST(NPROPT,NMATST),
     .          ELDATT(NDATAT),        ELPRET(NPREVT),
     .          ELVART(NSTATT),        ELMATT(NMATXT),
     .          WORK1T(*)
      DIMENSION RLOADT(*),             RLOAHT(NTOTVT,NFUNCT),
     .          TEMPIT(NPOINT),        ADVELT(NTOTVT*NDIMET),
     .          COORDT(NDIMET,NPOINT), BOUCHT(NPOINT),
     .          FPCHAT(NFPCH,NPOINT),  DISPLT(NTOTVM),
     .          LACTIT(NELEMT)
C
      WRITE(LUREST,900)
C
C**** READ BY LISTENT
C
      DO 1000 ILOELT=1,NLOELT
       NPRINT=1
       CALL LISTENT('LOAGRAT',NPRINT,ITAPET)
       IELEMT=INT(PARAMT(1))
       IF(NACTIT.EQ.1) THEN
        IF(LACTIT(IELEMT).EQ.0) GO TO 1000    ! skip non active elements
       ENDIF
       IF(IELEMT.GT.NELEMT)
     .  CALL RUNENDT('LOAGRAT: ERROR IN ELEMENT NUMBER')
       IFUNCT=INT(PARAMT(2))
       IF(IFUNCT.GT.NFUNCT)
     .  CALL RUNENDT('LOAGRAT: ERROR IN FUNCTION NUMBER')
       LGRUPT=MATNOT(IELEMT)
       LMATST=INT(PROELT(1,LGRUPT))
       NNODLT=INT(PROELT(2,LGRUPT))
       SVHEAT=PARAMT(3)
C
C**** WRITE
C
       WRITE(LUREST,905) IELEMT,IFUNCT,SVHEAT
C
C**** CHECK THE CORRECT NTYPE
C
       ITYPET=INT(PROELT(5,LGRUPT))
       IF(IFLAGT.EQ.3.AND.ITYPET.NE.5)
     .  CALL RUNENDT('LOAGRAT: ERROR IN ELEMENT TYPE')
       IF(IFLAGT.EQ.4.AND.ITYPET.NE.101)
     .  CALL RUNENDT('LOAGRAT: ERROR IN ELEMENT TYPE')
C
C**** READ ELDAT FROM DATA-BASE
C
       IF(NMEMO1.EQ.0.OR.NMEMO2.EQ.0)
     .  CALL DATBAST(ELDATT,    1,    2)
C
C**** SCALAR GATHER OPERATIONS ( VELCTT ---> WORK1T(ILOSUT(16)) )
C
C     To include general source term (depending on temperature in
C     time (t + a dt))
C
c      IF(KDYNAT.EQ.1)
c    . CALL GATHER(VELCTT,NDOFCT,NPOINT,WORK1T(ILOSUT(16)),NDOFCT,
c    .             NNODLT,LNODST(1,IELEMT))
C
C**** GATHER NODAL COORDINATES
C
      IF(NMEMO1.EQ.1) THEN    ! coordinates in a global array
       CALL GATHER(COORDT,NDIMET,NPOINT,WORK1T(ILOSUT(17)),NDIMET,
     .             NNODLT,LNODST(1,IELEMT))
      ENDIF
C
C**** SCALAR GATHER OPERATIONS ( TEMPIT ---> WORK1T(ILOSUT(31)) )
C
      CALL GATHER(TEMPIT,NDOFCT,NPOINT,WORK1T(ILOSUT(31)),NDOFCT,NNODLT,
     .            LNODST(1,IELEMT))     ! initial temperature
C
C**** SCALAR GATHER OPERATIONS ( BOUCHT ---> WORK1T(ILOSUT(33)) )
C
      IF(NMEMO10.EQ.1)
     .  CALL GATHER(BOUCHT,NDOFCT,NPOINT,WORK1T(ILOSUT(33)),NDOFCT,
     .              NNODLT,
     .              LNODST(1,IELEMT))   ! boundary changes due to rho
C
C**** SCALAR GATHER OPERATIONS ( ADVELT ---> WORK1T(ILOSUT(32)) )
C
      IF(ICONVT.EQ.1)
     .CALL GATHER(ADVELT,NDIMET,NTOTVT,WORK1T(ILOSUT(32)),NDIMET,NNODLT,
     .            LNODST(1,IELEMT))
C
C**** GATHER PHASE-CHANGE VECTOR
C
      IF(NFILL.EQ.1)
     . CALL GATHER(FPCHAT,NFPCH,NPOINT,WORK1T(ILOSUT(34)),NFPCH,
     .             NNODLT,LNODST(1,IELEMT))
C
      IF(ITERME.GT.0) THEN          ! bidirectional coupled
       IF(ITERMD.EQ.1) THEN         ! deformed shape
C
C**** SCALAR GATHER OPERATIONS ( DISPLT ---> WORK1T(ILOSUT(37)) )
C
        CALL GATHER(DISPLT,NDOFCM,NPOINT,WORK1T(ILOSUT(37)),NDOFCM,
     .              NNODLT,
     .              LNODST(1,IELEMT))
       ENDIF                        ! itermd.eq.1
      ENDIF                         ! iterme.gt.0
C
C**** CALL ELEMENT PROCESSOR TO PERFORM THE REQUIRED OPERATIONS
C
       CALL ELMLIBT(LNODST(1,IELEMT),PROELT(1,LGRUPT),PROPST(1,LMATST),
     .              ELDATT,ELPRET,ELVART,ELMATT,DUMMYT,WORK1T,     7)
C
C**** SCALAR SCATTER OPERATION ( RLOADT )
C
       CALL SCATER(WORK1T,NDOFNT,NNODLT,RLOADT,NDOFCT,
     .             NPOINT,LNODST(1,IELEMT))
C
C**** SCALAR SCATTER OPERATION ( RLOAHT )
C
       CALL SCATER(WORK1T,NDOFNT,NNODLT,RLOAHT(1,IFUNCT),NDOFCT,
     .             NPOINT,LNODST(1,IELEMT))
C
 1000 CONTINUE
C
      WRITE(LUREST,915)
C
      RETURN
  900 FORMAT(//,10X,6H ELEM.,9X,6H FUNC.,6X,11H HEAT VALUE)
c 905 FORMAT(2(5X,I10),6X,F10.6)
  905 FORMAT(2(5X,I10),6X,E15.6)
  915 FORMAT(/)
      END
