      SUBROUTINE PLASJT(NSTR1,PROPS,SGTOT)
C***********************************************************************
C
C****THIS ROUTINE EVALUATES SHEAR STRESSES FOR "SMEARED" JOINT MATERIALS
C    ACCORDING TO A PLASTIC DRUCKER-PRAGER YIELD SURFACE
C
C    Input variables:
C      
C      PROPS(NPROP)        Material properties
C
C    Input/Output variables:
C
C      SGTOT(NSTR1)        Current total stresses
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'auxl_om.f'
C
      DIMENSION PROPS(*), SGTOT(*)
C
C***IF THE JOINT IS OPENED APPLY TENSION CUT-OFF
C
      IF(SGTOT(1).GE.0.0) THEN
        SGTOT(3)=0.0
        IF(NSTRE.GT.4) SGTOT(5)=0.0
        RETURN
      ENDIF
C
C***CHECK IF PLASTIC BEHAVIOUR TAKES PLACE
C
      CALL YLDJNT(PROPS,SGTOT,COMPA)
      CALL EFFJNT(SGTOT,YIELD,NSTRE)
      IF(YIELD.EQ.0.0D+00) RATIO=0.0D00
      IF(YIELD.NE.0.0D+00) RATIO=COMPA/YIELD
C
      IF(RATIO.GT.1.0D+00) RETURN    ! STILL ELASTIC OR UNLOADING
C
C***REDUCE SHEAR STRESSES ACCORING TO DRUCKER-PRAGER CRITERION
C
C     ( RADIAL RETURN )
C
      SGTOT(3)=RATIO*SGTOT(3)
      IF(NSTR1.GT.4) SGTOT(5)=RATIO*SGTOT(5)
C
      RETURN
      END
