      SUBROUTINE FRIN02(EHIST,ELCOD,ELDIS,PROPS,
     .                  STRAN,STRSG,STRA0,STRS0,BMSIG)
C***********************************************************************
C
C**** THIS ROUTINE EVALUATES THE RESISTING FORCES OF THE SOLID PHASE
C               ( FOR ELEMENT NO. 2 )
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
C
      DIMENSION EHIST(NHIST,*),       ELCOD(NDIME,*),
     .          ELDIS(NDOFC,*),       PROPS(*),
     .          STRAN(*),             STRSG(*),
     .          STRA0(*),             STRS0(*)
      DIMENSION BMSIG(*)
      DIMENSION STRET(3),DCOSI(3)
C
      DATA TWOPI/6.283185307179586/
C
C***SET UP THE ELASTIC MATRIX
C
      YOUNG= PROPS(2)
      AREAC= PROPS(11)
C
C***COMPUTE LENGTH OF THE ELEMENT AND DIRECTOR COSINES
C
      ALENG=0.0D00
      DO IDIME=1,NDIME
      ALENG=ALENG+(ELCOD(IDIME,1)-ELCOD(IDIME,2))**2
      ENDDO
      ALENG=DSQRT(ALENG)
C
      DO IDIME=1,NDIME
      DCOSI(IDIME)=(ELCOD(IDIME,2)-ELCOD(IDIME,1))/ALENG
      ENDDO
C
C***CALCULATE THE MATERIAL STIFFNESS OF THE ELEMENT
C
      FACTR=YOUNG*AREAC/ALENG
      IF(NTYPE.EQ.3)THEN
        RADUS=(ELCOD(1,1)+ELCOD(1,2))/2.0
        FACTR=FACTR*RADUS*TWOPI
      ENDIF
C
C***COMPUTE STRETCH OF THE ELEMENT
C
      STREC=0.0D00
      DO IDIME=1,NDIME
        STRET(IDIME)=ELDIS(IDIME,2)-ELDIS(IDIME,1)
        STREC=STREC+STRET(IDIME)*DCOSI(IDIME)
      ENDDO
      STRAN(1)=STREC
C
C***EVALUATE ELEMENT CONTRIBUTION
C
      FORCE=FACTR*STREC
      STRSG(1)=FORCE/AREAC
C
      DO IDIME=1,NDIME
       BMSIG(IDIME)      =-FORCE*DCOSI(IDIME)
       BMSIG(IDIME+NDIME)= FORCE*DCOSI(IDIME)
      ENDDO
C
      RETURN
      END
