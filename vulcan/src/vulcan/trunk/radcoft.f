      SUBROUTINE RADCOFT(BASHH,PROPST,TGAUS1,TGAUS2,PSEUDO1,PSEUDO2,
     .                   INDEP)
C***********************************************************************
C
C**** THIS ROUTINE EVALUATES THE CONVECTION-RADIATION COEFFICIENT
C
C***********************************************************************
C
C     BASHH= Convection-radiation coefficient
C
C     INDEP=1    >>    elm104
C     INDEP=2    >>    elm101
C     INDEP=3    >>    elm101 transformed from elm104
C
C***********************************************************************
      IMPLICIT REAL*8 (A-H,O-Z)
C
      INCLUDE 'prob_omt.f'
      INCLUDE 'inte_omt.f'
      INCLUDE 'auxl_omt.f'
C
      DIMENSION PROPST(*)
C
C**** CONVECTION RADIATION COEFFICIENT: ONLY TEMPERATURE-DEPENDENT
C     (BODY 1) FOR BOUNDARY & GAP ELEMENTS (101 & 104)
C
      IF(INDEP.EQ.1.OR.INDEP.EQ.2) THEN
       IF(TGAUS1.LE.VRADH1(1,2)) BASHT1=VRADH1(1,1)
       DO IRADH1=2,NRADH1
        I1=IRADH1-1
        I2=IRADH1
        IF(TGAUS1.GT.VRADH1(I1,2).AND.TGAUS1.LE.VRADH1(I2,2)) 
     .   BASHT1=(VRADH1(I2,1)-VRADH1(I1,1))/(VRADH1(I2,2)-VRADH1(I1,2))*
     .          (TGAUS1-VRADH1(I1,2))+VRADH1(I1,1)
       ENDDO
       IF(TGAUS1.GT.VRADH1(NRADH1,2)) BASHT1=VRADH1(NRADH1,1)
      ENDIF                     ! indep.eq.1.or.indep.eq.2
C
      IF(INDEP.EQ.3) THEN
       IF(TGAUS1.LE.VRADH3(1,2)) BASHT1=VRADH3(1,1)
       DO IRADH3=2,NRADH3
        I1=IRADH3-1
        I2=IRADH3
        IF(TGAUS1.GT.VRADH3(I1,2).AND.TGAUS1.LE.VRADH3(I2,2)) 
     .   BASHT1=(VRADH3(I2,1)-VRADH3(I1,1))/(VRADH3(I2,2)-VRADH3(I1,2))*
     .          (TGAUS1-VRADH3(I1,2))+VRADH3(I1,1)
       ENDDO
       IF(TGAUS1.GT.VRADH3(NRADH3,2)) BASHT1=VRADH3(NRADH3,1)
      ENDIF                     ! indep.eq.3
C
      IF(INDEP.EQ.2.OR.INDEP.EQ.3) THEN
       BASHT2=1.0D0
      ELSE
C
C**** CONVECTION RADIATION COEFFICIENT: ONLY TEMPERATURE-DEPENDENT
C     (BODY 2) FOR GAP ELEMENTS (104)
C
       IF(TGAUS2.LE.VRADH2(1,2)) BASHT2=VRADH2(1,1)
       DO IRADH2=2,NRADH2
        I1=IRADH2-1
        I2=IRADH2
        IF(TGAUS2.GT.VRADH2(I1,2).AND.TGAUS2.LE.VRADH2(I2,2)) 
     .   BASHT2=(VRADH2(I2,1)-VRADH2(I1,1))/(VRADH2(I2,2)-VRADH2(I1,2))*
     .          (TGAUS2-VRADH2(I1,2))+VRADH2(I1,1)
       ENDDO
       IF(TGAUS2.GT.VRADH2(NRADH2,2)) BASHT2=VRADH2(NRADH2,1)
      ENDIF                     ! indep.eq.2.or.indep.eq.3
C
C**** COMPUTE CONVECTION-RADIATION COEFFICIENT
C
      BASHH=BASHT1*BASHT2
C
C**** DEALS WITH FILLING MATERIAL
C
      IF(IFILL.EQ.0) RETURN
C
      PSEUDO=PSEUDO1                       ! simplification when INDEP=2
C
C**** CONVECTION RADIATION COEFFICIENT: ONLY TEMPERATURE-DEPENDENT
C     (BODY 1) FOR BOUNDARY & GAP ELEMENTS (101 & 104)
C
      IF(INDEP.EQ.1.OR.INDEP.EQ.2) THEN
       IF(TGAUS1.LE.VRADH1FI(1,2)) BASHT1=VRADH1FI(1,1)
       DO IRADH1=2,NRADH1FI
        I1=IRADH1-1
        I2=IRADH1
        IF(TGAUS1.GT.VRADH1FI(I1,2).AND.TGAUS1.LE.VRADH1FI(I2,2))
     .   BASHT1=(VRADH1FI(I2,1)-VRADH1FI(I1,1))/
     .          (VRADH1FI(I2,2)-VRADH1FI(I1,2))*
     .          (TGAUS1-VRADH1FI(I1,2))+VRADH1FI(I1,1)
       ENDDO
       IF(TGAUS1.GT.VRADH1FI(NRADH1FI,2)) BASHT1=VRADH1FI(NRADH1FI,1)
      ENDIF                     ! indep.eq.1.or.indep.eq.2
C
      IF(INDEP.EQ.2.OR.INDEP.EQ.3) THEN
       BASHT2=1.0D0
      ELSE
C
C**** CONVECTION RADIATION COEFFICIENT: ONLY TEMPERATURE-DEPENDENT
C     (BODY 2) FOR GAP ELEMENTS (104)
C
       IF(TGAUS2.LE.VRADH2FI(1,2)) BASHT2=VRADH2FI(1,1)
       DO IRADH2=2,NRADH2FI
        I1=IRADH2-1
        I2=IRADH2
        IF(TGAUS2.GT.VRADH2FI(I1,2).AND.TGAUS2.LE.VRADH2FI(I2,2))
     .   BASHT2=(VRADH2FI(I2,1)-VRADH2FI(I1,1))/
     .          (VRADH2FI(I2,2)-VRADH2FI(I1,2))*
     .          (TGAUS2-VRADH2FI(I1,2))+VRADH2FI(I1,1)
       ENDDO
       IF(TGAUS2.GT.VRADH2FI(NRADH2FI,2)) BASHT2=VRADH2FI(NRADH2FI,1)
      ENDIF                     ! indep.eq.2.or.indep.eq.3
C
C**** COMPUTE CONVECTION-RADIATION COEFFICIENT
C
      BASHHFI=BASHT1*BASHT2
C
C**** COMPUTES THE WEIGHTED CONVECTION-RADIATION COEFFICIENT
C
      BASHH=PSEUDO*BASHH+(1.0D0-PSEUDO)*BASHHFI
C
      RETURN
      END
