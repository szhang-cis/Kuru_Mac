      SUBROUTINE PLFLOW(VECTF,VECFL,ABETA,DMATP,HARDS,NSTRS,NTYPE,
     .                  PROPS,VECTG,VECGL,DGVEC,NSTR1,
     .                  LARGE,IFREN,NDIME,XJACM,XJA3M,TRMTX)
C***********************************************************************
C
C**** THIS SUBROUTINE EVALUATES THE PLASTIC D VECTOR
C
C***********************************************************************
      IMPLICIT REAL*8 (A-H,O-Z)
C
      DIMENSION VECTF(*), DMATP(6,*), DGVEC(*), PROPS(*),
     .          VECTG(*), VECFL(*),   VECGL(*), XJACM(NDIME,*)
      DIMENSION TRMTX(NDIME,*), VECGX(6), VECFX(6)
C
C**** ROTATES (BACK) PLASTIC FLUX & NORMAL TENSORS FROM LOCAL TO GLOBAL
C     (MATERIAL) SYSTEM OF COORDINATES (THIS IS NEEDED FOR ORTHOTROPIC
C     MATERIALS)
C
      DO ISTR1=1,NSTR1
       VECGX(ISTR1)=VECGL(ISTR1)
       VECFX(ISTR1)=VECFL(ISTR1)
      ENDDO
      ISOTR=INT(PROPS(1))   ! 0=isotropic; 1=orthotropic
      IF(ISOTR.NE.0) THEN
       IF(IFREN.EQ.2.OR.IFREN.EQ.3) THEN
        XJA3M=1.0D0
        DETJX=1.0D0
        CALL PUFOBA(VECGL,VECGX,TRMTX,XJA3M,DETJX,    2)
        CALL PUFOBA(VECFL,VECFX,TRMTX,XJA3M,DETJX,    2)
       ELSE
        CALL RUNEND('ERROR: IFREN NE 2 OR 3 NOT IMPLEMENTED')
       ENDIF
      ENDIF
C
      IF(LARGE.EQ.0) THEN
       DO ISTR1=1,NSTR1
        VECTG(ISTR1)=VECGX(ISTR1)
        VECTF(ISTR1)=VECFX(ISTR1)
       ENDDO
      ELSE
C
C**** TRANSFORMS BACK PLASTIC FLUX & NORMAL TENSORS
C
       IF(IFREN.EQ.1.OR.IFREN.EQ.4) THEN                     ! no change
        DETJX=1.0D0
        CALL PUFOBA(VECGX,VECTG,XJACM,XJA3M,DETJX,    3)
        CALL PUFOBA(VECFX,VECTF,XJACM,XJA3M,DETJX,    3)
       ENDIF
       IF(IFREN.EQ.2.OR.IFREN.EQ.3.OR.IFREN.EQ.5.OR.
     .    IFREN.EQ.6.OR.IFREN.EQ.7.OR.IFREN.EQ.8.OR.
     .    IFREN.EQ.9.OR.IFREN.EQ.10) THEN
        DETJX=1.0D0
        CALL PUFOBA(VECGX,VECTG,XJACM,XJA3M,DETJX,    2)
        CALL PUFOBA(VECFX,VECTF,XJACM,XJA3M,DETJX,    2)
       ENDIF
      ENDIF        ! large.eq.0
C
C**** COMPUTE "DGVEC=(DMATP*VECTG)" PRODUCT
C
      CALL PLPDXV(VECTG,DMATP,DGVEC,NSTRS,NTYPE,ISOTR,NSTR1)
C
C**** COMPUTE THE DENOMINATOR OF CONSISTENT PLASTIC FACTOR
C
      DENOM=HARDS
      DO ISTRS=1,NSTRS
       DENOM=DENOM+VECTF(ISTRS)*DGVEC(ISTRS)
      ENDDO
      ABETA=1.0D0/DENOM
C
      RETURN
      END
