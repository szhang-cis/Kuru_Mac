      SUBROUTINE OUTGAU(ELDAT,ELPRE,ELVAR,ELMAT,
     .                  LNODS,MATNO,PROEL,PROPS,COORD,DISTO,
     .                  WORK1)
C***********************************************************************
C
C**** THIS ROUTINE OUTPUTS GAUSSIAN VARIABLES TO OUTPUT AND POST. FILES
C
C       KPRI2=0  DO NOT WRITE GAUSSIAN STRESSES TO OUTPUT FILE
C             1  WRITE GAUSSIAN STRESSES TO OUTPUT FILES
C       KPRI3=0  DO NOT WRITE GAUSSIAN PRINCIPAL STRESSES TO OUTPUT FILE
C             1  WRITE GAUSSIAN PRINCIPAL STRESSES TO OUTPUT FILES
C       KPRI4=0  DO NOT WRITE GAUSSIAN INTERNAL VARIABLES TO OUTPUT FILE
C             1  WRITE GAUSSIAN INTERNAL VARIABLES TO OUTPUT FILES
C       KFEMV=0  DO NOT WRITE TO POSTPROCESSOR FILE
C             1  WRITE TO POSTPROCESSOR FILE
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
C**** ADDITIONAL PARAMETERS
C
      INCLUDE 'addi_om.f'
C
C**** MECHANICAL VARIABLES
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
      INCLUDE 'inpo_om.f'
C
      DIMENSION MATNO(NELEM),       LNODS(NNODE,NELEM),
     .          PROEL(NPREL,NGRUP), PROPS(NPROP,NMATS),
     .          ELDAT(NDATA),       ELPRE(NPREV),
     .          ELVAR(NSTAT),       ELMAT(NMATX)
      DIMENSION COORD(NDIME,NPOIN), DISTO(*)
      DIMENSION WORK1(*)
C
C**** OUTPUT GAUSSIAN VARIABLES
C
      IF(IPRCO.GT.0) THEN
       IF((KPRI2+KPRI3+KPRI4).GT.0) WRITE(LURES,955)
      ENDIF
C
C**** LOOP OVER THE ELEMENTS
C
      DO IELEM=1,NELEM
       IF(IPRCO.GT.0) THEN
        LGRUP=MATNO(IELEM)
        LMATS=INT(PROEL(1,LGRUP))
        NNODL=INT(PROEL(2,LGRUP))
C
C**** READ ELDAT FROM DATA BASE
C
        IF(NMEMO1M.EQ.0.OR.NMEMO2M.EQ.0)
     .   CALL DATBAS(ELDAT, 1, 2)
C
C**** READ ELVAR FROM DATA BASE
C
        CALL DATBAS(ELVAR, 3, 2)
C
C**** GATHER NODAL COORDINATES
C
        IF(NMEMO1M.EQ.1) THEN    ! coordinates in a global array
         CALL GATHER(COORD,NDIME,NPOIN,WORK1(IGSMO(25)),NDIME,
     .               NNODL,LNODS(1,IELEM))
        ENDIF
C
        IF((KPRI2+KPRI3+KPRI4).GT.0) THEN
C
         IF(LARGE.NE.0) THEN
          IF(NMEMO5M.EQ.0) THEN
C
C**** SCALAR GATHER OPERATIONS ( DISTO ---> ELDIS(ELVAR) )
C
           CALL GATHER(DISTO,NDOFC,NPOIN,ELVAR,           NDOFC,NNODL,
     .                 LNODS(1,IELEM))
          ELSE
C
C**** SCALAR GATHER OPERATIONS ( DISTO ---> ELDIS )
C
           CALL GATHER(DISTO,NDOFC,NPOIN,WORK1(IGSMO(27)),NDOFC,NNODL,
     .                 LNODS(1,IELEM))
          ENDIF                   ! nmemo5m.eq.0
         ENDIF                    ! large.ne.0
C
C**** CALL THE ELEMENT PROCESSOR TO WRITE TO OUTPUT FILE
C
         CALL ELMLIB(LNODS(1,IELEM),PROEL(1,LGRUP),PROPS(1,LMATS),
     .               WORK1,WORK1,WORK1,WORK1,WORK1,
     .               ELDAT,ELPRE,ELVAR,ELMAT,WORK1,   12)
        ENDIF           ! kpri2+kpri3+...
       ENDIF            ! iprco.gt.0
C
C**** WRITE TO POSTPROCESSOR FILE
C
       IF(KPPCG.NE.0) THEN
        IF(KFEMV.NE.0)
     .   WRITE(LUPOS) (SNGL(ELVAR(JSTAT)),JSTAT=1,NSTAT)
       ENDIF
C
      ENDDO
C
      RETURN
  955 FORMAT(/,5X,' INFORMATION AT GAUSSIAN INTEGRATION POINTS')
      END
