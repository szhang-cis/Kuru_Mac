      SUBROUTINE KLARGB(CARTD,DVOLU,ESTIF,GPCOD,KSYMM,NDIME,NDOFN,
     .                  NEVAB,NNODE,NTYPE,SHAPE,SIGMA,
     .                  CMEAN,NSTR1,BMATX,NSTRE,NSTRS,ELDIS,NDOFC,
     .                  XJACM,XJACI,XJA3M,XJA3I,DETJM,DEFRA,
     .                  XJACN,XJANI,XJA3N,XJ3NI,DETJN,
     .                  KPARB)
C***********************************************************************
C
C**** THIS ROUTINE CALCULATES THE ADDITIONAL STIFFNESS TERMS DUE TO
C     THE FINITE LAGRANGE STRAIN FORMULATION
C
C     Notes:
C
C     CMEAN in this routine has the dimensions of BSBAR
C     LARGE flag independent computation of ESTIF is attempted for large
C     strains
C
C     To be improved for B-bar & B-bar-shear (commented below)
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      DIMENSION CARTD(NDIME,*), ESTIF(*), SHAPE(*), SIGMA(*)
      DIMENSION GPCOD(*)
      DIMENSION CMEAN(NSTR1,*)
      DIMENSION XJACM(NDIME,*), XJACN(NDIME,*)
      DIMENSION XJACI(NDIME,*), XJANI(NDIME,*)
      DIMENSION BMATX(NSTRE,*), ELDIS(NDOFC,*)
      DIMENSION CARTA(3,27)
C
C**** PRELIMINARY COMPUTATIONS
C
      DEFR2=DEFRA*DEFRA
      B13=1.0D0/3.0D0
      B23=2.0D0/3.0D0
C
      GO TO (21,22,23,21,22,23,21,22,23,
     .       24,24,24,25,25,25,26,26,26,
     .       21,21,21,27,27,27), KPARB
C
C**** B-BAR (SMOOTHING TECH., AVERAGE TECH. & SELECT. INTEGRATION)
C
c  21 CALL BBMATXL(CARTD,ELDIS,GPCOD,
c    .             NDIME,NDOFC,NDOFN,NEVAB,NNODE,NSTRE,
c    .             NTYPE,SHAPE,NSTR1,NSTRS,BMATX,BSBAR,
c    .             XJACM,XJA3M,DETJM,DEFRA,
c    .             XJACN,XJA3N,DETJN,
c    .             KPARB)
C
   21 DO IDIME=1,NDIME
       DO INODE=1,NNODE
        CARTA(IDIME,INODE)=CARTD(IDIME,INODE)
       ENDDO
      ENDDO
      GO TO 34
C
C**** B-SHEAR (SMOOTHING TECH., AVERAGE TECH. & SELECT. INTEGRATION)
C
c  22 CALL BBMATYL(CARTD,ELDIS,GPCOD,
c    .             NDIME,NDOFC,NDOFN,NEVAB,NNODE,NSTRE,
c    .             NTYPE,SHAPE,NSTR1,NSTRS,BMATX,BSBAR,
c    .             XJACM,XJA3M,DETJM,
c    .             XJACN,XJA3N,DETJN)
C
   22 DO IDIME=1,NDIME
       DO INODE=1,NNODE
        CARTA(IDIME,INODE)=CMEAN(IDIME,INODE)
       ENDDO
      ENDDO
      GO TO 34
C
C**** B-BAR-SHEAR (SMOOTHING TECH., AVERAGE TECH. & SELECT. INTEGRATION)
C
c  23 CALL BBMATZL(CARTD,ELDIS,GPCOD,
c    .             NDIME,NDOFC,NDOFN,NEVAB,NNODE,NSTRE,
c    .             NTYPE,SHAPE,NSTR1,NSTRS,BMATX,BSBAR,
c    .             XJACM,XJA3M,DETJM,DEFRA,
c    .             XJACN,XJA3N,DETJN)
C
   23 DO IDIME=1,NDIME
       DO INODE=1,NNODE
        CARTA(IDIME,INODE)=CMEAN(IDIME,INODE)
       ENDDO
      ENDDO
      GO TO 34
C
C**** OTHER B-BAR (SMOOTHING TECH., AVERAGE TECH. & SELECT. INTEGRATION)
C
c  24 CALL BBMATWL(CARTD,ELDIS,GPCOD,
c    .             NDIME,NDOFC,NDOFN,NEVAB,NNODE,NSTRE,
c    .             NTYPE,SHAPE,NSTR1,NSTRS,BMATX,BSBAR,
c    .             XJACM,XJA3M,DETJM,
c    .             XJACN,XJA3N,DETJN)
C
   24 DO IDIME=1,NDIME
       DO INODE=1,NNODE
        CARTA(IDIME,INODE)=CARTD(IDIME,INODE)
       ENDDO
      ENDDO
      GO TO 34
C
C**** B-e_SHEAR (SMOOTHING TECH., AVERAGE TECH. & SELECT. INTEGRATION)
C
c  25 CALL BBMATYLS(CARTD,ELDIS,GPCOD,
c    .             NDIME,NDOFC,NDOFN,NEVAB,NNODE,NSTRE,
c    .             NTYPE,SHAPE,NSTR1,NSTRS,BMATX,BSBAR,
c    .             XJACM,XJA3M,DETJM,
c    .             XJACN,XJA3N,DETJN,
c    .             XJACI,XJA3I,
c    .             XJANI,XJ3NI)
C
   25 DO IDIME=1,NDIME
       DO INODE=1,NNODE
c       CARTA(IDIME,INODE)=CMEAN(IDIME,INODE)
        CARTA(IDIME,INODE)=CARTD(IDIME,INODE)
       ENDDO
      ENDDO
      GO TO 34
C
C**** B-(e_SHEAR-BAR) (SMOOTHING TECH., AVERAGE TECH. & SELECT. INTEG.)
C
c  26 CALL BBMATZLS(CARTD,ELDIS,GPCOD,
c    .             NDIME,NDOFC,NDOFN,NEVAB,NNODE,NSTRE,
c    .             NTYPE,SHAPE,NSTR1,NSTRS,BMATX,BSBAR,
c    .             XJACM,XJA3M,DETJM,DEFRA,
c    .             XJACN,XJA3N,DETJN,
c    .             XJACI,XJA3I,
c    .             XJANI,XJ3NI)
C
   26 DO IDIME=1,NDIME
       DO INODE=1,NNODE
c       CARTA(IDIME,INODE)=CMEAN(IDIME,INODE)
        CARTA(IDIME,INODE)=CARTD(IDIME,INODE)
       ENDDO
      ENDDO
      GO TO 34
C
C**** J-BAR (SMOOTHING TECH., AVERAGE TECH. & SELECT. INTEG.)
C
   27 DO IDIME=1,NDIME
       DO INODE=1,NNODE
c       CARTA(IDIME,INODE)=CMEAN(IDIME,INODE)
        CARTA(IDIME,INODE)=CARTD(IDIME,INODE)
       ENDDO
      ENDDO
      GO TO 34
C
   34 CONTINUE
C
      GOTO (1,1,3,4,5),NTYPE
C
C**** PLANE STRAIN & STRESS
C
    1 DO INODE=1,NNODE
       KEVAB=(INODE-1)*NDOFN
       DO JNODE=INODE,NNODE
        JEVAB=(JNODE-1)*NDOFN
C
        ACOEF1=SIGMA(1)*CARTD(1,JNODE)
        ACOEF2=SIGMA(3)*CARTA(2,JNODE)
C
        BCOEF1=SIGMA(3)*CARTA(1,JNODE)
        BCOEF2=SIGMA(2)*CARTD(2,JNODE)
C
        CONST=(ACOEF1*CARTD(1,INODE)+ACOEF2*CARTA(1,INODE)+
     .         BCOEF1*CARTA(2,INODE)+BCOEF2*CARTD(2,INODE))*DVOLU*DEFR2
C
        DO IDOFN=1,NDOFN
         IEVAB=KEVAB+IDOFN
         JEVAB=JEVAB+1
         KLOCA=(2*NEVAB-IEVAB)*(IEVAB-1)/2+JEVAB
         IF(KSYMM.EQ.0) KLOCA=(IEVAB-1)*NEVAB+JEVAB  ! unsymmetric
         ESTIF(KLOCA)=ESTIF(KLOCA)+CONST
        ENDDO
       ENDDO
      ENDDO
C
      GO TO 100
C
C**** AXI-SYMMETRIC
C
    3 GPCO2=GPCOD(1)*GPCOD(1)
      DO INODE=1,NNODE
       KEVAB=(INODE-1)*NDOFN
       DO JNODE=INODE,NNODE
        JEVAB=(JNODE-1)*NDOFN
C
        ACOEF1=SIGMA(1)*CARTD(1,JNODE)
        ACOEF2=SIGMA(3)*CARTA(2,JNODE)
C
        BCOEF1=SIGMA(3)*CARTA(1,JNODE)
        BCOEF2=SIGMA(2)*CARTD(2,JNODE)
C
        CONST=(ACOEF1*CARTD(1,INODE)+ACOEF2*CARTA(1,INODE)+
     .         BCOEF1*CARTA(2,INODE)+BCOEF2*CARTD(2,INODE))*DVOLU*DEFR2
        CCOEF=SIGMA(4)*SHAPE(INODE)*SHAPE(JNODE)/GPCO2
C
        DO IDOFN=1,NDOFN
         IEVAB=KEVAB+IDOFN
         JEVAB=JEVAB+1
         KLOCA=(2*NEVAB-IEVAB)*(IEVAB-1)/2+JEVAB
         IF(KSYMM.EQ.0) KLOCA=(IEVAB-1)*NEVAB+JEVAB
         DCOEF=0.0D0
         IF(IDOFN.EQ.1) DCOEF=CCOEF*DVOLU*DEFR2
         ESTIF(KLOCA)=ESTIF(KLOCA)+CONST+DCOEF 
        ENDDO
       ENDDO
      ENDDO
C
      GO TO 100
C
C**** 3-D CASE
C
    4 DO INODE=1,NNODE
       KEVAB=(INODE-1)*NDOFN
       DO JNODE=INODE,NNODE
        JEVAB=(JNODE-1)*NDOFN
C
        ACOEF1=SIGMA(1)*CARTD(1,JNODE)
        ACOEF2=SIGMA(3)*CARTA(2,JNODE)
        ACOEF3=SIGMA(5)*CARTA(3,JNODE)
C
        BCOEF1=SIGMA(3)*CARTA(1,JNODE)
        BCOEF2=SIGMA(2)*CARTD(2,JNODE)
        BCOEF3=SIGMA(6)*CARTA(3,JNODE)
C
        CCOEF1=SIGMA(5)*CARTA(1,JNODE)
        CCOEF2=SIGMA(6)*CARTA(2,JNODE)
        CCOEF3=SIGMA(4)*CARTD(3,JNODE)
C
        CONST=(ACOEF1*CARTD(1,INODE)+ACOEF2*CARTA(1,INODE)+
     .         ACOEF3*CARTA(1,INODE)+
     .         BCOEF1*CARTA(2,INODE)+BCOEF2*CARTD(2,INODE)+
     .         BCOEF3*CARTA(2,INODE)+
     .         CCOEF1*CARTA(3,INODE)+CCOEF2*CARTA(3,INODE)+
     .         CCOEF3*CARTD(3,INODE))*DVOLU*DEFR2
C
        DO IDOFN=1,NDOFN
         IEVAB=KEVAB+IDOFN
         JEVAB=JEVAB+1
         KLOCA=(2*NEVAB-IEVAB)*(IEVAB-1)/2+JEVAB
         IF(KSYMM.EQ.0) KLOCA=(IEVAB-1)*NEVAB+JEVAB
         ESTIF(KLOCA)=ESTIF(KLOCA)+CONST
        ENDDO
       ENDDO
      ENDDO
C
      GO TO 100
C
C**** 1-D CASE
C
    5 DO INODE=1,NNODE
       KEVAB=(INODE-1)*NDOFN
       DO JNODE=INODE,NNODE
        JEVAB=(JNODE-1)*NDOFN
        CONST=SIGMA(1)*CARTD(1,JNODE)*CARTD(1,INODE)*DVOLU*DEFR2
        DO IDOFN=1,NDOFN
         IEVAB=KEVAB+IDOFN
         JEVAB=JEVAB+1
         KLOCA=(2*NEVAB-IEVAB)*(IEVAB-1)/2+JEVAB
         IF(KSYMM.EQ.0) KLOCA=(IEVAB-1)*NEVAB+JEVAB
         ESTIF(KLOCA)=ESTIF(KLOCA)+CONST
        ENDDO
       ENDDO
      ENDDO
C
C**** COMPLETE MATRIX FOR RECTANGULAR CASE
C
  100 IF(KSYMM.EQ.0) THEN
       DO IEVAB=1,NEVAB
        DO JEVAB=IEVAB,NEVAB
         KLOCS=(IEVAB-1)*NEVAB+JEVAB
         KLOCI=(JEVAB-1)*NEVAB+IEVAB
         ESTIF(KLOCI)=ESTIF(KLOCS)
        END DO
       END DO
      END IF
C
      RETURN
      END
