      SUBROUTINE PLASTI(AINTE,DEPSV,DESIG,DVSPE,EHIST,ESCUR,KGAUS,
     .                  KUNLD,NCRIT,NSTR1,NTYPE,PRESG,PROPS,SGTOT,
     .                  SIGMA,VSTOT)
C***********************************************************************
C
C****THIS ROUTINE CALCULATES THE REDUCTION FACTOR
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION DESIG(*), EHIST(*), PREDV(6), PRESG(*),
     .          PROPS(*), SIGDV(6), SIGMA(*), SGTOT(*)
C
C***CALCULATE THE EQUIVALENT STRESS
C
      CALL EQUIVA(EFFST,EHIST,NCRIT,NTYPE,PREDV,PREJ2,PREJ3,
     .            PREMN,PRESG,PREYS,PROPS,SIGYS,SIGMA)
C
C***CHECK IF PLASTICITY HAS OCCURED
C
      REDUC=1.0
C
      ESPRE=PREYS-EFFST
      DESPR=DABS(ESPRE)
      IF(DESPR.LT.1.E-5) ESPRE=0.0
      IF(ESPRE.GE.0.)GOTO 10
C
C***STRESS POINT WAS INSIDE THE YIELD SURFACE
C
      ESCUR=SIGYS-EFFST
      DESCU=DABS(ESCUR)
      IF(DESCU.LT.1.E-5) ESCUR=0.0
      IF(ESCUR.LE.0.)GOTO 20
C
      CALL FACTOR(DESIG,EHIST,KGAUS,NCRIT,NTYPE,PREDV,PREMN,
     .            PREJ2,PREJ3,PROPS,REDUC)
      GOTO 20
C
C***STRESS POINT WAS ON THE YIELD SURFACE
C
   10 ESCUR=SIGYS-PREYS
      DESCU=DABS(ESCUR)
      IF(DESCU.LT.1.E-5) ESCUR=0.0
      IF(ESCUR.GT.0.) REDUC=0.0
      IF(ESCUR.LE.0.) KUNLD=1
C
   20 CONTINUE
      EHIST(1)=ESCUR
C
C***REDUCE THE STRESS ON THE YIELD SURFACE
C
      RFACT=1.-REDUC
      DO 40 ISTR1=1,NSTR1
      SGTOT(ISTR1)=PRESG(ISTR1)+REDUC*DESIG(ISTR1)
      DESIG(ISTR1)=RFACT*DESIG(ISTR1)
   40 CONTINUE
C
      VSTOT=(1.+REDUC*DEPSV)*EHIST(2)
C
      IF(ESCUR.LE.0.)RETURN
C
      DVSPE=RFACT*DEPSV*EHIST(2)
C
C***FIRST ESTIMATION OF THE NUMBER OF STEPS FOR ELASTO-PLASTIC EQUIL.
C
      DENOM=EFFST
      IF(DENOM.EQ.0.)DENOM=SIGYS
      AINTE=ESCUR/DENOM
C
      RETURN
      END
