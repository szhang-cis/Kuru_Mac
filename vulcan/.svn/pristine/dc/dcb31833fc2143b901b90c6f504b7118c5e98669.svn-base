      SUBROUTINE EQPLAS(AINTE,DESIG,DMATX,DVSPE,EHIST,KGAUS,NBACK,
     .                  NCRIT,NSTRE,NSTR1,NTYPE,PROPS,SGTOT,VSTOT)
C***********************************************************************
C
C****THIS ROUTINE REDUCES THE STRESS STEPWISE ON THE SURFACE
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION AVECT(6), DESIG(*), DMATX(NSTR1,*), DVECT(6),
     .          EHIST(*), PROPS(*), SGTOT(*),       STRES(6)
      NSTRS=NSTRE
      IF(NTYPE.EQ.2) NSTRS=NSTR1
C
C***INSIDE CRITICAL SURFACE
C
      CALL STEPLA(AINTE,KGAUS,MINTE)
      AINTE=MINTE
      DO 1000 IINTE=1,MINTE
      CALL YIELDS(AVECT,DEFPI,EHIST,HARDS,NCRIT,NSTR1,NTYPE,
     .            PROPS,SGTOT,VSTOT)
      CALL FLOWPL(AVECT,ABETA,DMATX,DVECT,HARDS,NSTRS,NTYPE,
     .            PROPS)
C
C***CALULATE PLASTIC SCALAR
C
      AGASH=0.0
      DO 30 ISTR1=1,NSTRS
   30 AGASH=AGASH+AVECT(ISTR1)*DESIG(ISTR1)/AINTE
      DLAMD=AGASH*ABETA
      IF(DLAMD.LT.0.0) DLAMD=0.0
C
C***CALCULATE THE EQUIL. STRESS COMPATIBALE WITH THE CURRENT DISPLACEMENT
C
      DO 40 ISTRS=1,NSTRS
      STRES(ISTRS)=DESIG(ISTRS)/AINTE-DLAMD*DVECT(ISTRS)
      SGTOT(ISTRS)=SGTOT(ISTRS)+STRES(ISTRS)
   40 CONTINUE
C
C***CALCULATE THE NEW POSITION OF THE YIELD SURFACE
C
      DVPLA=DLAMD*DEFPI
      EHIST(6)=EHIST(6)+DVPLA
      CALL BRIBAC(DVPLA,EHIST,KGAUS,NBACK,NCRIT,NTYPE,PROPS,
     .            SGTOT,VSTOT)
C
      VSTOT=VSTOT+DVSPE/AINTE 
 1000 CONTINUE
C
C***STORE PLASTIC PARAMETERS IN EHIST
C
      EHIST(2)=VSTOT
C
      RETURN
      END
