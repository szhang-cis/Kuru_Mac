      SUBROUTINE PROP03(ITAPE,PROPS)
C***********************************************************************
C
C**** THIS ROUTINE READS THE MATERIAL PROPERTIES FOR THE LINK MODEL
C     (ELEMENT NO. 3)
C                                                      
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_om.f'
      INCLUDE 'auxl_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'inpo_om.f'
C
      DIMENSION PROPS(*)
C
C**** READ & WRITE MATERIAL PROPERTIES
C
      NPRIN=0
      PROPS(1)=2.0D0
C
C**** LOOK FOR 'STIFFNESS' CARD
C
      CALL LISTEN('PROP03',NPRIN,ITAPE)
      IF(WORDS(1).EQ.'STIFF') THEN
       PROPS(2)=PARAM(1)
       PROPS(3)=PARAM(2)
       PROPS(4)=PARAM(3)
      ELSE
       CALL RUNEND('PROP03: STIFFNESS CARD NOT FOUND')
      ENDIF
C
C**** READ CONTACT TEMPERATURE 'TEMPA' OR 'TEMPB' CARD
C
      CALL LISTEN('PROP03',NPRIN,ITAPE)
      IF(WORDS(1).EQ.'TEMPA') THEN
       PROPS(5)=PARAM(1)
       PROPS(6)=1.0D0    ! inote
      ELSE
       IF(WORDS(1).EQ.'TEMPB') THEN
        PROPS(5)=PARAM(1)
        PROPS(6)=2.0D0   ! inote
       ELSE
        PROPS(5)=-10.0E+10
        PROPS(6)=1.0D0    ! inote
        GO TO 1
       ENDIF
      ENDIF
C
C**** READ 'IMPOSED DEFORMATION' CARD
C
      CALL LISTEN('PROP03',NPRIN,ITAPE)
    1 IF(WORDS(1).EQ.'IMPOS') THEN
       PROPS(7)=PARAM(1)
       PROPS(8)=PARAM(2)
       PROPS(9)=PARAM(3)
       PROPS(10)=1.0D0   ! impde
      ELSE
       PROPS(7)=0.0D0
       PROPS(8)=0.0D0
       PROPS(9)=0.0D0
       PROPS(10)=0.0D0   ! impde
       GO TO 2
      ENDIF
C
C**** LOOK FOR 'END_MATERIAL' CARD
C
      NPRIN=0
      CALL LISTEN('PROP04',NPRIN,ITAPE)
    2 IF(WORDS(1).NE.'END_M')
     . CALL RUNEND('PROP03: END_MATERIAL CARD NOT FOUND   ')
      WRITE(LURES,806)
C
C**** WRITE
C
      WRITE(LURES,800)
      IF(NDIME.EQ.1) THEN
       WRITE(LURES,801)
       WRITE(LURES,804) PROPS(2)
      ENDIF
      IF(NDIME.EQ.2) THEN
       WRITE(LURES,802)
       WRITE(LURES,804) PROPS(2),PROPS(3)
      ENDIF
      IF(NDIME.EQ.3) THEN
       WRITE(LURES,803)
       WRITE(LURES,804) PROPS(2),PROPS(3), PROPS(4)
      ENDIF
      WRITE(LURES,805)
      WRITE(LURES,804) PROPS(5)
C
      IMPDE=DINT(PROPS(10))
      IF(IMPDE.EQ.1) THEN
       WRITE(LURES,810)
       IF(NDIME.EQ.1) THEN
        WRITE(LURES,801)
        WRITE(LURES,804) PROPS(7)
       ENDIF
       IF(NDIME.EQ.2) THEN
        WRITE(LURES,802)
        WRITE(LURES,804) PROPS(7),PROPS(8)
       ENDIF
       IF(NDIME.EQ.3) THEN
        WRITE(LURES,803)
        WRITE(LURES,804) PROPS(7),PROPS(8), PROPS(9)
       ENDIF
      ENDIF
C
      RETURN
C
  800 FORMAT(/,'STIFFNESS',/)
  801 FORMAT('X-DIRECTION')
  802 FORMAT('X-DIRECTION   Y-DIRECTION')
  803 FORMAT('X-DIRECTION   Y-DIRECTION   Z-DIRECTION')
  804 FORMAT(E15.6,10X,E15.6,10X,E15.6,/)
  805 FORMAT(/,'LINKING TEMPERATURE',/)
  806 FORMAT(/)
  810 FORMAT(/,'IMPOSED DEFORMATION',/)
      END
