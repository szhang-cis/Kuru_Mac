      SUBROUTINE BMASIG(BMSIG,CARTD,DVOLU,GPCOD,NDIME,NDOFN,NNODE,
     .                  NTYPE,SHAPE,SIGMA)
C***********************************************************************
C                                    T
C****THIS ROUTINE PERFORMES BMATX  SIGMA INITIAL
C
C**********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION BMSIG(*), CARTD(NDIME,*), SHAPE(*), SIGMA(*)
C
      GOTO(1,1,3,4,5),NTYPE
C
    1 CONTINUE
C
C***PLANE STRESS & PLANE STRAIN
C
C$DIR NO_RECURRENCE
      DO 10 INODE=1,NNODE
      IEVAB=(INODE-1)*NDOFN+1
      JEVAB=IEVAB+1
      BMSIG(IEVAB)=BMSIG(IEVAB)+
     .          (CARTD(1,INODE)*SIGMA(1)+CARTD(2,INODE)*SIGMA(3))*DVOLU
      BMSIG(JEVAB)=BMSIG(JEVAB)+
     .          (CARTD(2,INODE)*SIGMA(2)+CARTD(1,INODE)*SIGMA(3))*DVOLU
   10 CONTINUE
C
      RETURN
C
C***AXIAL-SIMMETRIC CASE
C
    3 CONTINUE
C
C$DIR NO_RECURRENCE
      DO 20 INODE=1,NNODE
      IEVAB=(INODE-1)*NDOFN+1
      JEVAB=IEVAB+1
      BMSIG(IEVAB)=BMSIG(IEVAB)+
     .            (CARTD(1,INODE)*SIGMA(1)+CARTD(2,INODE)*SIGMA(3)
     .            +SHAPE(INODE)*SIGMA(4)/GPCOD)*DVOLU
      BMSIG(JEVAB)=BMSIG(JEVAB)+
     .          (CARTD(2,INODE)*SIGMA(2)+CARTD(1,INODE)*SIGMA(3))*DVOLU
   20 CONTINUE
C
      RETURN
C
C***3-D CASE
C
    4 CONTINUE
C
C$DIR NO_RECURRENCE
      DO 30 INODE=1,NNODE
      IEVAB=(INODE-1)*NDOFN+1
      JEVAB=IEVAB+1
      KEVAB=JEVAB+1
      BMSIG(IEVAB)=BMSIG(IEVAB)+
     .            (CARTD(1,INODE)*SIGMA(1)+CARTD(2,INODE)*SIGMA(3)
     .            +CARTD(3,INODE)*SIGMA(5))*DVOLU
      BMSIG(JEVAB)=BMSIG(JEVAB)+
     .            (CARTD(2,INODE)*SIGMA(2)+CARTD(1,INODE)*SIGMA(3)
     .            +CARTD(3,INODE)*SIGMA(6))*DVOLU
      BMSIG(KEVAB)=BMSIG(KEVAB)+
     .            (CARTD(3,INODE)*SIGMA(4)+CARTD(1,INODE)*SIGMA(5)
     .            +CARTD(2,INODE)*SIGMA(6))*DVOLU
   30 CONTINUE
      RETURN
C
    5 CONTINUE
C
C***1-D CASE
C
C$DIR NO_RECURRENCE
      DO 40 INODE=1,NNODE
      IEVAB=(INODE-1)*NDOFN+1
      BMSIG(IEVAB)=BMSIG(IEVAB)+CARTD(1,INODE)*SIGMA(1)*DVOLU
   40 CONTINUE
C
      RETURN
      END
