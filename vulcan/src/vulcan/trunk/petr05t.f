      SUBROUTINE PETR05T(ELCODT,PROPST,ELDIST,SHAPET,CARTDT,
     .                   POSGPT,WEIGPT,GPCODT,SHAPDT,DERIVT,XJACMT,
     .                   CARDDT,VELCMT,WHAPEL,HACHET,
     .                   WERIVT,CENTRO,ADVEMT,TEINIT,FPCHLT,
     .                   EHISTT)
C***********************************************************************
C
C**** THIS ROUTINE COMPUTES GALERKIN (IGALET=0) OR PETROV-GALERKIN
C     (IGALET=1) TYPE WEIGHT FUNCTIONS
C
C     NOTES: WERIVT (DERIDT) IS NOT REALLY USED IN THE CALCULATION OF
C            THE WEIGHT FUNCTION WHAPEL
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
C**** COUPLING VARIABLES
C
      INCLUDE 'nuec_om.f'   ! thermal-mechanical
C
C**** THERMAL VARIABLES
C
      INCLUDE 'prob_omt.f'
      INCLUDE 'inte_omt.f'
      INCLUDE 'auxl_omt.f'
C
      COMMON/JACOBSTA/IERORT,KERORT
C
      DIMENSION ELCODT(NDIMET,*),        PROPST(*),
     .          ELDIST(NDOFCT,*),        SHAPET(NNODLT,*),
     .          CARTDT(NDIMET,NNODLT,*)
      DIMENSION POSGPT(NDIMET,*),        WEIGPT(*),
     .          GPCODT(NDIMET,*),        SHAPDT(NNODLT,*)
      DIMENSION DERIVT(NDIMET,NNODLT,*), XJACMT(NDIMET,*),
     .          CARDDT(NDIMET,NNODLT,*), VELCMT(*),
     .          WHAPEL(NNODLT,*),
     .          HACHET(NNODLT),
     .          WERIVT(NDIMET,NNODLT,*)
      DIMENSION CENTRO(NDIMET,*),        ADVEMT(NDIMET,*)
      DIMENSION TEINIT(NDOFCT,*),        FPCHLT(NFPCH,*)
      DIMENSION EHISTT(NHISTT,*)
C
      TWOPIT=6.283185307179586D0
C
      IF(IGALET.EQ.0) THEN
       DO IGAUST=1,NGAULT
        DO INODLT=1,NNODLT
         WHAPEL(INODLT,IGAUST)=SHAPET(INODLT,IGAUST)
        ENDDO
       ENDDO
       RETURN
      ENDIF
C
C**** IDENTIFY SAMPLING POINTS AND WEIGHTS OF THE SHAPE FUNCTION
C
      CALL RULEPW(NDIMET,NGAULT,NRULET,POSGPT,WEIGPT)
C
C**** LOOP ON INTEGRATION POINTS
C
      DO 100 IGAUST=1,NGAULT
C
C**** COMPUTE SHAPE FUNCTIONS AND DERIVATIVES
C
      EXISPT=POSGPT(1,IGAUST)
      ETASPT=0.0D0
      IF(NDIMET.GE.2) ETASPT=POSGPT(2,IGAUST)
      EZETAT=0.0D0
      IF(NDIMET.EQ.3) EZETAT=POSGPT(3,IGAUST)
      CALL SHAFUN(DERIVT(1,1,IGAUST),EXISPT,ETASPT,EZETAT,NDIMET,NNODLT,
     .            NQUTRT,     0,SHAPDT(1,IGAUST))
C
C**** CARTESIAN DERIVATIVES OF THE SHAPE FUNCTIONS
C
      CALL JACOBST(CARDDT(1,1,IGAUST), DERIVT(1,1,IGAUST), DETJMT,
     .             ELCODT,             GPCODT(1,IGAUST),   IELEMT,
     .             NDIMET,NNODLT,      SHAPDT(1,IGAUST),   XJACMT,
     .             LUREST,LUPRIT)
C
C**** DEFINES THE UPWIND FUNCTION
C
      NSUPE=1
      CALL HACHEC(ELCODT,PROPST,ELDIST,VELCMT,HACHET,CENTRO,
     .            ADVEMT,TEINIT,SHAPET(1,IGAUST),NSUPE,FPCHLT,
     .            EHISTT(1,IGAUST),CARTDT(1,1,IGAUST))
C
C**** COMPUTE PERTURBATION FUNCTIONS AND DERIVATIVES
C
      CALL WHAFUN(WERIVT(1,1,IGAUST),EXISPT,ETASPT,EZETAT,NDIMET,NNODLT,
     .            WHAPEL(1,IGAUST),HACHET,XJACMT,IPERT)
C
C**** COMPUTES WEIGHT FUNCTION (SHAPE FUNCTION + PERTURBATION FUNCTION)
C
      DO INODLT=1,NNODLT
       WHAPEL(INODLT,IGAUST)=SHAPET(INODLT,IGAUST)+
     .                       WHAPEL(INODLT,IGAUST)
      ENDDO
C
  100 CONTINUE           ! igaust=1,ngaust
C
      RETURN
      END
