      SUBROUTINE DMATRI(EPMTX,NSTRS,NTYPE,PROPS)
C************************************************************************
C
C****THIS ROUTINE SETS UP THE ELASTIC MATRIX FOR PLANE STRESS & STRAIN
C    AXISYMMETRIC AND 3-D PROBLEMS IN A ORTHOTROPIC MEDIUM
C
C************************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION EPMTX(*), PROPS(*)
C
C***INITIALISE THE ELASTIC MATRIX
C
      NKOST=(NSTRS+1)*NSTRS/2
      DO 10 IKOST=1,NKOST
   10 EPMTX(IKOST)=0.0D00
C
C***READ ELASTIC PROPERTIES
C
      EXX=PROPS(2)
      PXY=PROPS(3)
      EYY=PROPS(4)
      PXZ=PROPS(5)
      EZZ=PROPS(6)
      PYZ=PROPS(7)
      GXY=PROPS(8)
      GXZ=PROPS(9)
      GYZ=PROPS(10)
C
      GOTO (1,2,3,4,5),NTYPE
C
C***PLANE STRESS
C
    1 EXY=EXX/EYY
      RAT=EXX/(1.-EXY*PXY*PXY)
C
      EPMTX(1) =RAT
      EPMTX(2) =PXY*RAT
      EPMTX(4) =RAT/EXY
      EPMTX(6) =GXY
C
      RETURN
C
C
C***PLANE STRAIN
C
    2 EX1=EXX/(1.-PXZ*PXZ*EXX/EYY)
      EY1=EYY/(1.-PXZ*PXZ*EYY/EXX)
      PXY=(PXY+PXZ*PYZ*EYY/EZZ)/(1.-PYZ*PYZ*EYY/EZZ)
      EXX=EX1
      EYY=EY1
      EXY=EXX/EYY
      RAT=EXX/(1.-EXY*PXY*PXY)
C
      EPMTX(1) =RAT
      EPMTX(2) =PXY*RAT
      EPMTX(4) =EPMTX(2)          ! ?
      EPMTX(5) =RAT/EXY
      EPMTX(7) =EPMTX(2)          ! ?
      EPMTX(8) =GXY
      EPMTX(10)=RAT               ! ?
C
      RETURN
C
C***AXI-SYMMETRIC CASE
C
    3 GRZ=GXY
      ERR=EXX
      PRT=PXY
      EZZ=EYY
      PRZ=PXZ
C
      ERZ=ERR/EZZ
      GEZ=GRZ/EZZ
      RZ2=ERZ*PRZ*PRZ
      CON=EZZ/((1.+PRT)*(1.-PRT-2.*RZ2))
C
      EPMTX(1) =CON*(1.-PRT**2)
      EPMTX(2) =CON*ERZ*PRZ*(1.+PRT)
      EPMTX(4) =EPMTX(2)
      EPMTX(5) =CON*ERZ*(1. -RZ2)
      EPMTX(7) =CON*ERZ*(PRT+RZ2)
      EPMTX(8) =CON*GEZ*(1.+PRT)*(1.-PRT-2.*RZ2)
      EPMTX(10)=EPMTX(5)
C
      RETURN
C
C***3-D CASE
C
    4 CO1=1-PYZ*PYZ*EYY/EZZ
      CO2=PXY+PXZ*PYZ*EYY/EZZ
      C21=CO2/CO1
      CO3=1-(PXZ*PXZ+(EZZ/EYY)*C21*CO2)*EXX/EZZ
      CO4=PXZ+PYZ*C21
C
      EPMTX(1) =EXX/CO3
      EPMTX(2) =EPMTX(1)*C21
      EPMTX(4) =EPMTX(1)*CO4
      EPMTX(7) =EPMTX(2)*C21+EYY/CO1
      EPMTX(9) =EPMTX(4)*C21+EYY*PYZ/CO1
      EPMTX(12)=GXY
      EPMTX(16)=EPMTX(4)*PXZ+EPMTX(9)*PYZ+EZZ
      EPMTX(19)=GXZ
      EPMTX(21)=GYZ
C
      RETURN
C
C***1-D CASE
C
    5 EPMTX(1) =EXX
C
      RETURN
      END     
