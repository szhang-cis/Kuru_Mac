      SUBROUTINE PREDIS(DISTO,NCOMP,
     .                  SINI2,SINI3,NSINI)
C***********************************************************************
C
C**** THIS ROUTINE READS THE INITIAL VALUES FOR DISTO
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'inpo_om.f'
      INCLUDE 'auxl_om.f'
C
      COMMON/PRFILE/JTAPE,IFLAG
C
      DIMENSION DISTO(NDOFC,*), SINI2(NPRE2,*), SINI3(NPRE3,*)
      DIMENSION SUBTA(12)
C
      IERR1=1
      IF(JTAPE.NE.LUDAT) THEN
       JTAPE=LUINI
       IF(IOINI.EQ.0) OPEN(UNIT=LUINI,FILE=CD1,STATUS='OLD',ERR=1000)
       IOINI=1
       IERR1=0
C
 1000  IF(IERR1.NE.0) THEN
        IF(IERR1.EQ.1) WRITE(LURES,901)
        CALL RUNEND('PREDIS: ERROR IN OPENING FILES')
       ENDIF
      ENDIF
C
C**** READ FOR NUMBER OF NODES WITH INITIAL CONDITIONS
C
      NPRIN=1
      CALL LISTEN('PREDIS',NPRIN,JTAPE)
      NPOI1=DINT(PARAM(1))
      IF(NPOI1.LE.0.OR.NPOI1.GT.NPOIN) THEN
       WRITE(LURES,910)
       IF(NCOMP.GT.0)
     .  CALL RUNEND('PREDIS: ERROR WHEN READING THE NUMBER OF POINTS
     .                 WITH INITIAL DISPLACEMENT/VELOCITY/ACCELERATION')
       IF(NSINI.GT.0)
     .  CALL RUNEND('PREDIS: ERROR WHEN READING THE NUMBER OF POINTS
     .                 WITH INITIAL STRESS/INTERNAL VARIABLE')
      ENDIF
      WRITE(LURES,905) NPOI1
C
C**** DISPLACEMENTS/VELOCITIES/ACCELERATIONS
C
      IF(NCOMP.GT.0) THEN
C
C**** READ NODAL VARIABLES
C
       DO IPOIN=1,NPOI1
        CALL LISTEN('PREDIS',NPRIN,JTAPE)
        JPOIN=INT(PARAM(1))
        DO IDOFN=1,NDOFN
         DISTO(IDOFN,JPOIN)=PARAM(1+IDOFN)
        ENDDO
       ENDDO
C
C**** ECHO NODAL VALUES FOR THE WHOLE MESH
C
       IF(NCOMP.EQ.1) WRITE(LURES,921)
       IF(NCOMP.EQ.2) WRITE(LURES,922)
       IF(NCOMP.EQ.3) WRITE(LURES,923)
       DO IPOIN=1,NPOIN
        WRITE(LURES,930) IPOIN,(DISTO(IDOFN,IPOIN),IDOFN=1,NDOFN)
       ENDDO
      ENDIF             ! ncomp.gt.0
C
C**** STRESSES/INTERNAL VARIABLES
C
      IF(NSINI.GT.0) THEN
C
C**** READ NODAL VARIABLES
C
       IF(NSINI.EQ.1) THEN
        IF(NPRE2.LE.0)
     .   CALL RUNEND('ERROR: NPRE2 LE 0 WITH STRESS INITIAL CONDITIONS')
        DO IPOIN=1,NPOI1
         CALL LISTEN('PREDIS',NPRIN,JTAPE)
         JPOIN=INT(PARAM(1))
         DO IDOFN=1,NPRE2
          SINI2(IDOFN,JPOIN)=PARAM(1+IDOFN)
         ENDDO
        ENDDO
       ENDIF
       IF(NSINI.EQ.2) THEN
        IF(NPRE3.LE.0)
     .   CALL RUNEND('ERROR: NPRE3 LE 0 WITH INT. VAR. INITIAL COND.')
        DO IPOIN=1,NPOI1
         CALL LISTEN('PREDIS',NPRIN,JTAPE)
         JPOIN=INT(PARAM(1))
         DO IDOFN=1,NPRE3
          SINI3(IDOFN,JPOIN)=PARAM(1+IDOFN)
         ENDDO
        ENDDO
       ENDIF
C
C**** ECHO NODAL VALUES FOR THE WHOLE MESH
C
       IF(NSINI.EQ.1) THEN
        WRITE(LURES,941)
        DO IPOIN=1,NPOIN
         WRITE(LURES,950) IPOIN,(SINI2(IDOFN,IPOIN),IDOFN=1,NPRE2)
        ENDDO
       ENDIF
       IF(NSINI.EQ.2) THEN
        WRITE(LURES,942)
        DO IPOIN=1,NPOIN
         WRITE(LURES,950) IPOIN,(SINI3(IDOFN,IPOIN),IDOFN=1,NPRE3)
        ENDDO
       ENDIF
      ENDIF             ! nsini.gt.0
C
      WRITE(LURES,940)
C
      RETURN
C
  901 FORMAT(' ERROR IN OPENING INITIAL TEMPERATURE INPUT FILE 43')
  905 FORMAT(/,5X,'NUMBER OF NODES WITH INITIAL DATA',I5,/)
  910 FORMAT(//,
     .    'ERROR: NUMBER OF INPUT POINTS LT 0 OR GT TOTAL NO. POINTS')
  921 FORMAT(1X,'NODE',4X,'INITIAL DISPLACEMENT',/)
  922 FORMAT(1X,'NODE',8X,'INITIAL VELOCITY',/)
  923 FORMAT(1X,'NODE',4X,'INITIAL ACCELERATION',/)
  930 FORMAT(I5,10X,3(E15.6,5X))
  940 FORMAT(/)
  941 FORMAT(1X,'NODE',4X,'INITIAL STRESS',/)
  942 FORMAT(1X,'NODE',8X,'INITIAL INTERNAL VARIABLES',/)
  950 FORMAT(I5,10X,6(E15.6,5X))
      END
