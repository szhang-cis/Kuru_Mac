      SUBROUTINE PCGITET(DISIT,IFFIX,LNODS,REFOR,
     .                   NDOFN,NELEM,NEVAB,NNODE,NPOIN,NTOTV,
     .                   ALOAD,CSTIF,DELTA,GSTDI,DISIM,FOREL,
     .                   NPREL,NGRUP,NPROP,NMATS,
     .                   NDATA,NPREV,NSTAT,NMATX,NDIME,NTOTVM,NFPCH,
     .                   MATNO,PROEL,PROPS,ELDAT,ELPRE,
     .                   ELVAR,ELMAT,WORK1,DISTO,COORD,
     .                   ADVEL,TEMPI,PREAS,TGAPS,DISPL,FPCHA,LACTI)
C***********************************************************************
C
C**** THIS ROUTINE SOLVES THE SYSTEM OF EQUATIONS USING  
C     A PRECONDITIONED CONJUGATED GRADIENT ITERATIVE SOLVER
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      COMMON/SOLVERTA/KRENUT,KSOLVT,KSYMMT,NWIDTT,MITCGT,NBUFAT,NPRIRT,
     .                MITGMT,MKRYLT,IPGMRT
      COMMON/SOLVERTB/TOLCGT,TOLC1T,TOLGMT
C
      COMMON/LOGUNT/LUDTST,LUSOLT,LUFROT,LUFRHT,LUDATT,LUPRIT,LUREST,
     .              LUSO2T,LUFR2T,LUPOST,LURSTT,LUBFGT,LUPIPT,LUPANT,
     .              LUGEOT,LUSETT,LUMATT,LUINIT,LULOAT,LUFIXT,LUADVT,
     .              LUACTT,LUFANT,LUSTRT,
     .              LUCU1T,LUCU2T,LUCU3T,LUCU4T,LUCU5T,LUCU6T,LUCU7T,
     .              LUCU8T,LUCU9T,LUC10T
C
      DIMENSION DISIT(*), IFFIX(*), LNODS(NNODE,*), REFOR(*)
      DIMENSION ALOAD(*), CSTIF(*), DELTA(*), GSTDI(*), 
     .          DISIM(*), FOREL(*)
C
      DIMENSION MATNO(NELEM),
     .          PROEL(NPREL,NGRUP), PROPS(NPROP,NMATS),
     .          ELDAT(NDATA),       ELPRE(NPREV),
     .          ELVAR(NSTAT),       ELMAT(NMATX)
      DIMENSION DISTO(NTOTV,3),     COORD(NDIME,NPOIN)
      DIMENSION ADVEL(NTOTV*NDIME), TEMPI(NPOIN,2)
      DIMENSION PREAS(NPOIN),       TGAPS(NPOIN)
      DIMENSION DISPL(NTOTVM),      FPCHA(NFPCH,NPOIN),
     .          LACTI(NELEM)
C
C**** INITIALISE SOME ARRAYS
C
      DO ITOTV=1,NTOTV
       IF(IFFIX(ITOTV).NE.0) REFOR(ITOTV)=0.0
       ALOAD(ITOTV)=REFOR(ITOTV)
       DELTA(ITOTV)=0.0
      ENDDO
C
      ANORM=VECDOT(REFOR,REFOR,NTOTV)
      IF(ANORM.LE.TOLC1T) RETURN
C
C**** ITERATIVE SOLUTION
C
      BETAB=1.0D00
C
      DO 100 IITER=1,MITCGT
C
C**** COMPUTE FIRST PART OF ITERATIVE SOLUTION
C
      CALL PCGPRE(ALOAD,GSTDI,FOREL,NWIDTT,NDOFN,NPOIN)
C
C**** EVALUATE CG PARAMETER
C
      BETAT=VECDOT(ALOAD,REFOR,NTOTV)
      BETAK=BETAT/BETAB
      BETAB=BETAT
C
C**** EVALUATE ADVANCING DIRECTION
C
      DO ITOTV=1,NTOTV
       DELTA(ITOTV)=ALOAD(ITOTV)+BETAK*DELTA(ITOTV)
      ENDDO
C
C**** EVALUATE PRODUCT K*d
C
      CALL SKYRHST(CSTIF,DELTA,ALOAD,LNODS,NDOFN,NELEM,NEVAB,
     .             NNODE,NPOIN,DISIM,FOREL,    0,
     .             NPREL,NGRUP,NPROP,NMATS,
     .             NDATA,NPREV,NSTAT,NMATX,NTOTV,NDIME,NTOTVM,NFPCH,
     .             MATNO,PROEL,PROPS,ELDAT,ELPRE,
     .             ELVAR,ELMAT,WORK1,DISTO,COORD,
     .             ADVEL,TEMPI,PREAS,TGAPS,DISPL,FPCHA,LACTI)
C
      DO ITOTV=1,NTOTV
       IF(IFFIX(ITOTV).NE.0) ALOAD(ITOTV)=0.0
      ENDDO
C
C**** COMPUTE LINE SEARCH PARAMETER
C
      EETAT=VECDOT(DELTA,REFOR,NTOTV)
      EETAB=VECDOT(DELTA,ALOAD,NTOTV)
      EETAK=EETAT/EETAB
C
C**** UPDATE SOLUTION
C
      DO ITOTV=1,NTOTV
       DISIT(ITOTV)=DISIT(ITOTV)+EETAK*DELTA(ITOTV)
       REFOR(ITOTV)=REFOR(ITOTV)-EETAK*ALOAD(ITOTV)
       ALOAD(ITOTV)=REFOR(ITOTV)
      ENDDO
C
C**** CHECK FOR CONVERGENCE ( NORM OF RESIDUAL FORCES )
C
      BNORM=VECDOT(REFOR,REFOR,NTOTV)
      RATIO=DSQRT(BNORM/ANORM)
C
      IF(NPRIRT.EQ.1) WRITE(LUPRIT,900) IITER,RATIO
      IF(RATIO.LT.TOLCGT) THEN
        RETURN
      ENDIF
C
  100 CONTINUE
C
C**** NOT CONVERGED
C
      CALL RUNENDT('PCGITET: "PCG" SOLVER NOT CONVERGED ')
C
      RETURN
  900 FORMAT(5X,'PCG ITER. = ',I5,3X,'DPCG RATIO = ',E15.6)
      END   
