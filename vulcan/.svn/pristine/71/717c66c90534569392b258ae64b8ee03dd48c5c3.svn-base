      SUBROUTINE FACOEF(DESIG,DESMN,DESJ2,DESJ3,NCRIT,NTYPE,PREDV,
     .                  WCOJ3,YCOJ3,ZCOJ2)
C***********************************************************************
C
C****THIS ROUTINE CALCULATES THE COEFFICIENTS INVOLVED INTO THE
C    CALCULATION OF THE REDUCTION
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION DESDV(6), DESIG(*), PREDV(*)
C
C***SET UP CONSTANT
C
      CALL INVARS(DESIG,DESDV,NTYPE,DESMN,DESQU,DESTH,DESJ2,
     .            DESJ3)
      IF(NTYPE.EQ.4)GOTO 10
C
C***2-D CASE
C
      ZCOJ2=PREDV(3)*DESDV(3)+0.5*(PREDV(1)*DESDV(1)+PREDV(2)*DESDV(2)
     .     +PREDV(4)*DESDV(4))
C
      IF(NCRIT.EQ.2.OR.NCRIT.EQ.4)RETURN
C
      ACOJ3=PREDV(1)*PREDV(2)*DESDV(4)+PREDV(1)*PREDV(4)*DESDV(2)
     .     +PREDV(2)*PREDV(4)*DESDV(1)
      CCOJ3=2.0*PREDV(4)*PREDV(3)*DESDV(3)+PREDV(3)*PREDV(3)*DESDV(4)
      DCOJ3=PREDV(1)*DESDV(2)*DESDV(4)+PREDV(2)*DESDV(1)*DESDV(4)+
     .      PREDV(4)*DESDV(1)*DESDV(2)
      FCOJ3=2.0*(PREDV(3)*DESDV(4)*DESDV(3))+PREDV(4)*DESDV(3)*DESDV(3)
C
      YCOJ3=ACOJ3-CCOJ3
      WCOJ3=DCOJ3-FCOJ3
C
      RETURN
C
C***3-D CASE
C
   10 CONTINUE
      ZCOJ2=PREDV(3)*DESDV(3)+PREDV(5)*DESDV(5)+PREDV(6)*DESDV(6)
     .+0.5*(PREDV(1)*DESDV(1)+PREDV(2)*DESDV(2)+PREDV(4)*DESDV(4))
C
      IF(NCRIT.EQ.2.OR.NCRIT.EQ.4)RETURN
C
      ACOJ3= PREDV(1)*PREDV(2)*DESDV(4)+PREDV(1)*PREDV(4)*DESDV(2)
     .      +PREDV(2)*PREDV(4)*DESDV(1)
      BCOJ3=(PREDV(3)*PREDV(5)*DESDV(6)+PREDV(3)*PREDV(6)*DESDV(5)
     .      +PREDV(5)*PREDV(6)*DESDV(4))*2.
      CCOJ3=(PREDV(1)*PREDV(6)*DESDV(6)+PREDV(2)*PREDV(5)*DESDV(5)
     .      +PREDV(4)*PREDV(3)*DESDV(3))*2.
     .     +(PREDV(6)*PREDV(6)*DESDV(1)+PREDV(5)*PREDV(5)*DESDV(2)
     .      +PREDV(3)*PREDV(3)*DESDV(4))
      DCOJ3= PREDV(1)*DESDV(2)*DESDV(4)+PREDV(2)*DESDV(1)*DESDV(4)
     .      +PREDV(4)*DESDV(1)*DESDV(2)
      ECOJ3=(PREDV(3)*DESDV(5)*DESDV(6)+PREDV(5)*DESDV(3)*DESDV(6)
     .      +PREDV(6)*DESDV(3)*DESDV(5))*2.
      FCOJ3=(PREDV(6)*DESDV(1)*DESDV(6)+PREDV(5)*DESDV(2)*DESDV(5)
     .      +PREDV(3)*DESDV(4)*DESDV(3))*2.
     .      +PREDV(1)*DESDV(6)*DESDV(6)+PREDV(2)*DESDV(5)*DESDV(5)
     .      +PREDV(4)*DESDV(3)*DESDV(3)
C
      YCOJ3=ACOJ3+BCOJ3-CCOJ3
      WCOJ3=DCOJ3+ECOJ3-FCOJ3
C
      RETURN
      END
