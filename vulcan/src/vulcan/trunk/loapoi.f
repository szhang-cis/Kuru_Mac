      SUBROUTINE LOAPOI(NDIME,NDOFC,NDOFN,NPOIN,KPROB,NTOTV,NFUNC,
     .                  RLOAD,RLOAH,NLOPO,LURES)
C***********************************************************************
C
C**** THIS ROUTINE EVALUATES THE EQUIVALENT NODAL FORCES DUE TO
C     POINT LOADS (ACTING ON THE ELEMENT NODES)
C
C     Notes: DINT is used to avoid problems in challenge machines
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'inpo_om.f'
C
      COMMON/LDFILE/ITAPE
C
      DIMENSION POINT(6), RLOAD(NTOTV), RLOAH(NTOTV,NFUNC)
C
      WRITE(LURES,900)
      IF(KPROB.EQ.1) THEN
        IF(NDIME.EQ.2) WRITE(LURES,905)
        IF(NDIME.EQ.3) WRITE(LURES,910)
      ELSE IF(KPROB.EQ.2) THEN
        IF(NDIME.EQ.2) WRITE(LURES,915)
        IF(NDIME.EQ.3) WRITE(LURES,916)
      ENDIF
C
      DO ILOPO=1,NLOPO
       NPRIN=1
       CALL LISTEN('LOAPOI',NPRIN,ITAPE)
       LODPT=DINT(PARAM(1))
       IFUNC=DINT(PARAM(2))
       DO IDOFN=1,NDOFN
        POINT(IDOFN)=PARAM(IDOFN+2)
       ENDDO
C
       WRITE(LURES,925) LODPT,IFUNC,
     .              (POINT(IDOFN),IDOFN=1,NDOFN)
       IF(IFUNC.GT.NFUNC)
     .  CALL RUNEND('LOAPOI: ERROR IN FUNCTION NUMBER')
C
C**** ASSEMBLE IN GLOBAL ARRAY
C
       ITOTV=(LODPT-1)*NDOFC
       DO IDOFN=1,NDOFN
        ITOTV=ITOTV+1
        RLOAH(ITOTV,IFUNC)=RLOAH(ITOTV,IFUNC)+POINT(IDOFN)
        RLOAD(ITOTV)=RLOAD(ITOTV)+POINT(IDOFN)
       ENDDO
      ENDDO           ! ilopo=1,nlopo
C
      WRITE(LURES,930)
C
      RETURN
  900 FORMAT(//,5X,'POINT LOADS DATA :',/)
  905 FORMAT(1X,6H  NODE,9H FUNCTION,10X,9H X-FORCES,10X,9H Y-FORCES)
  910 FORMAT(1X,6H  NODE,9H FUNCTION,10X,9H X-FORCES,10X,9H Y-FORCES,
     .                               10X,9H Z-FORCES)
  915 FORMAT(6H  NODE,9H FUNCTION,4X,9H Z-FORCES,4X,9H X-COUPLE,4X,
     .                               9H Y-COUPLE)
  916 FORMAT(6H  NODE,9H FUNCTION,4X,9H X-FORCES,4X,9H Y-FORCES,4X,
     .                               9H Z-FORCES,
     .               4X,9H X-COUPLE,4X,9H Y-COUPLE,4X,9H Z-COUPLE)
  925 FORMAT(1X,I6,4X,I5,3(4X,F15.6))
  930 FORMAT(/)
      END
