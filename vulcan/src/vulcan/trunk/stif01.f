      SUBROUTINE STIF01(CARTD,DVOLU,EHIST,ELCOD,ELDIS,EPMTX,GPCOD,
     .                  PROPS,SHAPE,STRSG,ESTIF,HSTIF,EMASS,
     .                  BMATX,DMATX,SIGMA,XJACM,VELCM,STRAN)
C***********************************************************************
C
C**** THIS ROUTINE COMPUTES THE STIFFNESS & COUPLING MATRICES
C         ( ELEMENT TYPE NO. 1 )
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
C
      COMMON/REHMAT/KFLAG,IFLAG
C
      DIMENSION CARTD(NDIME,NNODE,*), DVOLU(*),          EHIST(NHIST,*),
     .          ELCOD(NDIME,*),       ELDIS(NDOFC,*),    EPMTX(*),
     .          GPCOD(NDIME,*),       PROPS(*),          SHAPE(NNODE,*),
     .          STRSG(NSTR1,*),       ESTIF(NKOVA),
     .          HSTIF(NEVAB,NNODE),
     .          EMASS(*)
      DIMENSION BMATX(NSTRE,*),       DMATX(NSTRS,*),    SIGMA(*),
     .          XJACM(NDIME,*),       VELCM(*), STRAN(NSTR1,*)
C
C**** UPDATE GEOMETRY FOR LARGE DISPLACEMENTS
C
      IF(LARGE.EQ.1) THEN
       DO IDIME=1,NDIME
        DO INODE=1,NNODL
         ELDIS(IDIME,INODE)=ELCOD(IDIME,INODE)+ELDIS(IDIME,INODE)
        END DO
       END DO
      END IF
C
C**** ENTER LOOPS FOR AREA NUMERICAL INTEGRATION
C
      DO 10 IGAUS=1,NGAUL
C
C**** SET UP THE ELASTIC MATRIX
C
          KOUNT=0
          DO 25 ISTRS=1,    NSTRS
          DO 25 JSTRS=ISTRS,NSTRS
          KOUNT=KOUNT+1
          DMATX(ISTRS,JSTRS)=EPMTX(KOUNT)
   25     DMATX(JSTRS,ISTRS)=DMATX(ISTRS,JSTRS)
C
C**** MODIFY D-MATRIX ACCORDING TO SELECTED NONLINEAR
C     CONSTITUTIVE MODEL
C
      IF(NALGO.GT.1.AND.NCRIT.NE.0) THEN
C
C    (A) ELASTO-PLASTICITY
C
c        IF(NCRIT.LE.4)
c     .   CALL PLAMTX(DMATX, EHIST(1,IGAUS), NCRIT,NSTR1,NSTRS,
c     .               NTYPE, PROPS,          STRSG(1,IGAUS))
C
C    (B) ELASTO-BRITTLE
C
c        IF(NCRIT.EQ.5)
c     .   CALL CRKMTX(DMATX,EHIST(1,IGAUS),PROPS)
C
C    (C) JOINT
C
c        IF(NCRIT.EQ.10) THEN
c         CONJU=EHIST(4,IGAUS)
c         DO 26 ISTRS=1,    NSTRS
c         DO 26 JSTRS=1,    NSTRS
c   26    DMATX(ISTRS,JSTRS)=DMATX(ISTRS,JSTRS)*CONJU
c        ENDIF
C
C    (D) ELASTO-DAMAGE
C
c        IF(NCRIT.GE.20.AND.NCRIT.LE.22)
c     .   CALL DMGMTX(DMATX,EHIST(11,IGAUS))
C
C    (E) GENERAL FORMULATION FOR ELASTO-PLASTICITY-DAMAGE
C
        IF(NCRIT.GE.31.AND.NCRIT.LE.38)
     .   CALL PLBMTX(DMATX,EHIST(18,IGAUS),NSTRS,NSTR1,KSYMM)
C
      ENDIF
C
C**** COMPUTE THE ADDITIONAL TERMS FOR FINITE DISPLACEMENT
C
      IF(LARGE.EQ.1)THEN
C
       CALL PROMA2(XJACM,ELDIS,CARTD(1,1,IGAUS),NDIME,NNODL)
C
       CALL BLARGE(BMATX,CARTD(1,1,IGAUS), ELDIS,GPCOD(1,IGAUS),
     .             LARGE,NDIME,NDOFC,NNODL,NSTRE,NTYPE,SHAPE(1,IGAUS),
     .             XJACM)
C
        IF(IFLAG.EQ.1) THEN
C
          DO 50 ISTR1=1,NSTR1
   50     SIGMA(ISTR1)=STRSG(ISTR1,IGAUS)
C
          IF(KPORE.NE.0)                 !! kpore=1 ??
     .      CALL TOTSIG(ELDIS,KPORE,NDOFC,NNODL,NSTR1,PROPS,SGTOT,
     .                  SHAPE(1,IGAUS))
C
          CALL KLARGE(CARTD(1,1,IGAUS), DVOLU(IGAUS),   ESTIF,
     .                GPCOD(1,IGAUS),   KSYMM, NDIME,NDOFN,
     .                NEVAB,NNODL,NTYPE,SHAPE(1,IGAUS), SIGMA)
C
        ENDIF
C
      ENDIF
C
C**** CALCULATE THE MATERIAL STIFFNESS MATRIX
C
      CALL KMATRI(BMATX,       CARTD(1,1,IGAUS), DMATX,
     .            DVOLU(IGAUS),ESTIF,            GPCOD(1,IGAUS),
     .            LARGE,KSYMM, NDIME,NDOFN,NEVAB,NKOVA,NNODL,
     .            NSTRE,NSTRS, NTYPE,SHAPE(1,IGAUS),
     .            NSTR1)
C
C**** COMPUTE THE COUPLING MATRIX
C
      IF(KFLAG.EQ.1)
     .  CALL HMATRI(BMATX,          CARTD(1,1,IGAUS), DVOLU(IGAUS),
     .              GPCOD(1,IGAUS), HSTIF,            IFLAG,NDIME,
     .              NEVAB,NNODL,    NSTRE,NTYPE,      PROPS,
     .              SHAPE(1,IGAUS))
C
   10 CONTINUE
C
C**** ADD HOURGLASS CONTROL
C
      IF(NHOUR.NE.0) THEN
        IF((NDIME.EQ.2).AND.((NNODL.NE.4).OR.(NGAUL.NE.1))) RETURN
        IF((NDIME.EQ.3).AND.((NNODL.NE.8).OR.(NGAUL.NE.1))) RETURN
        FMULT=1.0
        IF(KELAS.NE.1) THEN
          IF(NCRIT.GE.20.AND.NCRIT.LE.22) FMULT=1.0-EHIST(11,1)
        ENDIF  
        DO IKOVA=1,NKOVA
          ESTIF(IKOVA)=ESTIF(IKOVA)+FMULT*EMASS(IKOVA)
        ENDDO
      ENDIF
C
      RETURN
      END
