      SUBROUTINE COUPLE2(LNODS,MATNO,PROEL,PROPS,COORD,HTLOD,
     .                   IFFIX,PRESC,RLOAD,RLOAH,FICTO,TFICT,
     .                   DISIT,DISPR,DISTO,HEADS,REFOR,TLOAD,
     .                   LPNTN,ELDAT,ELPRE,ELVAR,ELMAT,TEMPN,
     .                   DTEMP,INFRI,COFRI,PWORK,PREAS,TGAPS,
     .                   VNORM,FPCHA,LACTI,NOPRF,PRESF,PREHF,
     .                   VANIS,WORK1,NEQNS,
     .                   LNODST,MATNOT,PROELT,PROPST,COORDT,HTLODT,
     .                   IFFIXT,PRESCT,RLOADT,RLOAHT,FICTOT,TFICTT,
     .                   DISITT,DISPRT,DISTOT,HEADST,REFORT,TLOADT,
     .                   LPNTNT,ELDATT,ELPRET,ELVART,ELMATT,DISPLT,
     .                   PWORKT,PREAST,TGAPST,TEMPIT,ADVELT,FPCHAT,
     .                   LACTIT,
     .                   WORK1T)
C***********************************************************************
C
C**** THIS ROUTINE SOLVES THE THERMOMECHANICAL COUPLED PROBLEM
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8, ALLOCATABLE, INTENT(INOUT) :: WORK1(:), WORK1T(:)
C
C**** COUPLING VARIABLES
C
      INCLUDE 'nuec_om.f'
C
C**** MECHANICAL VARIABLES
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
C
C**** THERMAL VARIABLES
C
      INCLUDE 'prob_omt.f'
      INCLUDE 'inte_omt.f'
      INCLUDE 'auxl_omt.f'
      INTERFACE
        INCLUDE 'iterat.inc'
        INCLUDE 'tmstep.inc'
        INCLUDE 'tmstept.inc'
      END INTERFACE
C
C**** MECHANICAL VARIABLES
C
      DIMENSION LNODS(NNODE,NELEM), MATNO(NELEM),
     .          PROEL(NPREL,NGRUP), PROPS(NPROP,NMATS),
     .          COORD(NDIME,NPOIN), HTLOD(NHLOD,NSUBF,NFUNC)
      DIMENSION IFFIX(NTOTV,2),     PRESC(NTOTV,2),
     .          RLOAD(NTOTV),       RLOAH(NTOTV,NFUNC),
     .          FICTO(NFUNC),       TFICT(NFUNC)
      DIMENSION DISIT(NTOTV,2),     DISPR(NTOTV,NDISR),
     .          DISTO(NTOTV,NDISO), HEADS(NPOIN,4),
     .          REFOR(NTOTV,2),     TLOAD(NTOTV,2)
      DIMENSION LPNTN(NPOIN)
      DIMENSION ELDAT(NDATA),       ELPRE(NPREV),
     .          ELVAR(NSTAT),       ELMAT(NMATX)
      DIMENSION TEMPN(NPOIN,2),     DTEMP(NPOIN)
      DIMENSION INFRI(NPOIN),       COFRI(NSKEW,NDIME,*),
     .          PWORK(NPOIN,2),     PREAS(NPREA,NPOIN),
     .          TGAPS(NPOIN),       VNORM(NTOTV),
     .          FPCHA(NFPCH,NPOIN), LACTI(NELEM)
      DIMENSION NOPRF(NNODE,NELEM), PRESF(NNODE,NDIME,NELEM),
     .          PREHF(NNODE,NDIME,NELEM,NFUNC),
     .          VANIS(NANIV,NANIC,NELEM)
C
C**** THERMAL VARIABLES
C
      DIMENSION LNODST(NNODET,NELEMT), MATNOT(NELEMT),
     .          PROELT(NPRELT,NGRUPT), PROPST(NPROPT,NMATST),
     .          COORDT(NDIMET,NPOINT), HTLODT(NHLODT,NSUBFT,NFUNCT)
      DIMENSION IFFIXT(NTOTVT,2),      PRESCT(NTOTVT,2),
     .          RLOADT(NTOTVT),        RLOAHT(NTOTVT,NFUNCT),
     .          FICTOT(NFUNCT),        TFICTT(NFUNCT)
      DIMENSION DISITT(NTOTVT,2),
     .          DISPRT(NTOTVT,3),      DISTOT(NTOTVT,3),
     .          HEADST(NPOINT,4),
     .          REFORT(NTOTVT,2),      TLOADT(NTOTVT,2),
     .          LPNTNT(NPOINT)
      DIMENSION ELDATT(NDATAT),        ELPRET(NPREVT),
     .          ELVART(NSTATT),        ELMATT(NMATXT)
      DIMENSION DISPLT(NTOTVM),        PWORKT(NPOINT,3),
     .          PREAST(NPREAT,NPOINT), TGAPST(NPOINT),
     .          TEMPIT(NPOINT,2),      ADVELT(NTOTVT*NDIMET),
     .          FPCHAT(NFPCH,NPOINT),  LACTIT(NELEMT)
C
C**** GLOBAL CONVERGENCE FOR OUTPUT
C
      NCKGLO=0
      IF(ISTAGG.EQ.1) NCKGLO=1
C
C**** INITIALISES THE INTERCHANGES ITERATION NUMBER
C
      IITERC=0
C
C**** TRANSFER NODAL MECHANICAL VARIABLES TO THERMAL PROBLEM
C
      CALL TRASMEC(DISTO, PWORK, PREAS(1,1), TGAPS,
     .             DISPLT,PWORKT,PREAST(1,1),TGAPST)
C
C**** TRANSFER GAUSSIAN MECHANICAL VARIABLES TO THERMAL PROBLEM
C
      CALL TRASMEG(ELVAR,ELVART)
C
C**** ADVANCE ON TIME AND PREDICT RESPONSE FOR THE THERMAL PROBLEM
C
      DO ISSTEPT=1,NSSTEPT             !  SUBINCREMENTS
C
       CALL TMSTEPT(DISITT,DISPRT,DISTOT,ELDATT,ELPRET,
     .              ELVART,ELMATT,HEADST,HTLODT,IFFIXT,
     .              PRESCT,LNODST,MATNOT,PROELT,PROPST,
     .              REFORT,RLOADT,RLOAHT,FICTOT,TFICTT,
     .              TLOADT,DISPLT,PWORKT,PREAST,TGAPST,
     .              COORDT,TEMPIT,ADVELT,FPCHAT,LACTIT,
     .              LPNTNT,WORK1T)
C
C**** LOOP ON ITERATIONS FOR THE THERMAL PROBLEM
C
       DO WHILE
     .  ((NCHEKT.NE.0).AND.(IITERT.LE.MITERT))
C
C**** ITERATIVE CORRECTION FOR THE THERMAL PROBLEM
C
        CALL ITERATT(DISITT,DISPRT,DISTOT,ELDATT,ELPRET,
     .               ELVART,ELMATT,HEADST,IFFIXT,PRESCT,
     .               LNODST,MATNOT,PROELT,PROPST,REFORT,
     .               RLOADT,FICTOT,TLOADT,DISPLT,PWORKT,
     .               PREAST,TGAPST,COORDT,TEMPIT,ADVELT,
     .               FPCHAT,LACTIT,HTLODT,WORK1T)
C
       ENDDO
C
       IF(NCHEKT.NE.0)
     .  CALL RUNENDT(' NOT CONVERGED                    ') 
C
C**** CHECK FOR ANALYSIS TERMINATION FOR THE THERMAL PROBLEM
C
       IF(NSSTEPT.GT.1)
     .  CALL FINISHT(DISPRT,DISTOT,ELPRET,ELVART,HEADST,HTLODT,
     .               IFFIXT,REFORT,RLOADT,TLOADT,PWORKT,TEMPIT)
C
      ENDDO             ! LOOP OF SUBINCREMENTS
C
C**** WRITES THE INTERCHANGES ITERATION NUMBER
C
      IITERC=1
      WRITE(LUPRI,900) IITERC
      WRITE(LUPRIT,900) IITERC
  900 FORMAT('INTERCHANGES ITERATION NUMBER = ',I5)
C
C**** TRANSFER THERMAL VARIABLES TO MECHANICAL PROBLEM
C
      CALL TRASTER(DISTOT,TEMPIT,FPCHAT,PWORKT,TEMPN,DTEMP,FPCHA,PWORK)
C
C**** ADVANCE ON TIME AND PREDICT RESPONSE FOR THE MECHANICAL PROBLEM
C
      DO ISSTEP=1,NSSTEP             !  SUBINCREMENTS
C
       IF(NSSTEP.GT.1) CALL SUBINC(TEMPN,DTEMP,FPCHA,PWORK,PWORKT,    0)
C
       CALL TMSTEP(DISIT,DISPR,DISTO,ELDAT,ELPRE,ELVAR,ELMAT,
     .             HEADS,HTLOD,IFFIX,PRESC,LNODS,MATNO,PROEL,
     .             PROPS,REFOR,RLOAD,RLOAH,FICTO,TFICT,TLOAD,
     .             TEMPN,DTEMP,INFRI,COFRI,PWORK,PREAS,TGAPS,
     .             VNORM,COORD,FPCHA,LACTI,LPNTN,NOPRF,PRESF,
     .             PREHF,VANIS,WORK1,NEQNS)
C
C**** LOOP ON ITERATIONS FOR THE MECHANICAL PROBLEM
C
       DO WHILE
     .  ((NCHEK.NE.0).AND.(IITER.LE.MITER))
C
C**** ITERATIVE CORRECTION FOR THE MECHANICAL PROBLEM
C
        CALL ITERAT(DISIT,DISPR,DISTO,ELDAT,ELPRE,ELVAR,ELMAT,
     .              HEADS,IFFIX,PRESC,LNODS,MATNO,PROEL,PROPS,
     .              REFOR,RLOAD,FICTO,TLOAD,TEMPN,DTEMP,INFRI,
     .              COFRI,PWORK,PREAS,TGAPS,VNORM,LPNTN,COORD,
     .              FPCHA,LACTI,NOPRF,PRESF,VANIS,WORK1,NEQNS)
C
       ENDDO
C
       IF(NCHEK.NE.0)
     .  CALL RUNEND(' NOT CONVERGED                    ')
C
C**** CHECK FOR ANALYSIS TERMINATION FOR THE MECHANICAL PROBLEM
C
       IF(NSSTEP.GT.1)
     .  CALL FINISH(DISPR,DISTO,ELPRE,ELVAR,HEADS,HTLOD,IFFIX,
     .              REFOR,RLOAD,TLOAD,INFRI,COFRI,LACTI)
C
       IF(NSSTEP.GT.1) CALL SUBINC(TEMPN,DTEMP,FPCHA,PWORK,PWORKT,    1)
C
      ENDDO             ! LOOP OF SUBINCREMENTS
C
      RETURN
      END
