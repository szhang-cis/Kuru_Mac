      SUBROUTINE INPDATT(ELDATT,ELPRET,ELVART,ELMATT,LNODST,MATNOT,
     .                   PROELT,PROPST,COORDT,WORK1T)
C***********************************************************************
C
C**** THIS ROUTINE ACCEPTS MOST OF THE INPUT DATA
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
C**** ADDITIONAL PARAMETERS
C
      INCLUDE 'addi_omt.f'
C
C**** THERMAL VARIABLES
C
      INCLUDE 'prob_omt.f'
      INCLUDE 'inte_omt.f'
      INCLUDE 'auxl_omt.f'
      INCLUDE 'inpo_omt.f'
C
      COMMON/LDFILET/ITAPET
      COMMON/ERRORST/NERORT
      COMMON/FREEPAT/FPARAT(10)
C
      DIMENSION MATNOT(NELEMT),        LNODST(NNODET,NELEMT),
     .          PROELT(NPRELT,NGRUPT), PROPST(NPROPT,NMATST),
     .          ELDATT(NDATAT),        ELPRET(NPREVT),
     .          ELVART(NSTATT),        ELMATT(NMATXT),
     .          COORDT(NDIMET,NPOINT), WORK1T(*)
C
      PARAMETER (MCOMMT=6)
      CHARACTER*5  COMMDT(MCOMMT)
      DATA COMMDT/'GEOME','PROPE','GRAVI','SETS','FREE_','END_G'/
C
      DATA FPARAT/10*0.0/
C
C**** BEGIN
C
      CALL CPUTIMT(TIME1T)
      WRITE(LUREST,900)
C
C**** LOOK FOR 'GENERAL_DATA' CARD
C
      NPRINT=0
      ITAPET=LUDATT
      CALL LISTENT('INPDATT',NPRINT,ITAPET)
      IF(WORDST(1).NE.'GENER')
     . CALL RUNENDT('INPDATT:WRONG GENERAL_DATA CARD    ')
C
C**** READ NEW COMMAND
C
  100 NPRINT=0
      ITAPET=LUDATT
      CALL LISTENT('INPDATT',NPRINT,ITAPET)
C
C**** IDENTIFY COMMAND
C
      DO ICOMMT=1,MCOMMT
       IF(WORDST(1).EQ.COMMDT(ICOMMT)) GO TO 200
      END DO
      CALL RUNENDT('INPDATT:WRONG CARD                 ')
C
C**** EXECUTE APROPRIATE COMMAND
C
  200 IF(PARAMT(1).NE.0.0) ITAPET=INT(PARAMT(1))
      GOTO (1,2,3,4,5,6), ICOMMT
C
C**** READ GEOMETRY
C
C     IBOEMT=0 > DOES NOT CHECK THE COORDINATES
C     IBOEMT=1 > CHECK THE COORDINATES CONSIDERED INTO THE THERMAL
C                CONTACT ("GAP") ELEMENTS
C
   1  INTERT=0
      IF(WORDST(2).EQ.'INTER') INTERT=1
C
      IBOEMT=0
      IF(NMEMO1.EQ.0) THEN           ! coordinates in an elemental array
       CALL INPGEOT(WORK1T(ISTART(1)),ELDATT,INTERT,ITAPET,LNODST,
     .              MATNOT,PROELT,IBOEMT)
      ELSE                           ! coordinates in a global array
       CALL INPGEOT(COORDT,ELDATT,INTERT,ITAPET,LNODST,
     .              MATNOT,PROELT,IBOEMT)
      ENDIF
      GO TO 100 ! Read next card
C
C**** READ MATERIAL PROPERTIES
C
   2  CALL INPPROT(ITAPET,PROPST,    0)
      GO TO 100 ! Read next card
C
C**** READ GRAVITY CONSTANT
C
   3  IF(PARAMT(1).NE.0.0) GRAVYT=PARAMT(1)
      WRITE(LUREST,905) GRAVYT
      GO TO 100 ! Read next card
C
C**** READ ELEMENT CHARACTERISTICS
C
   4  CALL INPSETT(ITAPET,PROELT)
      IBOEMT=1
      IF(NMEMO1.EQ.0) THEN           ! coordinates in an elemental array
       CALL INPGEOT(WORK1T(ISTART(1)),ELDATT,INTERT,ITAPET,LNODST,
     .              MATNOT,PROELT,IBOEMT)
      ELSE                           ! coordinates in a global array
       CALL INPGEOT(COORDT,ELDATT,INTERT,ITAPET,LNODST,
     .              MATNOT,PROELT,IBOEMT)
      ENDIF
      GO TO 100 ! Read next card
C
C**** READ FREE PARAMETERS
C
   5  DO IIPART=1,NNPART
       FPARAT(IIPART)=PARAMT(IIPART)
      ENDDO
      GO TO 100 ! Read next card
C
C**** END OF READING: PROCESS THE INPUT DATA
C
   6  CONTINUE
C
C**** LOOP ON ELEMENTS
C
      NERORT=0
C
      DO IELEMT=1,NELEMT
       LGRUPT=MATNOT(IELEMT)
       LMATST=INT(PROELT(1,LGRUPT))
C
C**** CHECK ON INTEGRATION RULES
C
       CALL ELMLIBT(LNODST(1,IELEMT),PROELT(1,LGRUPT),PROPST(1,LMATST),
     .              ELDATT,ELPRET,ELVART,ELMATT,DUMMYT,WORK1T,     9)
C
      END DO                    ! END LOOP ON ELEMENTS
C
      CALL CPUTIMT(TIME2T)
      CPUDAT=CPUDAT+(TIME2T-TIME1T)
C
      IF(NERORT.NE.0)
     . CALL RUNENDT('INPDATT:ERROR IN INTEGRATION RULE  ') 
C
      RETURN
C
  900 FORMAT(1H1,///,10X,'ECHO OF INPUT DATA FOLLOWS :',/
     .               10X,'=========================='/)
  905 FORMAT(//,6X,  'GRAVITY CONSTANT VALUE =',F10.5)
C
      END
