      SUBROUTINE UPDATE(ASDIT,ASDTF,DISIN,DISIT,NELEM,NEVAB,NTOTV,
     .                  RLOAD,TLOAD)
C***********************************************************************
C
C**** THIS ROUTINE CALCULATES THE CHANGE IN LOAD STEP ACCORDING TO 
C     SELECTED PATH AND UPDATES LOAD LEVEL AND DISPLACEMENT VECTORS
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'inte_om.f'
C
      COMMON/LOGUN/LUDTS,LUSOL,LUFRO,LUFRH,LUDAT,LUPRI,LURES,
     .             LUSO2,LUFR2,LUPOS,LURST,LUBFG,LUPIP,LUPAN,LUINF,
     .             LUGEO,LUSET,LUMAT,LUINI,LULOA,LUFIX,LUIN1,
     .             LUTUN,LUCON,LUACT,LUFAN,
     .             LUCU1,LUCU2,LUCU3,LUCU4,LUCU5,LUCU6,LUCU7,
     .             LUCU8,LUCU9,LUC10
C
      DIMENSION ASDIT(*), ASDTF(*), DISIN(*), 
     .          DISIT(*), RLOAD(NTOTV,*), TLOAD(*)
      SAVE FACTF
C
C***FOR THE FIRST ITERATION COMPUTE FACTO
C
      IF(IITER.EQ.1) THEN
C
        DO 1 ITOTV=1,NTOTV
    1   DISIN(ITOTV)=0.
C
        ARCLN=ARCLN*DSQRT(DITER/PITER)
C
        IF(KARCL.EQ.4) THEN                      ! FOR DISPL. CONTROL
          FACTO =(ARCLN-DISIT(NCDIS))/ASDIT(NCDIS)
        ELSE                                     ! FOR ARC-LENGTH
          DISPS = 0.0     
          SIGNR = 0.0                  
          DO 5 ITOTV = 1,NTOTV
          ASDTF(ITOTV) = ASDIT(ITOTV)
          SIGNR = SIGNR + ASDIT(ITOTV)*RLOAD(ITOTV,1)
    5     DISPS = DISPS + ASDIT(ITOTV)*ASDIT(ITOTV)
          SIGNR = SIGNR/DABS(SIGNR)
          FACTO = SIGNR*ARCLN/DSQRT(DISPS)
        ENDIF
C
        TFACT=TFACT+FACTO                    ! UPDATE LOAD FACTOR
        FACTF=FACTO
        DELTA=FACTO
        WRITE(LURES,900) ISTEP,TFACT,FACTO 
C
      ELSE
C
C***FOR ANY OTHER ITERATION COMPUTE DELTA ACCORDING TO PATH
C
        PROD1=0.0
        PROD2=0.0
        PROD3=0.0
C
        IF(KARCL.EQ.1) THEN                  ! NORMAL PLANE PATH
          DO 25 ITOTV=1,NTOTV
          VALUE=ASDTF(ITOTV)*FACTF
          PROD1=PROD1+VALUE*DISIT(ITOTV)
   25     PROD2=PROD2+VALUE*ASDIT(ITOTV)
          DELTA=-PROD1/PROD2
        ENDIF
C
        IF(KARCL.EQ.2) THEN                  ! UPDATED NORMAL PLANE PATH
          DO 35 ITOTV=1,NTOTV
          VALUE = DISIN(ITOTV)
          PROD1 = PROD1 + VALUE*DISIT(ITOTV)
   35     PROD2 = PROD2 + VALUE*ASDIT(ITOTV)
          DELTA =-PROD1 / PROD2
        ENDIF
C
        IF(KARCL.EQ.3) THEN                  ! SPHERICAL PATH
C
          DO 50 ITOTV=1,NTOTV
          VALU1=ASDIT(ITOTV)
          VALU3=DISIN(ITOTV)+DISIT(ITOTV)
          PROD1=PROD1+VALU1*VALU1
          PROD2=PROD2+VALU3*VALU1
          PROD3=PROD3+VALU3*VALU3
   50     CONTINUE
C
          AVALU = PROD1
          BVALU = 2.0 * PROD2
          CVALU = PROD3 - ARCLN * ARCLN
          USQRT = BVALU * BVALU - 4.0 * AVALU * CVALU
          USQRT = SQRT(USQRT)
          ROOT1 = (-BVALU+USQRT)/(2.0*AVALU)
          ROOT2 = (-BVALU-USQRT)/(2.0*AVALU)
          ANGL1 = 0.0
          ANGL2 = 0.0
C
          DO 60 ITOTV=1,NTOTV
          VALU1 = DISIN(ITOTV)
          VALU2 = DISIT(ITOTV)
          VALU3 = ASDIT(ITOTV)
          VALU4 = VALU1 + VALU2 + ROOT1*VALU3
          VALU5 = VALU1 + VALU2 + ROOT2*VALU3
          ANGL1 = ANGL1 + VALU4*VALU1
          ANGL2 = ANGL2 + VALU5*VALU1
   60     CONTINUE
C
          IF(ANGL1.GT.0.0.AND.ANGL2.GT.0.0) THEN
            RLINR = -CVALU / BVALU
            DIFF1 = ABS(ROOT1-RLINR)
            DIFF2 = ABS(ROOT2-RLINR)
            DELTA = ROOT1
            IF(DIFF1.GT.DIFF2) DELTA = ROOT2
         ELSE
            IF(ANGL1.GT.0.0) DELTA = ROOT1
            IF(ANGL2.GT.0.0) DELTA = ROOT2
          ENDIF
C
        ENDIF
C
        IF(KARCL.EQ.4) THEN                  ! DISPLACEMENT CONTROL
          DELTA = -DISIT(NCDIS)/ASDIT(NCDIS)
        ENDIF
C
C***CORRECT LOAD LEVEL
C
        FACTO = FACTO + DELTA
        TFACT = TFACT + DELTA
        WRITE(LURES,910) TFACT,FACTO 
      ENDIF
C
C***FOR EVERY ITERATION UPDATE LOAD VECTORS &
C                COMPUTE COMPLETE ITERATIVE DISPLACEMENT VECTOR
C
      DO 90 ITOTV=1,NTOTV
cc      TLOAD(ITOTV)=TLOAD(ITOTV)+DELTA*RLOAD(ITOTV,2)
  90  DISIT(ITOTV)=DISIT(ITOTV)+DELTA*ASDIT(ITOTV)
C
      RETURN
C
  900 FORMAT(1H1,//,132('*'),/,
     .       10X,'LOAD STEP NO.',I5,/,
     .       15X,'INITIAL LOAD LEVEL  =',E15.6,/,
     .       15X,'INITIAL LOAD STEP   =',E15.6,/132('*'))
  910 FORMAT(//,132('='),//,
     .       15X,'UPDATED LOAD LEVEL  =',E15.6,/,
     .       15X,'UPDATED LOAD STEP   =',E15.6,/)
      END
