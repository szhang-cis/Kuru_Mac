      SUBROUTINE INPDAT(ELDAT,ELPRE,ELVAR,ELMAT,LNODS,MATNO,PROEL,
     .                  PROPS,COORD,VANIS,WORK1)
C***********************************************************************
C
C**** THIS ROUTINE ACCEPTS MOST OF THE INPUT DATA
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
C**** ADDITIONAL PARAMETERS
C
      INCLUDE 'addi_om.f'
C
C**** COUPLING VARIABLES
C
      INCLUDE 'nuec_om.f'
C
C**** MECHANICAL VARIABLES
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
      INCLUDE 'inpo_om.f'
C
      COMMON/LDFILE/ITAPE
      COMMON/ERRORS/NEROR,NEROB,NEROT,NEROI
      COMMON/FREEPA/FPARA(10)
C
      DIMENSION MATNO(NELEM),       LNODS(NNODE,NELEM),
     .          PROEL(NPREL,NGRUP), PROPS(NPROP,NMATS),
     .          ELDAT(NDATA),       ELPRE(NPREV),
     .          ELVAR(NSTAT),       ELMAT(NMATX),
     .          COORD(NDIME,NPOIN), VANIS(NANIV,NANIC,NELEM),
     .          WORK1(*)
C
      PARAMETER (MCOMM=7)
      CHARACTER*5  COMMD(MCOMM)
      DATA COMMD/'GEOME','PROPE','TUNIN','SETS','FREE_','CONTA','END_G'/
C
      DATA FPARA/10*0.0/
C
C**** BEGIN
C
      CALL CPUTIM(TIME1)
      WRITE(LURES,900)
C
C**** LOOK FOR 'GENERAL_DATA' CARD
C
      NPRIN=0
      ITAPE=LUDAT
      CALL LISTEN('INPDAT',NPRIN,ITAPE)
      IF(WORDS(1).NE.'GENER')
     . CALL RUNEND('INPDAT: WRONG GENERAL_DATA CARD    ')
C
C**** READ NEW COMMAND
C
  100 NPRIN=0
      ITAPE=LUDAT
      CALL LISTEN('INPDAT',NPRIN,ITAPE)
C
C**** IDENTIFY COMMAND
C
      DO ICOMM=1,MCOMM
        IF(WORDS(1).EQ.COMMD(ICOMM)) GO TO 200
      END DO
      CALL RUNEND('INPDAT: WRONG CARD                 ')
C
C**** EXECUTE APROPRIATE COMMAND
C
  200 ITAPE=LUDAT
      IF(PARAM(1).NE.0.0) ITAPE=INT(PARAM(1))
      GOTO (1,2,3,4,5,6,7), ICOMM
C
C**** READ GEOMETRY
C
C     IBOEMT=0 > DOES NOT CHECK THE COORDINATES
C     IBOEMT=1 > CHECK THE COORDINATES CONSIDERED INTO THE MECHANICAL
C                CONTACT ELEMENTS
C
   1  INTER=0
      IF(WORDS(2).EQ.'INTER') INTER=1
C
      IBOEM=0
      IF(NMEMO1M.EQ.0) THEN          ! coordinates in an elemental array
       CALL INPGEO(WORK1(ISTAR(1)),ELDAT,INTER,ITAPE,LNODS,MATNO,PROEL,
     .             IBOEM)
      ELSE                           ! coordinates in a global array
       CALL INPGEO(COORD,ELDAT,INTER,ITAPE,LNODS,MATNO,PROEL,IBOEM)
      ENDIF
      GO TO 100 ! Read next card
C
C**** READ MATERIAL PROPERTIES
C
   2  CALL INPPRO(ITAPE,PROPS,VANIS,MATNO,PROEL,COORD,LNODS,    0)
      CALL POINTE
      GOTO 100 ! Read next card
C
C**** READ TUNING PARAMETERS (at GENERAL_DATA level)
C
   3  CALL INPTUN(ITAPE,PROEL)
      GOTO 100 ! Read next card
C
C**** READ ELEMENT CHARACTERISTICS
C
   4  CALL INPSET(ITAPE,PROEL)
C
      IBOEM=1
      IF(NMEMO1M.EQ.0) THEN          ! coordinates in an elemental array
       CALL INPGEO(WORK1(ISTAR(1)),ELDAT,INTER,ITAPE,LNODS,MATNO,PROEL,
     .             IBOEM)
      ELSE                           ! coordinates in a global array
       CALL INPGEO(COORD,ELDAT,INTER,ITAPE,LNODS,MATNO,PROEL,IBOEM)
      ENDIF
      GO TO 100 ! Read next card
C
C**** READ FREE PARAMETERS
C
   5  DO IIPAR=1,NNPAR
       FPARA(IIPAR)=PARAM(IIPAR)
      ENDDO
      GOTO 100 ! Read next card
C
C**** READ CONTACT CONTROL PARAMETERS (always external file)
C
   6  ICONC=1
      GOTO 100 ! Read next card
C
C**** END OF READING: PROCESS THE INPUT DATA
C
   7  CONTINUE
C
C**** LOOP ON ELEMENTS
C
      NEROR=0
      NEROB=0
      NEROT=0
      NEROI=0
C
      DO IELEM=1,NELEM
       LGRUP=MATNO(IELEM)
       LMATS=INT(PROEL(1,LGRUP))
C
C**** CHECK ON INTEGRATION RULES
C
       CALL ELMLIB(LNODS(1,IELEM),PROEL(1,LGRUP),PROPS(1,LMATS),
     .             WORK1,WORK1,WORK1,WORK1,WORK1,
     .             ELDAT,ELPRE,ELVAR,ELMAT,WORK1,    9)
C
      END DO                    ! END LOOP ON ELEMENTS
C
      CALL CPUTIM(TIME2)
      CPUDA=CPUDA+(TIME2-TIME1)
C
      IF(NEROR.NE.0)
     . CALL RUNEND('INPDAT: ERROR IN INTEGRATION RULE  ') 
      IF(NEROB.NE.0)
     . CALL RUNEND('INPDAT: ERROR IN INDEX KPARB       ')
      IF(ITERME.GE.0) THEN
       IF(NEROT.NE.0)
     .  CALL RUNMEN('INPDAT: WARNING IN INDEX KPART     ')
      ENDIF
      IF(NEROI.NE.0)
     . CALL RUNMEN('INPDAT: WARNING IN INDEX KPARI     ')
C
      RETURN
C
  900 FORMAT(1H1,///,10X,'ECHO OF INPUT DATA FOLLOWS :',/
     .               10X,'=========================='/)
c  905 FORMAT(//,6X,  'GRAVITY CONSTANT VALUE =',F10.5)
C
      END
