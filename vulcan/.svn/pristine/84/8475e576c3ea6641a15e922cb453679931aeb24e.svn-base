
      SUBROUTINE STIF02(EHIST,ELCOD,PROPS,ESTIF,HSTIF)
C*****************************************************************
C
C****THIS ROUTINE COMPUTES THE STIFFNESS & COUPLING MATRICES
C         ( ELEMENT NO. 2 )
C
C*****************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
C
      COMMON/REHMAT/KFLAG,IFLAG
C
      DIMENSION EHIST(NHIST,*), ELCOD(NDIME,*),      PROPS(*)
      DIMENSION ESTIF(NKOVA),   HSTIF(NEVAB,NNODE)
      DIMENSION DCOSI(3)
      DATA TWOPI/6.283185307179586/
C
C***SET UP THE ELASTIC MATRIX
C
      YOUNG= PROPS(2)
      AREAC= PROPS(11)
C
C***COMPUTE LENGTH OF THE ELEMENT AND DIRECTOR COSINES
C
      ALENG=0.0D00
      DO IDIME=1,NDIME
      ALENG=ALENG+(ELCOD(IDIME,1)-ELCOD(IDIME,2))**2
      ENDDO
      ALENG=DSQRT(ALENG)
C
      DO IDIME=1,NDIME
      DCOSI(IDIME)=(ELCOD(IDIME,2)-ELCOD(IDIME,1))/ALENG
      ENDDO
C
C***CALCULATE THE MATERIAL STIFFNESS MATRIX
C
      FACTR=YOUNG*AREAC/ALENG
      IF(NTYPE.EQ.3)THEN
        RADUS=(ELCOD(1,1)+ELCOD(1,2))/2.0
        FACTR=FACTR*RADUS*TWOPI
      ENDIF
C
      DO INDEX=    1,NDIME
      DO JNDEX=INDEX,NDIME
        IPOS1=((INDEX-1)*(2*NDIME-INDEX))/2+JNDEX
        IPOS2=((INDEX-1)*(2*NDIME-INDEX))/2+JNDEX+NDIME
        IPOS3=((JNDEX-1)*(2*NDIME-JNDEX))/2+INDEX+NDIME
        IPOS4=((INDEX+NDIME-1)*(2*NDIME-(INDEX+NDIME)))/2+JNDEX+NDIME
        TERMO=FACTR*DCOSI(INDEX)*DCOSI(JNDEX)
        ESTIF(IPOS1)  = TERMO 
        ESTIF(IPOS2)  =-TERMO 
        ESTIF(IPOS3)  =-TERMO 
        ESTIF(IPOS4)  = TERMO 
      ENDDO
      ENDDO
C
      RETURN
      END
