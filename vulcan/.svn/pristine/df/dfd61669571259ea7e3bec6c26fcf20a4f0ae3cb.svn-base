      SUBROUTINE INVARS(STRES,DEVIA,NTYPE,PMEAN,QINVA,THETA,VARJ2,
     .                  VARJ3)
C***********************************************************************
C
C**** THIS ROUTINE EVALUATES THE DEVIATORIC STRESS AND ITS 
C     INVARIANTS
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      DIMENSION DEVIA(*),STRES(*)
C
      DATA ROOT3/1.732050807568877/
C
      IF(NTYPE.EQ.5) THEN
       DEVIA(1)=2.0/3.0*STRES(1)
      ENDIF
C
      PMEAN=(STRES(1)+STRES(2)+STRES(4))/3.0
      DEVIA(1)=STRES(1)-PMEAN
      DEVIA(2)=STRES(2)-PMEAN
      DEVIA(3)=STRES(3)
      DEVIA(4)=STRES(4)-PMEAN
C
      IF(NTYPE.EQ.4)GOTO 10
C
C***2-D CASE
C
      VARJ2=DEVIA(3)*DEVIA(3)+
     . 0.5*(DEVIA(1)*DEVIA(1)+DEVIA(2)*DEVIA(2)+DEVIA(4)*DEVIA(4))
      VARJ3=DEVIA(4)*(DEVIA(4)*DEVIA(4)-VARJ2)
      GOTO 20
C
C***3-D CASE
C
   10 DEVIA(5)=STRES(5)
      DEVIA(6)=STRES(6)
C
      VARJ2= DEVIA(3)*DEVIA(3)+DEVIA(5)*DEVIA(5)+DEVIA(6)*DEVIA(6)
     . +0.5*(DEVIA(1)*DEVIA(1)+DEVIA(2)*DEVIA(2)+DEVIA(4)*DEVIA(4))
      VARJ3= DEVIA(1)*DEVIA(2)*DEVIA(4)+DEVIA(3)*DEVIA(5)*DEVIA(6)*2.
     .     -(DEVIA(1)*DEVIA(6)*DEVIA(6)+DEVIA(2)*DEVIA(5)*DEVIA(5)
     .      +DEVIA(4)*DEVIA(3)*DEVIA(3))
C
C***CALCULATE THE THREE STRESS INVARIANT
C
   20 CONTINUE
      STEFF=DSQRT(VARJ2)
      QINVA=ROOT3*STEFF
C
      SINT3=1.0
      DENOM=VARJ2*STEFF
      IF(DENOM.NE.0.0)SINT3=-3.0*ROOT3*VARJ3/(2.0*DENOM)
      IF(SINT3.LT.-0.9986295348) SINT3=-1.0
      IF(SINT3.GT. 0.9986295348) SINT3= 1.0
      THETA=DASIN(SINT3)/3.0
C
      RETURN
      END
