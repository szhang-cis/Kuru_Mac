      SUBROUTINE BRIBA2(DVPLA,EHIST,NCRIT,NTYPE,PROPS,SGTOT,VSTOT)
C***********************************************************************
C
C****THIS ROUTINE BRINGS BACK THE STRESS POINT ON THE SURFACE
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION DEVIA(6), EHIST(*), PROPS(*), SGTOT(*)
C
C***CALCULATE THE VARIATION OF THE EQUIVALENT MEAN PRESSURE
C
      EQUMN=EHIST(5)
      DCOEF=PROPS(31)
      HLAMD=PROPS(32)
      CHAIS=PROPS(33)
      RATIO=PROPS(34)
      FACTO=PROPS(35)
C
      DEQUM=-VSTOT*EQUMN*DVPLA/(HLAMD-CHAIS)
      EQUMN=EQUMN+DEQUM
      HPARA=(DCOEF-EQUMN)/(FACTO+1)
      BETAS=DCOEF-FACTO*HPARA
C
      EHIST(3)=HPARA
      EHIST(4)=BETAS
      EHIST(5)=EQUMN
C
      CALL INVARS(SGTOT,DEVIA,NTYPE,PMEAN,QINVA,THETA,VARJ2,
     .            VARJ3)
      IF(NCRIT.EQ.4)GOTO 10
C
C***DETERMINE RATIO
C
      CALL VARTHE(CAPTH,CINTE,FINCL,IFLAG,PROPS,THETA)
      RATIO=FACTO*FINCL
C
C***BRING BACK THE STRESS POINT ON THE SURFACE
C
   10 CONTINUE
      QULIN=RATIO*DSQRT(HPARA*HPARA-(PMEAN-BETAS)*(PMEAN-BETAS))
      REDUC=QULIN/QINVA
      SGTOT(1)=REDUC*DEVIA(1)+PMEAN   
      SGTOT(2)=REDUC*DEVIA(2)+PMEAN   
      SGTOT(3)=REDUC*DEVIA(3)
      SGTOT(4)=REDUC*DEVIA(4)+PMEAN   
C
      IF(NTYPE.NE.4)RETURN
C
      SGTOT(5)=REDUC*DEVIA(5)
      SGTOT(6)=REDUC*DEVIA(6)
C
      RETURN
      END
