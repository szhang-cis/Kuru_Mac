      SUBROUTINE PRETEM(ELPRE,LNODS,MATNO,PROEL,TEMPC)
C***********************************************************************
C
C**** THIS ROUTINE READS THE TEMPERATURE DATA AT NODES
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
C
      COMMON/PRFILE/JTAPE,IFLAG
C
      DIMENSION ELPRE(*),           LNODS(NNODE,NELEM), MATNO(*), 
     .          PROEL(NPREL,NGRUP), TEMPC(NPOIN,4)
      DIMENSION SUBTA(12)
C
      TWOPI=6.283185307179586
C
C**** READ NUMBER OF NODES WITH TEMPERATURE DATA
C
      READ(JTAPE,900) NPOI1
      WRITE(LURES,905) NPOI1
      IF(NPOIN.LT.NPOI1) THEN
        WRITE(LURES,910)
        CALL RUNEND('ERROR IN PRETEM                    ')
      ENDIF
C
C**** READ TEMPERATURE DATA
C
      READ(JTAPE,915) SUBTA
      WRITE(LURES,916)    SUBTA
C
      DO INDEX=1,4
        DO IPOIN=1,NPOIN
          TEMPC(IPOIN,INDEX)=0.0
        ENDDO
      ENDDO
C       
      DO IPOIN=1,NPOI1
        READ(JTAPE,920) JPOIN,(TEMPC(JPOIN,I),I=1,4)
      ENDDO
C
C**** ECHO TEMPERATURE DATA FOR THE WHOLE MESH
C
      WRITE(LURES,925)
      WRITE(LURES,930) (IPOIN,(TEMPC(IPOIN,INDEX),INDEX=1,4),
     .                                            IPOIN=1,NPOIN)
C
C**** AVERAGE DATA FOR EACH ELEMENT FROM NODAL VALUES
C     AND STORE IT IN DATA BASE
C
      INDE1=IPREV(3)-1
C
      DO 100 IELEM=1,NELEM
C
      LGRUP=MATNO(IELEM)
      NNODL=INT(PROEL(2,LGRUP))
      ANODE=FLOAT(NNODL)
C
C**** READ ELPRE FROM RESTART-FILE
C
      CALL DATBAS(ELPRE,    2,    2)
C
C**** COMPUTE AVERAGE VALUES
C
      DO INDEX=1,4
        DUMMY=0.0D00
        DO INODE=1,NNODL
          LNODE=LNODS(INODE,IELEM)
          DUMMY=DUMMY+TEMPC(LNODE,INDEX)
        ENDDO
        ELPRE(INDE1+INDEX)=DUMMY/ANODE
      ENDDO
      ELPRE(INDE1+4)=TWOPI/ELPRE(INDE1+4)
C
C**** WRITE ELPRE TO RESTART-FILE
C 
      CALL DATBAS(ELPRE,    2,    1)
C
  100 CONTINUE
C
      RETURN
  900 FORMAT(2I5)
  905 FORMAT(/,5X,'NUMBER OF POINTS WITH TEMPERATURE DATA:',I5,/)
  910 FORMAT(//,
     .    '***ERROR: # OF POINTS WITH TEMPERATURE DATA.GT.NPOIN')
  915 FORMAT(12A6)
  916 FORMAT(5X,12A6)
  920 FORMAT(I5,4E15.6)
  925 FORMAT(2(1X,'NODE',5X,'MEAN TEMP.',5X,' AMPLITUDE',
     .                   5X,'PHASE ANG.',5X,'    PERIOD',7X),/)
  930 FORMAT(2(I5,4(5X,F15.6),7X))
      END
