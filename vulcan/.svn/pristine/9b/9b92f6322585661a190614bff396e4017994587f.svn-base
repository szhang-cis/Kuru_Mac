      SUBROUTINE STIF04A(PROPS,ESTIF,TENOD,ELDIS,ELCOD,DISPL,VNORL)
C***********************************************************************
C
C**** THIS ROUTINE COMPUTES THE STIFFNESS MATRIX FOR THE NODAL GAP 
C     ELEMENT NO. 4
C
C**** PROPERTIES FOR THE CONTACT PROBLEM
C
C     PROPS(2)=RIGIN            : NORNAL STIFFNESS
C     PROPS(5)=TEMPL            : LIQUIDUS TEMPERATURE
C     PROPS(6)=INOTE            : DENOTES WHICH NODE BELONGS TO PIECE
C
C**** STABILIZATION PARAMETERS (CONTROL AT MATERIAL PROPERTIES AND/OR
C     INTERVAL DATA LEVEL) COMING FROM A COMMON OF TUNING PARAMETERS
C
C     IITEF                     : ITERATION FROM WHICH TRUPL ACTS
C     TRUPL                     : FIRST STIFFNESS IN TENSION
C     TRUPM                     : SECOND STIFFNESS IN TENSION
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
C
      COMMON/TUNING4/RITEN,RITEF,TRUPL,TRUPM,TOLGA,TOLGAM
C
      DIMENSION PROPS(NPROP),       ESTIF(NKOVA)
      DIMENSION TENOD(NNODL)
      DIMENSION ELDIS(NDOFN,NNODL), ELCOD(NDIME,NNODL)
      DIMENSION DISPL(NDOFN,*),     VNORL(NDIME,NNODL)
      DIMENSION AUXS1(3,3),         AUXS2(6,6),
     .          VERSO(3)
C
      RIGIN=PROPS(2)
      TEMPL=PROPS(5)
      INOTE=INT(PROPS(6))
C
      IITEF=INT(RITEF)
C
C**** OUTWARD UNIT NORMAL TO BODY 1
C
      TOLNOR=-1.0E-08
      VNORC=0.0
      DO IDIME=1,NDIME
       VNORA=VNORL(IDIME,1)*VNORL(IDIME,2)
       IF(VNORA.GT.TOLNOR) VNORL(IDIME,1)=0.0
       VNORA=VNORL(IDIME,1)
       VNORC=VNORC+VNORA*VNORA
      ENDDO
      VNORC=DSQRT(VNORC)
      DO IDIME=1,NDIME
       VNORA=VNORL(IDIME,1)
       VERSO(IDIME)=VNORA/VNORC
      ENDDO
C
C**** INITIALISE AUXS1
C
      DO IDOFN=1,NDOFN
       DO JDOFN=IDOFN,NDOFN
        AUXS1(IDOFN,JDOFN)=0.0
       ENDDO
      ENDDO
C
C**** COMPUTE NORMAL GAP (CURRENT)
C
C     DGAPN  > 0: CONTACT PROBLEM
C     DGAPN <= 0: NO CONTACT PROBLEM
C
      DGAPN=0.0
      DO IDOFN=1,NDOFN
       PROYE=VERSO(IDOFN)
       ELDI1=ELDIS(IDOFN,1)
       ELDI2=ELDIS(IDOFN,2)
       DGAPN=DGAPN+PROYE*(ELDI1-ELDI2)
      END DO
C
C**** COMPUTE NORMAL GAP (LAST CONVERGED)
C
      DGAPA=0.0
      DO IDOFN=1,NDOFN
       PROYE=VERSO(IDOFN)
       ELDI1=ELDIS(IDOFN,1)-DISPL(IDOFN,1)
       ELDI2=ELDIS(IDOFN,2)-DISPL(IDOFN,2)
       DGAPA=DGAPA+PROYE*(ELDI1-ELDI2)
      END DO
C
C**** COMPUTE CONTACT RIGIDITY
C
      IF(TENOD(INOTE).LT.TEMPL) THEN
C
C**** "STABILIZED" CONTACT RIGIDITY
C
       IF(DGAPN.LE.0.0) THEN
        IF(IITER.GT.IITEF) RIGIN=RIGIN*TRUPL
cgap        IF(DGAPA.LE.0.0)   RIGIN=RIGIN*TRUPM
        IF(DGAPA.LE.TOLGA)   RIGIN=RIGIN*TRUPM
       ENDIF
      ENDIF       !tenod(inote).lt.templ
C
C**** COMPUTE ONLY A FORTH PART OF ELEMENTAL CONTACT MATRIX (UPPER
C     TRIANGLE)
C
      DO IDOFN=1,NDOFN
       PROYI=VERSO(IDOFN)
       DO JDOFN=IDOFN,NDOFN
        PROYJ=VERSO(JDOFN)
        AUXS1(IDOFN,JDOFN)=AUXS1(IDOFN,JDOFN)+RIGIN*PROYI*PROYJ
       ENDDO
      ENDDO
C
C**** ELEMENTAL CONTACT MATRIX (UPPER TRIANGLE)
C
      DO IDOFN=1,NDOFN
       DO JDOFN=IDOFN,NDOFN
        AUXS2(IDOFN,JDOFN)=AUXS1(IDOFN,JDOFN)
        AUXS2(IDOFN,JDOFN+NDOFN)=-AUXS1(IDOFN,JDOFN)
        AUXS2(JDOFN,IDOFN+NDOFN)=-AUXS1(IDOFN,JDOFN)
        AUXS2(IDOFN+NDOFN,JDOFN+NDOFN)=AUXS1(IDOFN,JDOFN)
       ENDDO
      ENDDO
C
C**** EVALUATE ELEMENT CONTRIBUTION
C
      DO IEVAB=1,NDOFN*2
       DO JEVAB=IEVAB,NDOFN*2
        ICONB=(2*NEVAB-IEVAB)*(IEVAB-1)/2+JEVAB
        IF(KSYMM.EQ.0) ICONB=(IEVAB-1)*NEVAB+JEVAB  ! unsymmetric
        ESTIF(ICONB)=AUXS2(IEVAB,JEVAB)
       ENDDO
      ENDDO
C
C**** LOAD ESTIF IN A SQUARE FORM FOR UNSYMMETRIC SOLVER
C
      IF(KSYMM.EQ.0) THEN
       DO IEVAB=1,NEVAB
        DO JEVAB=IEVAB,NEVAB
         KLOCS=(IEVAB-1)*NEVAB+JEVAB
         KLOCI=(JEVAB-1)*NEVAB+IEVAB
         ESTIF(KLOCI)=ESTIF(KLOCS)
        END DO
       END DO
      END IF
C
      RETURN
      END
