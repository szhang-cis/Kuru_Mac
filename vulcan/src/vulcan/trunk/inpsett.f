      SUBROUTINE INPSETT(ITAPET,PROELT)
C***********************************************************************
C
C**** THIS ROUTINE READS THE MATERIAL PROPERTIES
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_omt.f'
      INCLUDE 'inte_omt.f'
      INCLUDE 'inpo_omt.f'
C
      DIMENSION PROELT(NPRELT,*)
C
      IERR1T=1
      IF(ITAPET.NE.LUDATT.AND.ITAPET.NE.LUSETT) GOTO 1000
      IF(ITAPET.EQ.LUSETT) THEN       ! external .set file
       OPEN(UNIT=LUSETT,FILE=CB1T,STATUS='OLD',ERR=1000)
       IERR1T=0
C
 1000  IF(IERR1T.NE.0) THEN
        IF(IERR1T.EQ.1) WRITE(LUREST,901)
        CALL RUNENDT('ERROR IN OPENING FILE         ')
       ENDIF
      ENDIF
C
      DO 10 IGRUPT=1,NGRUPT
C
C**** READ ELEMENTS CHARACTERISTICS
C
       NPRINT=1
       CALL LISTENT('INPSETT',NPRINT,ITAPET)
       ISETST=INT(PARAMT(1))
       IMATST=INT(PARAMT(2))
       ITYPET=INT(PARAMT(3))
       NTYPET=INT(PARAMT(4))
       IRULET=INT(PARAMT(5))
       IGAUST=INT(PARAMT(6))
       THICKT=    PARAMT(7)
C
       IF(ISETST.LE.0.OR.ISETST.GT.NGRUPT)
     .  CALL RUNENDT('WRONG ISETST VALUE')
       IF(IMATST.LE.0.OR.IMATST.GT.NMATST)
     .  CALL RUNENDT('WRONG IMATST VALUE')
       IF(ITYPET.NE.5.AND.ITYPET.NE.101.AND.ITYPET.NE.104)
     .  CALL RUNENDT('WRONG ITYPET VALUE')
       IF(NTYPET.LE.0.OR.NTYPET.GT.5)
     .  CALL RUNENDT('WRONG NTYPET VALUE')
       IF(IRULET.LE.0.OR.IRULET.GT.14)
     .  CALL RUNENDT('WRONG IRULET VALUE')
       IF(IRULET.LE.10.AND.IRULET.GT.8)
     .  CALL RUNENDT('WRONG IRULET VALUE')
       IF(IGAUST.LE.0.OR.IGAUST.GT.NGAUST)
     .  CALL RUNENDT('WRONG IGAUST VALUE')
C
       PROELT(1,ISETST)=FLOAT(IMATST)
       PROELT(3,ISETST)=FLOAT(IRULET)
       PROELT(4,ISETST)=FLOAT(IGAUST)
       IF(ITYPET.EQ.0) ITYPET=1
       PROELT(5,ISETST)=FLOAT(ITYPET)
       PROELT(6,ISETST)=FLOAT(NTYPET)
C
C**** SOLID ELEMENTS
C
       IF(ITYPET.EQ.5) THEN
        IF(NTYPET.EQ.1.OR.NTYPET.EQ.2) THEN     ! 2D
         IF(THICKT.EQ.0.0D0)
     .    CALL RUNENDT('ERROR: THICK VALUE EQUAL ZERO')
         PROELT(7,ISETST)=THICKT
        ENDIF
        IF(NTYPET.EQ.3.OR.NTYPET.EQ.4) THEN     ! axisymm. & 3D
         IF(THICKT.NE.0.0D0)
     .    CALL RUNMENT('WARNING: THICK VALUE NOT USED')
         PROELT(7,ISETST)=1.0D0
        ENDIF
        IF(NTYPET.EQ.5) THEN                    ! 1D
         IF(THICKT.EQ.0.0D0)
     .    CALL RUNENDT('ERROR: CROSS AREA VALUE EQUAL ZERO')
         PROELT(7,ISETST)=THICKT
        ENDIF
       ENDIF
C
C**** BOUNDARY ELEMENTS
C
       IF(ITYPET.EQ.101) THEN
        IF(NTYPET.EQ.1.OR.NTYPET.EQ.2.OR.NTYPET.EQ.3) THEN  ! 2D & axys.
         IF(THICKT.NE.0.0D0)
     .    CALL RUNMENT('WARNING: THICK VALUE NOT USED')
         PROELT(7,ISETST)=1.0D0
        ENDIF
        IF(NTYPET.EQ.4) THEN                    ! impossible
         CALL RUNENDT('ERROR: 3D FOR GAP/BOUNDARY IS NOT POSSIBLE')
        ENDIF
        IF(NTYPET.EQ.5) THEN                    ! 1D
         IF(THICKT.EQ.0.0D0)
     .    CALL RUNENDT('ERROR: CROSS AREA VALUE EQUAL ZERO')
         PROELT(7,ISETST)=THICKT
        ENDIF
       ENDIF
C
C**** GAP ELEMENTS
C
       IF(ITYPET.EQ.104) THEN
        IF(NTYPET.EQ.1.OR.NTYPET.EQ.2.OR.NTYPET.EQ.3) THEN  ! 2D & axys.
         IF(NOCOIT.GT.0) THEN
          ISETCT=INT(PARAMT(7))
          IMAEST=INT(PARAMT(8))
          THICKT=    PARAMT(9)
          PROELT(11,ISETST)=1.0D0               ! non-coinc. contact set
          IF(NOCOIT.EQ.2.AND.IMAEST.EQ.0)
     .     PROELT(11,ISETST)=0.0D0              ! coinc. contact set
         ENDIF
         IF(THICKT.NE.0.0D0)
     .    CALL RUNMENT('WARNING: THICK VALUE NOT USED')
         PROELT(7,ISETST)=1.0D0
        ENDIF
        IF(NTYPET.EQ.4) THEN                    ! impossible
         CALL RUNENDT('ERROR: 3D FOR GAP/BOUNDARY IS NOT POSSIBLE')
        ENDIF
        IF(NTYPET.EQ.5) THEN                    ! 1D
         IF(NOCOIT.GT.0) THEN
          ISETCT=INT(PARAMT(7))
          IMAEST=INT(PARAMT(8))
          THICKT=    PARAMT(9)
          PROELT(11,ISETST)=1.0D0               ! non-coinc. contact set
          IF(NOCOIT.EQ.1.AND.IMAEST.EQ.0) THEN  ! old version
           THICKT=PARAMT(7)
           ISETCT=0
          ENDIF
          IF(NOCOIT.EQ.2.AND.IMAEST.EQ.0) THEN
           THICKT=PARAMT(7)
           ISETCT=0
           PROELT(11,ISETST)=0.0D0              ! coinc. contact set
          ENDIF
         ENDIF
         IF(THICKT.EQ.0.0D0)
     .    CALL RUNENDT('ERROR: CROSS AREA VALUE EQUAL ZERO')
         PROELT(7,ISETST)=THICKT
        ENDIF
        IF(NOCOIT.GT.0) THEN
         PROELT(8,ISETST)=FLOAT(ISETCT)
         PROELT(9,ISETST)=FLOAT(IMAEST)
        ENDIF
       ENDIF
C
C**** PRINT ELEMENTS CHARACTERISTICS
C
       IF(ITYPET.EQ.5) THEN                     ! solid
        WRITE(LUREST,900)
        IF(NTYPET.EQ.5) THEN                    ! 1D
         WRITE(LUREST,910) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST,
     .                     THICKT
        ENDIF
        IF(NTYPET.EQ.1.OR.NTYPET.EQ.2) THEN     ! 2D
         WRITE(LUREST,910) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST,
     .                     THICKT
        ENDIF
        IF(NTYPET.EQ.3.OR.NTYPET.EQ.4) THEN     ! axisymm. & 3D
         WRITE(LUREST,912) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST
        ENDIF
       ENDIF
C
       IF(ITYPET.EQ.101) THEN                   ! boundary
        WRITE(LUREST,900)
        IF(NTYPET.EQ.5) THEN                    ! 1D
         WRITE(LUREST,910) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST,
     .                     THICKT
        ENDIF
        IF(NTYPET.EQ.1.OR.NTYPET.EQ.2) THEN     ! 2D
         WRITE(LUREST,912) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST
        ENDIF
        IF(NTYPET.EQ.3) THEN                    ! axisymm.
         WRITE(LUREST,912) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST
        ENDIF
       ENDIF
C
       IF(ITYPET.EQ.104) THEN                   ! gap
        IF(NTYPET.EQ.5) THEN                    ! 1D
         IF(NOCOIT.EQ.0.OR.(NOCOIT.EQ.2.AND.IMAEST.EQ.0)) THEN
          WRITE(LUREST,900)
          WRITE(LUREST,910) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST,
     .                      THICKT
         ELSE
          WRITE(LUREST,902)
          WRITE(LUREST,920) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST,
     .                      ISETCT,IMAEST,THICKT
         ENDIF
        ENDIF
        IF(NTYPET.EQ.1.OR.NTYPET.EQ.2) THEN     ! 2D
         IF(NOCOIT.EQ.0.OR.(NOCOIT.EQ.2.AND.IMAEST.EQ.0)) THEN
          WRITE(LUREST,900)
          WRITE(LUREST,912) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST
         ELSE
          WRITE(LUREST,902)
          WRITE(LUREST,922) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST,
     .                      ISETCT,IMAEST
         ENDIF
        ENDIF
        IF(NTYPET.EQ.3) THEN                    ! axisymm.
         IF(NOCOIT.EQ.0.OR.(NOCOIT.EQ.2.AND.IMAEST.EQ.0)) THEN
          WRITE(LUREST,900)
          WRITE(LUREST,912) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST
         ELSE
          WRITE(LUREST,902)
          WRITE(LUREST,922) ISETST,IMATST,ITYPET,NTYPET,IRULET,IGAUST,
     .                      ISETCT,IMAEST
         ENDIF
        ENDIF
       ENDIF
C
   10 CONTINUE
C
C**** LOOK FOR THE 'END_SETS' CARD
C
      NPRINT=0
      JTAPET=LUDATT
      CALL LISTENT('INPSETT',NPRINT,JTAPET)
      IF(WORDST(1).NE.'END_S')
     . CALL RUNENDT('INPSETT: END_SETS CARD NOT FOUND')
C
      RETURN
  900 FORMAT(//,6X,'ELEMENT CHARACTERISTICS',/,6X,23('-'),
     .        /,5X,'GROUP',3X,'MATERIAL',3X,'TYPE1',3X,'TYPE2',
     .          3X,'INT. RULE',3X,'NO. PTS.',3X,'AREA/THICKNESS',/)
  901 FORMAT(' ERROR IN OPENING SET INPUT FILE 241 (51 LINUX)')
  902 FORMAT(//,6X,'ELEMENT CHARACTERISTICS',/,6X,23('-'),
     .        /,5X,'GROUP',3X,'MATERIAL',3X,'TYPE1',3X,'TYPE2',
     .          3X,'INT. RULE',3X,'NO. PTS.',3X,'CONTACT SET',
     .          3X,'CONTACT INDEX',3X,'AREA/THICKNESS',/)
c 905 FORMAT(6I5,F10.3)
  910 FORMAT(5X,I5,6X,I5,3X,I5,3X,I5,7X,I5,6X,I5,7X,F10.3)
  912 FORMAT(5X,I5,6X,I5,3X,I5,3X,I5,7X,I5,6X,I5,7X,'---')
  920 FORMAT(5X,I5,6X,I5,3X,I5,3X,I5,7X,I5,6X,I5,7X,I5,6X,I5,7X,F10.3)
  922 FORMAT(5X,I5,6X,I5,3X,I5,3X,I5,7X,I5,6X,I5,9X,I5,11X,I5,7X,'---')
      END
