      SUBROUTINE FLOWPL(AVECT,ABETA,DMATX,DVECT,HARDS,NSTRS,NTYPE,
     .                  PROPS)
C**********************************************************************
C
C****THIS ROUTINE EVALUATES THE PLASTIC D VECTOR
C
C**********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION AVECT(*), DMATX(NSTRS,*), DVECT(*), PROPS(*)
C
      IF(PROPS(1).NE.0.)GOTO 10
      IF(NTYPE.EQ.1)GOTO 20
C
C***PLANE STRAIN AND AXISYMMETRIC CASE
C
      DVECT(1)=DMATX(1,1)*AVECT(1)+DMATX(1,2)*AVECT(2)
     .        +DMATX(1,4)*AVECT(4)
      DVECT(2)=DMATX(2,1)*AVECT(1)+DMATX(2,2)*AVECT(2)
     .        +DMATX(2,4)*AVECT(4)
      DVECT(3)=DMATX(3,3)*AVECT(3)
      DVECT(4)=DMATX(4,1)*AVECT(1)+DMATX(4,2)*AVECT(2)
     .        +DMATX(4,4)*AVECT(4)
C
      IF(NTYPE.NE.4)GOTO 30
C
C***ADD THE PROPER TERMS FOR 3-D CASE
C
      DVECT(5)=DMATX(5,5)*AVECT(5)
      DVECT(6)=DMATX(6,6)*AVECT(6)
C
      GOTO 30
C
C***PLANE STRESS
C
   20 DVECT(1)=DMATX(1,1)*AVECT(1)+DMATX(1,2)*AVECT(2)
      DVECT(2)=DMATX(2,1)*AVECT(1)+DMATX(2,2)*AVECT(2)
      DVECT(3)=DMATX(3,3)*AVECT(3)
C
      GOTO 30
C
C***FOR THE CASE WHEN THE PRINCIPAL DIRECTION OF THE ELASTIC TENSOR
C   NON COINCIDE WITH THE CARTESIAN REFERENCE SYSTEM
C
   10 DO 40 ISTRS=1,NSTRS
      DVECT(ISTRS)=0.
      DO 40 JSTRS=1,NSTRS
   40 DVECT(ISTRS)=DVECT(ISTRS)+DMATX(ISTRS,JSTRS)*AVECT(JSTRS)
C
C***AND FINALLY COMPUTE THE DENOMINATOR OF THE EQUILIBRIUM PLASTIC EQUATION
C
   30 DENOM=HARDS
      DO 50 ISTRS=1,NSTRS
   50 DENOM=DENOM+AVECT(ISTRS)*DVECT(ISTRS)
      ABETA=1./DENOM
C
      RETURN
      END
