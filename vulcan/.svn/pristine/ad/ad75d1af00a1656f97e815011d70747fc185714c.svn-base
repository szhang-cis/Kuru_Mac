      SUBROUTINE OUTREA(IFFIX,REFOR,INFRI)
C***********************************************************************
C
C**** THIS ROUTINE WRITES REACTIONS TO OUTPUT FILE
C
C       KPRI8=0  DO NOT WRITE REACTIONS TO OUTPUT FILE
C             1  WRITE REACTIONS TO OUTPUT FILES
C       KFEMV=0  DO NOT WRITE REACTIONS TO POSTPROCESSOR FILE  ! NOT YET
C             1  WRITE REACTIONS TO POSTPROCESSOR FILE         ! NOT YET
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inpo_om.f'
C
      DIMENSION IFFIX(NTOTV), REFOR(NTOTV), REACT(60), INFRI(NPOIN)
C
      IF(IPRCO.GT.0) THEN
       IF(KPRI8.EQ.1) THEN               ! OUTPUT REACTIONS
C
        LURES=7
        WRITE(LURES,900)
        IF(KPROB.EQ.1) THEN ! Continuum
         IF(NDIME.EQ.1.AND.KPORE.EQ.0) WRITE(LURES,910)
         IF(NDIME.EQ.2.AND.KPORE.EQ.0) WRITE(LURES,920)
         IF(NDIME.EQ.3.AND.KPORE.EQ.0) WRITE(LURES,930)
         IF(NDIME.EQ.2.AND.KPORE.NE.0) WRITE(LURES,940)
         IF(NDIME.EQ.3.AND.KPORE.NE.0) WRITE(LURES,950)
        ELSE IF(KPROB.EQ.2) THEN ! Shells
         IF(NDIME.EQ.2) WRITE(LURES,960)
         IF(NDIME.EQ.3) WRITE(LURES,970)
        ELSE IF(KPROB.EQ.3) THEN ! Waves
         WRITE(LURES,980)
        END IF ! KPROB.EQ.?
C
        ITOTV=0
        DO IPOIN=1,NPOIN
         JPRIN=0
         DO IDOFN=1,NDOFC
          REACT(IDOFN)=0.0D0
         END DO
         DO IDOFN=1,NDOFC
          ITOTV=ITOTV+1
C
C**** REACTIONS IN GLOBAL SYSTEM OF COORDINATES (EVEN WHEN SKEW SYSTEMS
C     ARE USED FOR THE BOUNDARY CONDITIONS)
C
          IOEF1=0
          IF(NSKEW.GT.0) IOEF1=INFRI(IPOIN)
          IF(IOEF1.EQ.0) THEN
           IF(IFFIX(ITOTV).EQ.1) THEN
            JPRIN=1
            REACT(IDOFN)=-REFOR(ITOTV)
           END IF
          ELSE
           JPRIN=1
           REACT(IDOFN)=-REFOR(ITOTV)
          ENDIF
C
         END DO
         IF(JPRIN.EQ.1)
     .    WRITE(LURES,800) IPOIN,(REACT(IDOFN),IDOFN=1,NDOFC)
        END DO ! IPOIN=1,NPOIN
C
       END IF ! (KPRI0.EQ.1)
      ENDIF
C
      IF(KFEMV.EQ.1)                       ! WRITE TO POSTPROCESSOR FILE
c    . WRITE(LUPOS) (SNGL(-REFOR(ITOTV)),ITOTV=1,NTOTV),      ! old form
c    .              (IFFIX(ITOTV)),ITOTV=1,NTOTV)
     . WRITE(LUPOS) (SNGL(-REFOR(ITOTV)),ITOTV=1,NTOTV)       ! new form
C
  800 FORMAT(5X,I5,3E15.6,:,/,4X,3E15.6)
c 900 FORMAT(/,8X,'REACTIONS:',/,8X,9('-'),/)
  900 FORMAT(/,6X,'REACTIONS')
  910 FORMAT(6X,'NODE',8X,'X-REAC.')
  920 FORMAT(6X,'NODE',8X,'X-REAC.',8X,'Y-REAC.')
  930 FORMAT(6X,'NODE',8X,'X-REAC.',8X,'Y-REAC.',8X,'Z-REAC.')
  940 FORMAT(6X,'NODE',8X,'X-REAC.',8X,'Y-REAC.',8X,'FLU.PWP')
  950 FORMAT(6X,'NODE',8X,'X-REAC.',8X,'Y-REAC.',8X,'Z-REAC.',/,
     .                12X,'FLU.PWP')
  960 FORMAT(6X,'NODE',8X,'W-REAC.',8X,'X-RESU.',8X,'Y-RESU.')
  970 FORMAT(6X,'NODE',8X,'X-REAC.',8X,'Y-REAC.',8X,'Z-REAC.',/,
     .                12X,'X-RESU.',8X,'Y-RESU.',8X,'Z-RESU.')
  980 FORMAT(6X,'NODE',8X,'HIDROSTATIC FORCE')
C
      END
