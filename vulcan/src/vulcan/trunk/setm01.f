      SUBROUTINE SETM01(CARTD,DVOLU,ELCOD,EMASS,EPMTX,GPCOD,LNODS,
     .                  PROPS,RMAT1,SHAPE,THICK,
     .                  DERIV,POSGP,WEIGP,XJACM,RMAT2)
C***********************************************************************
C
C**** THIS ROUTINE SETS UP SOME NEEDED CONSTANT MATRICES FOR
C     FUTURE USE ( ELEMENT NO. 1 )
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
C
      COMMON/JACOBSA/IEROR,KEROR
C
      DIMENSION CARTD(NDIME,NNODE,*), DVOLU(*),
     .          ELCOD(NDIME,*),       EPMTX(*),
     .          GPCOD(NDIME,*),       LNODS(*),
     .          PROPS(*),             RMAT1(NDIME,*), 
     .          SHAPE(NNODE,*)
      DIMENSION DERIV(NDIME,NNODE,*), 
     .          POSGP(NDIME,*),       WEIGP(*),
     .          XJACM(NDIME,*),       RMAT2(NSTRS,*)
      DATA TWOPI/6.283185307179586/
C
C**** EVALUATE QUANTITIES ASSOCIATED WITH THE GAUSSIAN POINTS
C
C**** ROTATION MATRIX 
C
      IFLAG=INT(PROPS(1))
      IF(IFLAG.EQ.1)
     .  CALL ROTATE(IELEM,NDIME,NSTRS,PROPS,RMAT1,RMAT2)
C 
C**** ELASTIC CONSTANT MATRIX
C
      CALL ELASTI(NSTRS,NTYPE,EPMTX,PROPS,RMAT2,ncrit)
C
C**** IDENTIFY SAMPLING POINTS AND WEIGHTS
C
      CALL RULEPW(NDIME,NGAUL,NRULE,POSGP,WEIGP)
C
C**** LOOP ON INTEGRATION POINTS
C
      DO 100 IGAUS=1,NGAUL
C
C**** COMPUTE SHAPE FUNCTIONS AND DERIVATIVES
C
      EXISP=POSGP(1,IGAUS)
      ETASP=0.0
      IF(NDIME.GE.2) ETASP=POSGP(2,IGAUS)
      EZETA=0.0
      IF(NDIME.EQ.3) EZETA=POSGP(3,IGAUS)
      CALL SHAFUN(DERIV(1,1,IGAUS),EXISP,ETASP,EZETA,NDIME,NNODL,
     .            NQUTR,    0,SHAPE(1,IGAUS))
C
C**** CARTESIAN DERIVATIVES 
C
      CALL JACOBS(CARTD(1,1,IGAUS), DERIV(1,1,IGAUS), DETJM,
     .            ELCOD,            GPCOD(1,IGAUS),   IELEM,
     .            NDIME,NNODL,      SHAPE(1,IGAUS),   XJACM)
      IF(IEROR.NE.0) GO TO 101
C
C**** MAKE A CONTROL ON THE CORRECTNESS OF THE SHAPE FUNCTION AND THEIR
C     DERIV.
C
      ICHEK=0
      IF(ICHEK.EQ.1)CALL TESTM2(CARTD(1,1,IGAUS),DERIV(1,1,IGAUS),
     .                          SHAPE(1,  IGAUS),IGAUS,NDIME,NNODL)
C
C**** INTEGRATION WEIGHTS
C
                       DVOLU(IGAUS)=WEIGP(IGAUS)*DETJM
      IF(NTYPE.EQ.3)   DVOLU(IGAUS)=DVOLU(IGAUS)*TWOPI*GPCOD(1,IGAUS)
      IF(NTYPE.NE.4)   DVOLU(IGAUS)=DVOLU(IGAUS)*THICK
C
  100 CONTINUE
C
C**** COMPUTE MASS MATRIX TO PERFORM SMOOTHING OF GAUSSIAN TENSORS
C
      IF(KSGAU.NE.0) CALL SMOMTX(ELCOD,DVOLU,SHAPE,EMASS)
C
C**** COMPUTE STABILIZATION MATRIX FOR HOURGLASS CONTROL
C
      IF(NHOUR.NE.0) CALL HOURMX(CARTD,EPMTX,DVOLU,ELCOD,GPCOD,PROPS,
     .                           EMASS,THICK)
C
  101 CONTINUE
C
      RETURN
      END
