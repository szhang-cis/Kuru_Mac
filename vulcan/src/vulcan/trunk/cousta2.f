      SUBROUTINE COUSTA2(LNODS,MATNO,PROEL,PROPS,COORD,HTLOD,
     .                   IFFIX,PRESC,RLOAD,RLOAH,FICTO,TFICT,
     .                   DISIT,DISPR,DISTO,HEADS,REFOR,TLOAD,
     .                   LPNTN,ELDAT,ELPRE,ELVAR,ELMAT,TEMPN,
     .                   DTEMP,INFRI,COFRI,PWORK,PREAS,TGAPS,
     .                   VNORM,FPCHA,LACTI,NOPRF,PRESF,PREHF,
     .                   VANIS,WORK1,NEQNS,
     .                   LNODST,MATNOT,PROELT,PROPST,COORDT,HTLODT,
     .                   IFFIXT,PRESCT,RLOADT,RLOAHT,FICTOT,TFICTT,
     .                   DISITT,DISPRT,DISTOT,HEADST,REFORT,TLOADT,
     .                   LPNTNT,ELDATT,ELPRET,ELVART,ELMATT,DISPLT,
     .                   PWORKT,PREAST,TGAPST,TEMPIT,ADVELT,FPCHAT,
     .                   LACTIT,
     .                   WORK1T)
C***********************************************************************
C
C**** THIS ROUTINE PERFORMS THE STAGGERED SOLUTION
C
C     Note: Dimensions of PREAS are inverted in order to properly
C           transfer it to output.f
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8, ALLOCATABLE, INTENT(INOUT) :: WORK1(:), WORK1T(:)
C
C**** COUPLING VARIABLES
C
      INCLUDE 'nuec_om.f'
C
C**** MECHANICAL VARIABLES
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
C
C**** THERMAL VARIABLES
C
      INCLUDE 'prob_omt.f'
      INCLUDE 'inte_omt.f'
      INCLUDE 'auxl_omt.f'
      INTERFACE
        INCLUDE 'iterat.inc'
      END INTERFACE
C
C**** MECHANICAL VARIABLES
C
      DIMENSION LNODS(NNODE,NELEM), MATNO(NELEM),
     .          PROEL(NPREL,NGRUP), PROPS(NPROP,NMATS),
     .          COORD(NDIME,NPOIN), HTLOD(NHLOD,NSUBF,NFUNC)
      DIMENSION IFFIX(NTOTV,2),     PRESC(NTOTV,2),
     .          RLOAD(NTOTV),       RLOAH(NTOTV,NFUNC),
     .          FICTO(NFUNC),       TFICT(NFUNC)
      DIMENSION DISIT(NTOTV,2),     DISPR(NTOTV,NDISR),
     .          DISTO(NTOTV,NDISO), HEADS(NPOIN,4),
     .          REFOR(NTOTV,2),     TLOAD(NTOTV,2)
      DIMENSION LPNTN(NPOIN)
      DIMENSION ELDAT(NDATA),       ELPRE(NPREV),
     .          ELVAR(NSTAT),       ELMAT(NMATX)
      DIMENSION TEMPN(NPOIN,2),     DTEMP(NPOIN)
      DIMENSION INFRI(NPOIN),       COFRI(NSKEW,NDIME,*),
     .          PWORK(NPOIN,2),     PREAS(NPOIN,NPREA),
     .          TGAPS(NPOIN),       VNORM(NTOTV),
     .          FPCHA(NFPCH,NPOIN), LACTI(NELEM)
      DIMENSION NOPRF(NNODE,NELEM), PRESF(NNODE,NDIME,NELEM),
     .          PREHF(NNODE,NDIME,NELEM,NFUNC),
     .          VANIS(NANIV,NANIC,NELEM)
C
C**** THERMAL VARIABLES
C
      DIMENSION LNODST(NNODET,NELEMT), MATNOT(NELEMT),
     .          PROELT(NPRELT,NGRUPT), PROPST(NPROPT,NMATST),
     .          COORDT(NDIMET,NPOINT), HTLODT(NHLODT,NSUBFT,NFUNCT)
      DIMENSION IFFIXT(NTOTVT,2),      PRESCT(NTOTVT,2),
     .          RLOADT(NTOTVT),        RLOAHT(NTOTVT,NFUNCT),
     .          FICTOT(NFUNCT),        TFICTT(NFUNCT)
      DIMENSION DISITT(NTOTVT,2),
     .          DISPRT(NTOTVT,3),      DISTOT(NTOTVT,3),
     .          HEADST(NPOINT,4),
     .          REFORT(NTOTVT,2),      TLOADT(NTOTVT,2),
     .          LPNTNT(NPOINT)
      DIMENSION ELDATT(NDATAT),        ELPRET(NPREVT),
     .          ELVART(NSTATT),        ELMATT(NMATXT)
      DIMENSION DISPLT(NTOTVM),        PWORKT(NPOINT,3),
     .          PREAST(NPREAT,NPOINT), TGAPST(NPOINT),
     .          TEMPIT(NPOINT,2),      ADVELT(NTOTVT*NDIMET),
     .          FPCHAT(NFPCH,NPOINT),  LACTIT(NELEMT)
C
  100 CONTINUE
C
C**** TRANSFER NODAL MECHANICAL VARIABLES TO THERMAL PROBLEM
C
      CALL TRASMEC(DISTO, PWORK, PREAS(1,1), TGAPS,
     .             DISPLT,PWORKT,PREAST(1,1),TGAPST)
C
C**** TRANSFER GAUSSIAN MECHANICAL VARIABLES TO THERMAL PROBLEM
C
      CALL TRASMEG(ELVAR,ELVART)
C
C**** INITIALISE ITERATIVE TEMPERATURES
C
      DO ITOTVT=1,NTOTVT
       DISITT(ITOTVT,1)=0.0D0
      END DO
C
C**** CALCULATES THE "THERMAL RESIDUAL FORCES"
C
      CALL RESIDUT(DISITT,DISPRT,DISTOT,ELDATT,ELPRET,ELVART,ELMATT,
     .             HEADST,IFFIXT,LNODST,MATNOT,PROELT,PROPST,REFORT,
     .            TLOADT,DISPLT,PWORKT,PREAST(1,1),TGAPST,COORDT,TEMPIT,
     .             ADVELT,FPCHAT,LACTIT,HTLODT,WORK1T)
C
C**** CHECK CONVERGENCE FOR THE THERMAL PROBLEM
C
      CALL CONVERT(DISITT,DISPRT,DISTOT,REFORT,TLOADT,IFFIXT)
C
      IF(NCHEKT.NE.0) THEN
C
C**** THERMAL PROBLEM OUTPUT (NOT CONVERGED)
C
       IF(IPRCOT.EQ.0)
     .  CALL OUTPUTT(DISTOT,ELDATT,ELPRET,ELVART,ELMATT,HEADST,
     .               IFFIXT,LNODST,MATNOT,PROELT,PROPST,TLOADT,
     .               COORDT,FPCHAT,PREAST(NPRE1T+1,1),DISPLT,LACTIT,
     .               WORK1T)
C
C**** LOOP ON ITERATIONS FOR THE THERMAL PROBLEM
C
       DO WHILE
     .    ((NCHEKT.NE.0).AND.(IITERT.LE.MITERT))
C
C**** ITERATIVE CORRECTION FOR THE THERMAL PROBLEM
C
        CALL ITERATT(DISITT,DISPRT,DISTOT,ELDATT,ELPRET,
     .               ELVART,ELMATT,HEADST,IFFIXT,PRESCT,
     .               LNODST,MATNOT,PROELT,PROPST,REFORT,
     .               RLOADT,FICTOT,TLOADT,DISPLT,PWORKT,
     .               PREAST,TGAPST,COORDT,TEMPIT,ADVELT,
     .               FPCHAT,LACTIT,HTLODT,WORK1T)
C
       ENDDO
C
       IF(NCHEKT.NE.0)
     .  CALL RUNENDT(' NOT CONVERGED                    ') 
C
C**** WRITES THE INTERCHANGES ITERATION NUMBER
C
       IITERC=IITERC+1
       WRITE(LUPRI,900) IITERC
       WRITE(LUPRIT,900) IITERC
  900  FORMAT('INTERCHANGES ITERATION NUMBER = ',I5)
C
C**** TRANSFER THERMAL VARIABLES TO MECHANICAL PROBLEM
C
       CALL TRASTER(DISTOT,TEMPIT,FPCHAT,PWORKT,TEMPN,DTEMP,FPCHA,PWORK)
C
C**** INITIALISE ITERATIVE DISPLACEMENTS
C
       DO ITOTV=1,NTOTV
        DISIT(ITOTV,1)=0.0D0
       END DO
C
C**** CALCULATES THE MECHANICAL RESIDUAL FORCES
C
       CALL RESIDU(DISIT,DISPR,DISTO,ELDAT,ELPRE,ELVAR,ELMAT,
     .             HEADS,IFFIX,LNODS,MATNO,PROEL,PROPS,REFOR,
     .             TLOAD,TEMPN,DTEMP,INFRI,COFRI,PWORK,PREAS,
     .             TGAPS,VNORM,COORD,FPCHA,LACTI,NOPRF,PRESF,
     .             VANIS,WORK1)
C
C**** CHECK CONVERGENCE FOR THE MECHANICAL PROBLEM
C
       CALL CONVER(DISIT,DISPR,DISTO,REFOR,TLOAD,IFFIX)
C
       IF(NCHEK.NE.0) THEN
C
C**** MECHANICAL PROBLEM OUTPUT (NOT CONVERGED)
C
        CALL OUTPUT(DISTO,ELDAT,ELPRE,ELVAR,ELMAT,HEADS,IFFIX,
     .              LNODS,MATNO,PROEL,PROPS,TLOAD,REFOR,COORD,
     .              PWORK(1,2),TGAPS,PREAS(1,1),DISTO,INFRI,LACTI,
     .              PREAS(1,NPRE1+1),PREAS(1,NPRE1+NPRE2+1),WORK1)
C
C**** LOOP ON ITERATIONS FOR THE MECHANICAL PROBLEM
C
        DO WHILE
     .     ((NCHEK.NE.0).AND.(IITER.LE.MITER))
C
C**** ITERATIVE CORRECTION FOR THE MECHANICAL PROBLEM
C
         CALL ITERAT(DISIT,DISPR,DISTO,ELDAT,ELPRE,ELVAR,ELMAT,
     .               HEADS,IFFIX,PRESC,LNODS,MATNO,PROEL,PROPS,
     .               REFOR,RLOAD,FICTO,TLOAD,TEMPN,DTEMP,INFRI,
     .               COFRI,PWORK,PREAS,TGAPS,VNORM,LPNTN,COORD,
     .               FPCHA,LACTI,NOPRF,PRESF,VANIS,WORK1,NEQNS)
C
        ENDDO
C
        IF(NCHEK.NE.0)
     .   CALL RUNEND(' NOT CONVERGED                    ')
C
        GO TO 100
       ENDIF  ! mechanical problem is converged => global converg.
      ENDIF   ! thermal problem is converged => global convergence
C
C**** GLOBAL CONVERGENCE FOR OUTPUT
C
      NCKGLO=0
C
C**** THERMAL PROBLEM OUTPUT (GLOBAL CONVERGED)
C
      IF(IPRCOT.EQ.0)
     . CALL OUTPUTT(DISTOT,ELDATT,ELPRET,ELVART,ELMATT,HEADST,
     .              IFFIXT,LNODST,MATNOT,PROELT,PROPST,TLOADT,
     .              COORDT,FPCHAT,PREAST(NPRE1T+1,1),DISPLT,LACTIT,
     .              WORK1T)
C
C**** MECHANICAL PROBLEM OUTPUT (GLOBAL CONVERGED)
C
      CALL OUTPUT(DISTO,ELDAT,ELPRE,ELVAR,ELMAT,HEADS,IFFIX,
     .            LNODS,MATNO,PROEL,PROPS,TLOAD,REFOR,COORD,
     .            PWORK(1,2),TGAPS,PREAS(1,1),DISTO,INFRI,LACTI,
     .            PREAS(1,NPRE1+1),PREAS(1,NPRE1+NPRE2+1),WORK1)
C
      RETURN
      END
