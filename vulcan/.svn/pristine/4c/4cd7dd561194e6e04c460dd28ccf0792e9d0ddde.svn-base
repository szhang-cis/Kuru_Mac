
      SUBROUTINE GRACOE(CONS1,CONS2,CONS3,DEFPI,EHIST,FAC11,FAC22,
     .                  FAC33,FAC12,FAC13,FAC23,HARDS,IFLAG,ITEST,
     .                  NCRIT,PMEAN,PROPS,QINVA,THETA,VSTOT)
C***********************************************************************
C
C****THIS ROUTINE EVALUATES THE FLOW VECTOR
C
C    ITEST= 1     ELASTO-PLASTIC ANALYSIS (ESEPA)
C    ITEST= 2     ELASTO-VISCO-PLASTIC ANALYSIS (VESPA)
C
C    IFLAG= 0     EITHER THE YIELD FUNCTION IS THETA INDIPENDENT
C                 OR THETA=30 DEGREE
C    IFLAG= 1     THETA DEPENDENT YIELD FUNCTION AND ABS(THETA).LT.29 
C                 DEGREE
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION EHIST(*), PROPS(*)
C
C***CALCULATE THE DERIVATIVE OF THE POTENTIAL FUNCTION
C
C***PLASTIC LINEAR MODEL
C
      IF(NCRIT.EQ.1.OR.NCRIT.EQ.2)
     . CALL POTEN1(DEFPI,DEFQU,DEFTH,DEFPP,DEFQQ,DEFTT,DEFPQ,
     .             DEFPT,DEFQT,EHIST,HARDS,IFLAG,ITEST,NCRIT,
     .             PROPS,QINVA,THETA)
C
C***CAM CLAY
C
      IF(NCRIT.EQ.3.OR.NCRIT.EQ.4)
     . CALL POTEN2(DEFPI,DEFQU,DEFTH,DEFPP,DEFQQ,DEFTT,DEFPQ,
     .             DEFPT,DEFQT,EHIST,HARDS,IFLAG,ITEST,NCRIT,
     .             PMEAN,PROPS,QINVA,THETA,VSTOT)
C
C***CALCULATE GRADIENT CONSTANT
C
      CONS1=DEFPI/3.
      CONS2=0.
      CONS3=0.
      IF(ITEST.EQ.1)GOTO 1
C
      FAC11=DEFPP/9.
      FAC22=0.
      FAC33=0.
      FAC12=0.
      FAC13=0.
      FAC23=0.
C
    1 IF(QINVA.LE.0.)RETURN
      QINV2=QINVA*QINVA
C
      IF(IFLAG.EQ.1)GOTO 10
C
C***RESERVED ONLY TO VON-MISES OR DRUCKER_PRAGER LIMITING SURFACE
C   OR FOR ROUNDING OF THE CORNER FOR THE OTHER TWO CRITERIA
C
      CONS2=3.*DEFQU/(2.*QINVA)
C
      IF(ITEST.EQ.1)RETURN
C
      FAC22=9.*(DEFQQ-DEFQU/QINVA)/(4.*QINV2)
      FAC12=DEFPQ/(2.*QINVA)
C
      RETURN
C
C***RESERVED TO TRESCA AND MOHR COULOMB LIMITING SURFACES AND
C    ABS.THETA LESS THAN 29 DEGREE
C
   10 CONTINUE
      COST3=DCOS(3.*THETA)
      TANT3=DTAN(3.*THETA)
      CONS2=3.*(DEFQU-TANT3*DEFTH/QINVA)/(2.*QINVA)
      CONS3=-9.*DEFTH/(2.*(QINVA**3.)*COST3)
C
      IF(ITEST.EQ.1)RETURN
C
      COS32=COST3*COST3
      TAN32=TANT3*TANT3
      FAC22=9./(4.*QINV2)*(DEFQQ-DEFQU/QINVA+TANT3/QINVA*
     .      ((5.+3*TAN32)*DEFTH/QINVA-2.*DEFQT+TANT3*DEFTT/QINVA))
      FAC33=27.*(3.*TANT3*DEFTH+DEFTT)/(4.*COS32*(QINVA**6))
      FAC12=(DEFPQ-TANT3*DEFPT/QINVA)/(2.*QINVA)
      FAC13=-3.*DEFPT/(2.*QINVA*QINV2*COST3)
      FAC23=-27./(4.*QINV2*QINV2*COST3)*
     .      (DEFQT-(3.*DEFTH/COS32+TANT3*DEFTT)/QINVA)
C
      RETURN
      END
