      SUBROUTINE PLRIGI(ABETA,NSTRS,DGVEC,PROPS,DMATX,DMTEP,EBASE,NCRIT,
     .                  NCRIP,NTYPE,NSTR1,DMATE,VECTF,KSYMM,ICOCO,HARDE,
     .                  ALFAP,TLAMD,NAUXI)
C***********************************************************************
C
C**** THIS SUBROUTINE EVALUATES THE ELASTO-PLASTIC MATERIAL TENSOR
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      COMMON/CONSIS/COC21,COC22,COC23
C
      DIMENSION DMATX(NAUXI,*), DVECT(6),   PROPS(*), DMATE(6,6),
     .          DGVEC(6),       EBASE(*),   DMTEP(*), VECTF(6),
     .          HARDE(6)
      DIMENSION UNOMA(6),       DMACO(6,6), DMACU(6)
C
      IF(ICOCO.EQ.-1) RETURN
C
C**** COMPUTE "DVECT=(DMATE*VECTF)" PRODUCT
C
      ISOTR=INT(PROPS(1))   ! 0=isotropic; 1=orthotropic
      CALL PLPDXV(VECTF,DMATE,DVECT,NSTRS,NTYPE,ISOTR,NSTR1)
C
C**** ADDS "HARDE" TO "DVECT"
C
      DO ISTRS=1,NSTRS
       DVECT(ISTRS)=DVECT(ISTRS)+HARDE(ISTRS)
      ENDDO
C
C**** COMPUTE THE ELASTO-PLASTIC MATERIAL TENSOR
C
      IKONT=0
      DO ISTRS=1,NSTRS
       INDEX=ISTRS
       IF(KSYMM.EQ.0) INDEX=1
       DO JSTRS=INDEX,NSTRS
        IKONT=IKONT+1
        DMTEP(IKONT)=DMATE(ISTRS,JSTRS)-ABETA*DGVEC(ISTRS)*DVECT(JSTRS)
c       DMTEP(IKONT)=DMTEP(IKONT)-ALFAP*ABETA*DGVEC(ISTRS)*DVECT(JSTRS)
       ENDDO
      ENDDO
C
C**** COMPUTE THE CONSISTENT ELASTO-PLASTIC MATERIAL TENSOR
C
      IF(ICOCO.EQ.1) THEN
       DO ISTR1=1,NSTR1
        UNOMA(ISTR1)=0.0D0
       ENDDO
       UNOMA(1)=1.0D0
       UNOMA(2)=1.0D0
       UNOMA(4)=1.0D0
C
       DO ISTRS=1,NSTRS
        DO JSTRS=1,NSTRS
         DMACO(ISTRS,JSTRS)=0.0D0
         DO KSTRS=1,NSTRS
c         IF(KSTRS.EQ.3.OR.KSTRS.EQ.5.OR.KSTRS.EQ.6) THEN
c          DMACO(ISTRS,JSTRS)=DMACO(ISTRS,JSTRS)+
c    .                       2.0D0*DMATE(ISTRS,KSTRS)*DMATE(KSTRS,JSTRS)
c         ELSE
           DMACO(ISTRS,JSTRS)=DMACO(ISTRS,JSTRS)+
     .                             DMATE(ISTRS,KSTRS)*DMATE(KSTRS,JSTRS)
c         ENDIF
         ENDDO
        ENDDO
       ENDDO
C
       DO ISTRS=1,NSTRS
        DMACU(ISTRS)=0.0D0
        DO JSTRS=1,NSTRS
         DMACU(ISTRS)=DMACU(ISTRS)+DMATE(ISTRS,JSTRS)*UNOMA(JSTRS)
        ENDDO
       ENDDO
C
       IKONT=0
       DO ISTRS=1,NSTRS
        INDEX=ISTRS
        IF(KSYMM.EQ.0) INDEX=1
        DO JSTRS=INDEX,NSTRS
         IKONT=IKONT+1
         DMTEP(IKONT)=DMTEP(IKONT)-
     .    TLAMD*(COC21*DGVEC(ISTRS)*DGVEC(JSTRS)+
     .           COC22*DMACO(ISTRS,JSTRS)+
     .           COC23*DMACU(ISTRS)*DMACU(JSTRS))
        ENDDO
       ENDDO
      ENDIF              ! icoco.eq.1
C
      RETURN
      END
