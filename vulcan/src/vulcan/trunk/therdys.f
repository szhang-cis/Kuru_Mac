      SUBROUTINE THERDYS(VELCTS,LNODSS,MATNOS,PROELS,PROPSS,
     .                   ELOADS,ELDATS,ELPRES,ELVARS,ELMATS,
     .                   WORK1S,DISITS,ELOATS,COORDS,
     .                   TEMPIS,ADVELS,FPCHAS,DISTOS,DISPLS)
C***********************************************************************
C
C**** THIS ROUTINE CALCULATES THE "THERMAL DYNAMIC FORCES"
C    
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
C**** ADDITIONAL PARAMETERS
C
      INCLUDE 'addi_oms.f'
C
C**** COUPLING VARIABLES
C
      INCLUDE 'nuec_om.f'
      INCLUDE 'nued_om.f'
C
C**** MICROSTRUCTURAL VARIABLES
C
      INCLUDE 'prob_oms.f'
      INCLUDE 'inte_oms.f'
      INCLUDE 'auxl_oms.f'
C
      DIMENSION VELCTS(*),        ELOADS(*),      LNODSS(NNODES,NELEMS),
     .          DISITS(*),        MATNOS(NELEMS), PROELS(NPRELS,NGRUPS),
     .          PROPSS(NPROPS,NMATSS)
      DIMENSION WORK1S(*)
      DIMENSION ELDATS(NDATAS),        ELPRES(NPREVS),
     .          ELVARS(NSTATS),        ELMATS(NMATXS)
      DIMENSION ELOATS(*)
      DIMENSION COORDS(NDIMES,NPOINS)
      DIMENSION TEMPIS(NPOINS),        ADVELS(NTOTVS*NDIMES),
     .          FPCHAS(NFPCH,NPOINS)
      DIMENSION DISTOS(NTOTVS),        DISPLS(NTOTVMS)
C
C**** LOOP ON ELEMENTS
C
      DO IELEMS=1,NELEMS
       LGRUPS=MATNOS(IELEMS)
       LMATSS=INT(PROELS(1,LGRUPS))
       NNODLS=INT(PROELS(2,LGRUPS))
       LTYPES=INT(PROELS(5,LGRUPS))
C     
C**** READ ELEM. DATA FROM DATA BASE 
C
       IF(NMEMO1S.EQ.0.OR.NMEMO2S.EQ.0)
     .  CALL DATBASS(ELDATS,    1,    2)
C
       IF(NMEMO3S.EQ.1.OR.NMEMO4S.EQ.1.OR.NMEMO5S.EQ.0)
     .  CALL DATBASS(ELVARS,    3,    2)    ! current
C
       IF(NMEMO5S.EQ.1) THEN
C
C**** SCALAR GATHER OPERATIONS ( DISTOT ---> ELDIST )
C
        CALL GATHER(DISTOS,NDOFCS,NPOINS,WORK1S(IFORDS(32)),NDOFCS,
     .              NNODLS,
     .              LNODSS(1,IELEMS))
       ENDIF
C
C**** GATHER NODAL RATES OF TEMPERATURES
C
       CALL GATHER(VELCTS,NDOFCS,NPOINS,WORK1S(IFORDs(2)),NDOFNS,NNODLS,
     .             LNODSS(1,IELEMS))
C
C**** GATHER NODAL ITERATIVE TEMPERATURES
C
       CALL GATHER(DISITS,NDOFCS,NPOINS,WORK1S(IFORDS(3)),NDOFNS,NNODLS,
     .             LNODSS(1,IELEMS))
C
C**** GATHER NODAL ADVECTIVE VELOCITY (npoins=ntotvs to be improved)
C
       IF(ICONVS.EQ.1)
     .  CALL GATHER(ADVELS,NDIMES,NTOTVS,WORK1S(IFORDS(22)),NDIMES,
     .              NNODLS,
     .              LNODSS(1,IELEMS))
C
C**** GATHER NODAL COORDINATES
C
       IF(NMEMO1S.EQ.1) THEN    ! coordinates in a global array
        CALL GATHER(COORDS,NDIMES,NPOINS,WORK1S(IFORDS(17)),NDIMES,
     .              NNODLS,LNODSS(1,IELEMS))
       ENDIF
C
C**** SCALAR GATHER OPERATIONS ( TEMPIS ---> WORK1T(IFORDS(33)) )
C
       CALL GATHER(TEMPIS,NDOFCS,NPOINS,WORK1S(IFORDS(33)),NDOFCS,
     .             NNODLS,LNODSS(1,IELEMS))     ! initial temperature
C
C**** GATHER PHASE-CHANGE VECTOR
C
       IF(NFILLS.EQ.1)
     .  CALL GATHER(FPCHAS,NFPCH,NPOINS,WORK1S(IFORDS(34)),NFPCH,
     .              NNODLS,LNODSS(1,IELEMS))
C
       IF(ITERME.GT.0) THEN          ! bidirectional coupled
        IF(ITERMD.EQ.1) THEN         ! deformed shape
C
C**** SCALAR GATHER OPERATIONS ( DISPLT ---> WORK1T(IFORDT(37)) )
C
         CALL GATHER(DISPLS,NDOFCMS,NPOINS,WORK1S(IFORDS(37)),NDOFCMS,
     .               NNODLS,
     .               LNODSS(1,IELEMS))
        ENDIF                        ! itermd.eq.1
       ENDIF                         ! iterme.gt.0
C
C**** CALL ELEMENT PROCESSOR TO PERFORM REQUIRED OPERATIONS
C
       CALL ELMLIBS(LNODSS(1,IELEMS),PROELS(1,LGRUPS),PROPSS(1,LMATSS),
     .              ELDATS,ELPRES,ELVARS,ELMATS,WORK1S,       6)
C
C**** WRITE CURRENT STATE VARIABLES TO DATA BASE
C
       IF(NMEMO3S.EQ.1.OR.NMEMO4S.EQ.1.OR.NMEMO5S.EQ.0)
     .  CALL DATBASS(ELVARS,    3,    1)
C
C**** SCATTER RESULTS INTO ELOAD
C
       CALL SCATER(WORK1S(IFORDS(1)),NDOFNS,NNODLS,ELOADS,NDOFCS,NPOINS,
     .             LNODSS(1,IELEMS))
C
C**** SCALAR SCATTER OPERATION ( WORK1S --> ELOATS )
C
       LHINCS=0                        ! better as input (see frdy05t.f)
       IF(LHINCS.EQ.1)
     .  CALL SCATER(WORK1S(IFORDS(6)),NDOFNS,NNODLS,ELOATS,NDOFCS,
     .              NPOINS,LNODSS(1,IELEMS))
C
C**** SCALAR SCATTER OPERATION ( WORK1S --> FPCHAS )
C
       IF(LTYPES.EQ.5)
     .  CALL SCATERA(WORK1S(IFORDS(34)),NFPCH,NNODLS,FPCHAS,NFPCH,
     .               NPOINS,LNODSS(1,IELEMS))
C
      ENDDO
C
      RETURN
      END
