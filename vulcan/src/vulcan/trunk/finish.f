      SUBROUTINE FINISH(DISPR,DISTO,ELPRE,ELVAR,HEADS,HTLOD,IFFIX,
     .                  REFOR,RLOAD,TLOAD,INFRI,COFRI,LACTI)
C***********************************************************************
C
C**** THIS ROUTINE SAVES THE CONVERGED VALUES AND CHECKS FOR ANALYSIS
C     TERMINATION
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
      INCLUDE 'prob_om.f'
      INCLUDE 'inte_om.f'
      INCLUDE 'auxl_om.f'
C
      DIMENSION DISPR(NTOTV,NDISR), DISTO(NTOTV,NDISO),
     .          ELPRE(NPREV),       ELVAR(NSTAT),          
     .          HEADS(NPOIN,4),     HTLOD(NHLOD,NSUBF,NFUNC),    
     .          IFFIX(NTOTV,2),     REFOR(NTOTV),          
     .          RLOAD(NTOTV),       TLOAD(NTOTV,2)
      DIMENSION INFRI(NPOIN),       COFRI(NSKEW,NDIME,*)
      DIMENSION LACTI(NELEM)
C
C**** CHECK IF CPU TIME IS RUNNING OUT
C
      ILATE=0
      CALL CPUTIM(CUCPU)
      IF(CUCPU.GE.0.95*TLIMT) ILATE=1
C
C**** APPLY SKEW SYSTEM CONDITIONS ON REFOR & TLOAD (GLOBAL > LOCAL)
C
      IF(NSKEW.GT.0) THEN
       CALL TRSKEW(NPOIN,NTOTV,NDOFN,REFOR,INFRI,COFRI,    1,
     .             NSKEW)
       CALL TRSKEW(NPOIN,NTOTV,NDOFN,TLOAD,INFRI,COFRI,    1,
     .             NSKEW)
      ENDIF
C
C**** COMPUTE SOME CONVERGED VALUES 
C
      CALL STIPAR(DISPR,IFFIX,TLOAD,REFOR)
C
C**** APPLY SKEW SYSTEM CONDITIONS ON REFOR & TLOAD (LOCAL > GLOBAL)
C
      IF(NSKEW.GT.0) THEN
       CALL TRSKEW(NPOIN,NTOTV,NDOFN,REFOR,INFRI,COFRI,    2,
     .             NSKEW)
       CALL TRSKEW(NPOIN,NTOTV,NDOFN,TLOAD,INFRI,COFRI,    2,
     .             NSKEW)
      ENDIF
C
C**** SAVE CONVERGED VALUES AND WRITE TO RESTART FILE IF NECESSARY
C
      CALL STORES(DISPR,DISTO,ELPRE,ELVAR,HEADS,HTLOD,IFFIX,
     .            REFOR,RLOAD,TLOAD,LACTI,ILATE)
C
C**** CHECK FOR ANALYSIS TERMINATION
C
      IF(CUCPU.GE.TLIMT)
     . CALL RUNEND(' CPU TIME EXPIRED                  ')
      IF(STIFI.NE.0.0.AND.STICU.LT.STIFI) 
     . CALL RUNEND(' FINAL STIFFNESS REACHED           ')
C
      RETURN
      END
