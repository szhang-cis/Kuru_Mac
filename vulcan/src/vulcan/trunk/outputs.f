      SUBROUTINE OUTPUTS(DISTOS,ELDATS,ELPRES,ELVARS,ELMATS,HEADSS,
     .                   IFFIXS,LNODSS,MATNOS,PROELS,PROPSS,TLOADS,
     .                   COORDS,FPCHAS,PREASS,DISPLS,WORK1S)
C***********************************************************************
C
C**** THIS ROUTINE OUTPUTS TEMPERATURES & HEAT FLUXES
C
C       KPRI0=0  DO NOT WRITE TO OUTPUT FILE
C             1  WRITE TO OUTPUT FILES
C       KPRI1=0  DO NOT WRITE TEMPERATURES TO OUTPUT FILE
C             1  WRITE TEMPERATURES TO OUTPUT FILES
C       KPRI2=0  DO NOT WRITE GAUSSIAN HEAT FLUXES TO OUTPUT FILE
C             1  WRITE GAUSSIAN HEAT FLUXES TO OUTPUT FILES
C       KPRI3=0  ---
C             1  ---
C       KPRI4=0  DO NOT WRITE GAUSSIAN INTERNAL VARIABLES TO OUTPUT FILE
C             1  WRITE GAUSSIAN INTERNAL VARIABLES TO OUTPUT FILES
C       KPRI5=0  DO NOT WRITE NODAL HEAT FLUXES
C             1  WRITE NODAL HEAT FLUXES
C       KPRI6=0  ---
C             1  ---
C       KPRI7=0 DO NOT WRITE NODAL INTERNAL VARIABLES TO OUTPUT FILE
C             1 WRITE NODAL INTERNAL VARIABLES TO OUTPUT FILES
C       KPRI8=0 DO NOT WRITE NODAL POROSITY CRITERIA
C            =1 WRITE NODAL POROSITY CRITERIA
C
C       KPRIN=1 WRITE TO OUTPUT FILES TEMPERATURES & HEAT FLUXES
C
C       KFEMV=0 DO NOT WRITE FOR POSTPROCESOR
C             1 WRITE FOR POSTPROCESOR
C
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C
C**** COUPLING VARIABLES
C
      INCLUDE 'nuec_om.f'
      INCLUDE 'nued_om.f'
C
C**** MICROSTRUCTURAL VARIABLES
C
      INCLUDE 'prob_oms.f'
      INCLUDE 'inte_oms.f'
      INCLUDE 'auxl_oms.f'
      INCLUDE 'inpo_oms.f'
C
      DIMENSION MATNOS(NELEMS),        LNODSS(NNODES,NELEMS),
     .          PROELS(NPRELS,NGRUPS), PROPSS(NPROPS,NMATSS),
     .          ELDATS(NDATAS),        ELPRES(NPREVS),
     .          ELVARS(NSTATS),        ELMATS(NMATXS),
     .          WORK1S(*)
      DIMENSION DISTOS(NTOTVS,3),      HEADSS(NPOINS,*), 
     .          IFFIXS(*),             TLOADS(*)
      DIMENSION COORDS(NDIMES,NPOINS), FPCHAS(NFPCH,NPOINS),
     .          PREASS(NPOROS,NPOINS), DISPLS(NTOTVMS)
C
      CALL CPUTIMS(TIME1S)
C
C**** IF NECESSARY PRINT OUT X-Y CURVES
C
      IF(NCHEKS.EQ.0.AND.NCURVS.NE.0.AND.NCKGLO.EQ.0)
     . CALL PLOOUTS(DISTOS,ELVARS,TLOADS)
C
C**** CHECK IF THE RESULTS SHOULD BE PRINTOUT
C
      CALL OUTCEKS(IITERS,ISTEPS,KPRINS,NCHEKS,NOUTPS,NSTEPS,TLIMTS,
     .             NCKGLO)
C
C**** PRINTOUT CONVERGENCE INFORMATION
C
      IF(IPRCOS.GT.0) THEN       ! no print of initial conditions
       WRITE(LURESS,897) ITIMES,ISTEPS,IITERS,NCHEKS,NCKGLO
C
C**** PRINTOUT GENERAL TITLE
C
       IF(KPRI0S.EQ.0.AND.KFEMVS.EQ.1.AND.NCHEKS.EQ.0.AND.NCKGLO.EQ.0) 
     .                          WRITE(LURESS,885) ITIMES,ISTEPS,IITERS
       IF(KPRI0S.EQ.1.AND.NCHEKS.EQ.0.AND.NCKGLO.EQ.0)
     .                          WRITE(LURESS,890) ITIMES,ISTEPS,IITERS
       IF(KPRI0S.EQ.1.AND.NCHEKS.NE.0)
     .                          WRITE(LURESS,895) ITIMES,ISTEPS,IITERS
C
C**** TO DEAL WITH COUPLED PROBLEMS
C
       IF(KPRI0S.EQ.1.AND.NCHEKS.EQ.0.AND.NCKGLO.EQ.1)
     .                          WRITE(LURESS,896) ITIMES,ISTEPS,IITERS
      ENDIF
C
C**** PERFORMS SMOOTHING OVER GAUSSIAN HEAT FLUXES IF NECESSARY
C
      IF(KSGAUS.NE.0) THEN
       IF((KPRI5S+KPRI7S+KPRI8S).GT.0.OR.(KFEMVS.EQ.1)) THEN
        CALL OUTSMOS(ELDATS,ELPRES,ELVARS,ELMATS,LNODSS,MATNOS,PROELS,
     .               PROPSS,COORDS,PREASS,FPCHAS,DISTOS(1,1),
     .               DISTOS(1,2),DISPLS,
     .               WORK1S(IGSMOS(1)),WORK1S(IGSMOS(2)),
     .               WORK1S(IGSMOS(3)),
     .               WORK1S(IGSMOS(8)),WORK1S(IGSMOS(22)),  !smstp,smspp
     .               WORK1S)
       ENDIF
      ENDIF
C
C**** PRINTOUT RESULTS TO RESULTS FILES
C
      IF(KPRI0S.EQ.1.OR.KFEMVS.EQ.1)
     . CALL OUTPRIS(DISTOS(1,1),ELDATS,ELPRES,ELVARS,ELMATS,HEADSS,
     .              LNODSS,MATNOS,PROELS,PROPSS,
     .              WORK1S(IGSMOS(1)),WORK1S(IGSMOS( 2)),
     .              WORK1S(IGSMOS(8)),WORK1S(IGSMOS(22)),
     .              COORDS,FPCHAS,DISPLS,WORK1S)
C
      CALL CPUTIMS(TIME2S)
      CPUOUS=CPUOUS+(TIME2S-TIME1S)
C
      RETURN
  885 FORMAT(1H1,///,
     .10X,'* * *  CONVERGED RESULTS STORED ONLY ON FILES * * *',//,
     .        5X,'ITIME =',I5,   5X,'ISTEP =',I5,   5X,'IITER =',I5)
  890 FORMAT(1H1,///,
     .10X,'* * * CONVERGED RESULTS * * *',//,
     .        5X,'ITIME =',I5,   5X,'ISTEP =',I5,   5X,'IITER =',I5)
  895 FORMAT(//,
     .10X,'* * * NOT-CONVERGED RESULTS * * *',//,
     .        5X,'ITIME =',I5,   5X,'ISTEP =',I5,   5X,'IITER =',I5)
C
  896 FORMAT(//,
     .10X,'* * * CONVERGED RESULTS - NOT-CONVERGED GLOBAL 
     .           RESULTS * * *',//,
     .        5X,'ITIME =',I5,   5X,'ISTEP =',I5,   5X,'IITER =',I5)
C
  897 FORMAT(//,
     .'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *',
     .' * * * * * * *',//,
     .10X,'* * * CONVERGENCE INFORMATION * * *',//,
     .        3X,'ITIME =',I5,   3X,'ISTEP =',I5,   3X,'IITER =',I5,
     .        3X,'NCHEK =',I5,   3X,'NCKGLO =',I5,//,
     .'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *',
     .' * * * * * * *')
C
      END
